/* -=- Properties -=-
 * Code that handles the Processing of class properties, 
 *
 * 

*/

#library "A_Properties"
#include "zcommon.acs"
#include "8bdmlib.acs"
#import "DB_Properties.acs"
#import "A_Core.acs"
#import "db_Loadout.acs"
#import "A_Loadout.acs"
#import "A_Pain.acs"

Function int MasteryCheck (int ThisWepType)
{
if(!ThisWepType){return 0;}
//First we will gather all the masteries we have and combine them as a bitflag
int MasteryFlag;

if(CheckProp(MasterProPList[0][PROP_NAME])){MasteryFlag = MasteryFlag| WF_BUSTER;}
if(CheckProp(MasterProPList[1][PROP_NAME])){MasteryFlag = MasteryFlag| WF_MACHINEGUN;}
if(CheckProp(MasterProPList[2][PROP_NAME])){MasteryFlag = MasteryFlag| WF_SPRAYER;}
if(CheckProp(MasterProPList[3][PROP_NAME])){MasteryFlag = MasteryFlag| WF_LAUNCHER;}
if(CheckProp(MasterProPList[4][PROP_NAME])){MasteryFlag = MasteryFlag| WF_MELEE;}
//log(f:ThisWepType,s:" : ",f:MasteryFlag);
if(ThisWepType & MasteryFlag){return 1;}
return 0;
}



Function bool CheckProp(str PropName)
{
if(CheckInventory(StrParam(s:"Pro_",s:PropName)))
    {
    //log(s:"got ",s:StrParam(s:"Pro_",s:PropName));
    Return true;
    }

return false;
}

Function bool CheckPlayerProp(int P, str PropName)
{
if(CheckActorInventory(P+1000,StrParam(s:"Pro_",s:PropName)))
    {
    //log(s:"got ",s:StrParam(s:"Pro_",s:PropName));
    Return true;
    }

return false;
}

Function bool CheckPropRange(int A, int B)
{
For(int h = A; h <= B; h++)
	{
    if(CheckProp(MasterProPList[h][PROP_NAME]))
        {
        Return true;
        }
    }
return false;
}

Function bool CheckMasteryDamage(str PainType)
{
//First Check for the flags
if(!CheckPropRange(0,4)){Return 0;}

int WepFlag;
For(int i = 0; i < WEAPONLIST; i++)
	{
	If(StrCmp(PainType,MasterWepList[i][4])==0 || StrCmp(PainType,MasterWepList[i][5])==0)
		{
        //found our weapon paintype, now see if the player has a mastery for this weapon.
        WepFlag = MasterWepList[i][3];      
		}
	}

Return MasteryCheck(WepFlag);
}

Function bool CheckForElement (str PainType, int ElementFlag)
{
//Check to see if the given paintype is of a specific element
For(int i = 0; i < PAINCOUNT; i++)
	{
	If(PainRecord[i][0] == PainType)
		{
        //found our weapon paintype
        if(ElementFlag & PainRecord[i][1]) Return 1;      
		}
	}
Return 0;
}

Function int GetElement (str PainType)
{
//Check to see if the given paintype is of a specific element
int elmFlag;
For(int i = 0; i < PAINCOUNT; i++)
	{
	If(PainRecord[i][0] == PainType)
		{
        //found our weapon paintype, clear out non elemental flags first
        //

         elmFlag = PainRecord[i][1];

         if(elmFlag & ELM_EXPLOSION){elmFlag = elmFlag & ~ELM_EXPLOSION;}
         if(elmFlag & ELM_DONTSTOP){elmFlag = elmFlag & ~ELM_DONTSTOP;}
         Return elmFlag;   
		}
	}
Return 0;
}

Function Bool CheckForResistance(str PainType)
{
//First Check for the flags
if(!CheckPropRange(5,12)){Return 0;}

int ElementFlag;
For(int i = 0; i < PAINCOUNT; i++)
	{
	If(PainRecord[i][0] == PainType)
		{
        //found our weapon paintype
        ElementFlag = PainRecord[i][1];      
		}
	}
if(!ElementFlag){Return 0;}

//now see if the player has Resistance To this Element

Switch(ElementFlag)
    {
    Case ELM_FIRE: return CheckProp(MasterProPList[5][PROP_NAME]); break;
    Case ELM_ICE: return CheckProp(MasterProPList[6][PROP_NAME]); break;
    Case ELM_AQUA: return CheckProp(MasterProPList[7][PROP_NAME]); break;
    Case ELM_TOXIC: return CheckProp(MasterProPList[8][PROP_NAME]); break;
    Case ELM_ELEC: return CheckProp(MasterProPList[9][PROP_NAME]); break;
    Case ELM_WIND: return CheckProp(MasterProPList[10][PROP_NAME]); break;
    Case ELM_BREAK: return CheckProp(MasterProPList[11][PROP_NAME]); break;
    Case ELM_EUCLID: return CheckProp(MasterProPList[12][PROP_NAME]); break;
    }

return 0;
}



Function Bool CheckForBladeParry(str PainType)
{
if(!CheckProp(MasterProPList[13][PROP_NAME])){Return 0;}
//First, check to see if we are carrying a melee or blade weapon
int ThisWeapon = STD_WhichWeapon();
If(!(MasterWepList[ThisWeapon][3] & WF_MELEE) && !(MasterWepList[ThisWeapon][3] & WF_BLADE)){Return 0;}

//Now check if it is a tossshield, if so, then we need to confirm it is inhand
If(MasterWepList[ThisWeapon][3] & WF_TOSSSHIELD)
    {
    if(CheckInventory(MasterWepList[ThisWeapon][2])){Return 0;}
    }

//then Check for weapon pain
For(int i = 0; i < WEAPONLIST; i++)
	{
    If(MasterWepList[i][4] == PainType || MasterWepList[i][5] == PainType)
		{
        If(MasterWepList[i][3] & WF_MELEE || MasterWepList[i][3] & WF_BLADE)
            {
            //This is melee/Break damage, don't reduce it.
            Return 0;   
            }
 
		}
	}

//Next check for anything blade elemental
int ElementFlag;
For(int j = 0; j < PAINCOUNT; j++)
	{
	If(PainRecord[j][0] == PainType)
		{
        //found our weapon paintype
        ElementFlag = PainRecord[j][1];      
		}
	}
if(ElementFlag & ELM_BLADE || ElementFlag & ELM_BREAK){Return 0;}

return 1;
}


Script "MSTD_PropertySetup" ENTER
{

//Initiates all the passive scripts this class needs
Delay(1);
//if(CheckProp(MasterProPList[14][PROP_NAME])){ACS_NamedExecuteAlways("Passive_Speedrunner",0,0);}
//if(CheckProp(MasterProPList[15][PROP_NAME])){ACS_NamedExecuteAlways("Passive_Acrobat",0,0);}
if(CheckProp(MasterProPList[15][PROP_NAME])){PlayerJumpZ[PlayerNumber()]+=5.0; SetActorProperty(0,APROP_JUMPZ,PlayerJumpZ[PlayerNumber()]);}
if(CheckProp(MasterProPList[20][PROP_NAME])){ACS_NamedExecuteAlways("Passive_Undershirt",0,0);}
ACS_NamedExecuteAlways("RepairWepRecovery",0,0);//if(CheckProp(MasterProPList[22][PROP_NAME])){ACS_NamedExecuteAlways("Passive_TeamMedic",0,0);}
if(CheckProp(MasterProPList[23][PROP_NAME])){ACS_NamedExecuteAlways("Passive_Subtank",0,0);}
if(CheckProp(MasterProPList[29][PROP_NAME])){ACS_NamedExecuteAlways("Passive_EnergyBalancer",0,0);}
}

Script "Passive_Undershirt" (void)
{
int HPMax = GetActorProperty(0,APROP_SPAWNHEALTH);
int HPNow = GetActorProperty(0,APROP_HEALTH);
/*
The undershirfFlag is taken by damage events, when the player doesn't have it,
this script waits untill they get back to full HP to restore the undershirfFlag
*/
GiveInventory("UndershirtFlag",1);
GiveInventory("UnderShirtFX_On",1);
while(GetActorProperty(0,APROP_HEALTH) > 0)
    {
    
    if(!CheckInventory("UndershirtFlag"))
        {
        HPNow = GetActorProperty(0,APROP_HEALTH);
        if(HPNow >= HPMax){GiveInventory("UndershirtFlag",1);GiveInventory("UnderShirtFX_On",1);localAmbientSound("item/Refill",127);}
        }
    delay(17);
    }
}

Script "Passive_TeamMedic" (void)
{
/*
So long as the player is equipped with a medi weapon, they will auto regen HP after avoiding
damage for 1 second
*/
if(!CheckForTeamMedic()){Terminate;}

int HPMax = GetActorProperty(0,APROP_SPAWNHEALTH);
while(GetActorProperty(0,APROP_HEALTH) > 0)
    {
    //log(i:GetActorPowerupTics(0,"HealRampPower"));
    if(GetActorPowerupTics(0,"HealRampPower")<490 && GetActorProperty(0,APROP_HEALTH) < GetActorProperty(0,APROP_SPAWNHEALTH))
        {
        if(CheckInventory("PoisonMark")){GiveInventory("DetoxLevel",5);GiveInventory("DetoxEfectSpawn",1);}
        else{HealThing(2,HPMax);}
        }
    delay(5);
    }
}

Script "RepairWepRecovery" (void)
{
/*
So long as the player is equipped with a Repair weapon, they will auto regen HP after avoiding
damage for 1 second
*/
if(!CheckForRepairWep()){Terminate;}
Bool TeamMedic = CheckProp(MasterProPList[22][PROP_NAME]);
int HPMax = GetActorProperty(0,APROP_SPAWNHEALTH);
int RecovStart = 403;
int RecovRate = GetHealthPercent(-1,1,1);
if(TeamMedic){RecovStart = 490; RecovRate = GetHealthPercent(-1,1,2);}
while(GetActorProperty(0,APROP_HEALTH) > 0)
    {
    //log(i:GetActorPowerupTics(0,"HealRampPower"));
    if(GetActorPowerupTics(0,"HealRampPower")<RecovStart && GetActorProperty(0,APROP_HEALTH) < GetActorProperty(0,APROP_SPAWNHEALTH))
        {
        if(CheckInventory("PoisonMark")){GiveInventory("DetoxLevel",5);GiveInventory("DetoxEfectSpawn",1);}
        else{HealThing(RecovRate,HPMax);}
        }
    delay(7);
    }
}

Script "Passive_EnergyBalancer" (void)
{
/*
First Grab the player's weapons, this is called on spawn so we can just grab their uservariables since they
shouldn't get changet in that time 
*/
delay(5);
int pln = playernumber();
int ThisWeapon;
int WepA_Num = GetUserCvar(pln,"Lo_WepA");
int WepB_Num = GetUserCvar(pln,"Lo_WepB");
int PastWep;

str WepA = MasterWepList[WepA_Num][1];
str WepB = MasterWepList[WepB_Num][1];

Str WepAmmo;
int WepAmmoCount;

int RefillTimer;
/*
Every 5 seconds, we will give the player 50% ammo on whatever weapon they aren't wielding at the moment.
*/

while(GetActorProperty(0,APROP_HEALTH) > 0)
    {

    RefillTimer += 17;
    if(ThisWeapon != STD_WhichWeapon())
        {
        ThisWeapon = STD_WhichWeapon();
        RefillTimer = 0;
        //log(i:WepA_Num,s:" : ",i:WepB_Num);
        }
    
    If(RefillTimer >= 85)
        {
            if(ThisWeapon == WepA_Num)
            {
            WepAmmo = MasterWepList[WepB_Num][2];
            }
        else if (ThisWeapon == WepB_Num)
                {
                WepAmmo = MasterWepList[WepA_Num][2];
                }
        else
            {
            //you are on an ability, so just replenish the previously replenished weapon.
            WepAmmo = MasterWepList[PastWep][2];
            }

            WepAmmoCount = GetAmmoCapacity(WepAmmo);
            //log(s:WepAmmo,s:" : ",i:WepAmmoCount);
            if(CheckInventory(WepAmmo) < WepAmmoCount)
                {
                GiveInventory(WepAmmo, (WepAmmoCount*0.5)>>16);
                PastWep = ThisWeapon;
                localAmbientSound("item/Refill",127);
                }
            RefillTimer = 0;
        }
    
    delay(17);
    }

}

Script "Passive_Subtank" (void)
{
if(GetCvar("BuckShot")==1)
    {
    ACS_NamedExecuteAlways("MSTD_Basic(Over)Heal",0,0,ACS_NamedExecuteWithResult("MSTD_HealthPercent",0,1,50),50);
    GiveInventory("SubTankDelay",1);
    }

}

//#region -=- depreciated -=-
Script "Passive_Speedrunner" (Void)
{
/*
Checks the user's HP every second and gives them the speed boost if they are above
Half health and don't already have it. the passive is lost upon taking damage.
*/
while(GetActorProperty(0,APROP_HEALTH) > 0)
    {
    //log(i:GetHealthPercent(0,0,0) );
    if(GetHealthPercent(0,0,0) > 50 && !CheckInventory("PowerSpeedRunner")){GiveInventory("PowerSpeedRunner",1); LocalAmbientSound("support/Statup",127);}
    delay(35);
    }

}

Function Bool CheckSpeedrunnerLoss(void)
{
if(GetHealthPercent(0,0,0) <= 50 && CheckInventory("PowerSpeedRunner")){Return 1;}
Return 0;
}


Script "Passive_Acrobat" (Void)
{
/*
So long as we have movespeed of 1.0 or more, or we have a speedbuff, this passive will be in effect
*/


while(GetActorProperty(0,APROP_HEALTH) > 0)
    {
    TriggerAcrobatCheck();
    //log(f:PlayerJumpZ[PlayerNumber()]);
    delay(25);
    }
}

Function void TriggerAcrobatCheck (void)
{
int HighJump = 15.0;
int NoHighJump = 10.0;
    if(CheckProp(MasterProPList[15][PROP_NAME]) && !CheckInventory("CannotJump"))
    {
       if(CheckAcrobatCriteria()) 
        {
        if(GetActorProperty(0,APROP_JUMPZ) != HighJump){PlayerJumpZ[PlayerNumber()] = HighJump; SetActorProperty(0,APROP_JUMPZ,PlayerJumpZ[PlayerNumber()]);}
        }
    else
        {
        if(GetActorProperty(0,APROP_JUMPZ) != NoHighJump){PlayerJumpZ[PlayerNumber()] = NoHighJump; SetActorProperty(0,APROP_JUMPZ,PlayerJumpZ[PlayerNumber()]);}      
        } 
    }
}

Function Bool CheckAcrobatCriteria(void)
{

//Dequalifiers
if(CheckInventory("FrostBiteMark")){Return 0;}

if(GetActorProperty(0,APROP_SPEED) >= 1.0 
|| CheckInventory("PowerSpeedRunner")
){Return 1;}
Return 0;
}
//#endregion

Function Bool CheckForCrit(str PainType)
{
if(!CheckProp(MasterProPList[19][PROP_NAME])){Return 0;}
//Checks if the damage would be critical damage
int CritFlag;
For(int j = 0; j < CRITCOUNT; j++)
	{
	If(CritRecord[j][0] == PainType)
		{
        //found our weapon paintype
        CritFlag = CritRecord[j][1];      
		}
	}
if(CritFlag & CRIT_ALWAYS){Return 1;}

Return 0;
}



Function Bool CheckForTeamMedic(Void)
{
if(!CheckProp(MasterProPList[22][PROP_NAME])){Return 0;}
//Confirm if we are equipped with a medical weapon
str wepnamecheck;
For(int i = 0; i < WEAPONLIST; i++)
	{
    wepnamecheck = MasterWepList[i][1];
    If(CheckInventory(wepnamecheck) || CheckInventory(StrParam(s:wepnamecheck,i:2)))
		{
        If(MasterWepList[i][3] & WF_MEDICAL)
            {
            //You have a medical weapon
            Return 1;   
            }
 
		}
	}
Return 0;
}

Function Bool CheckForRepairWep(Void)
{
//Confirm if we are equipped with a medical weapon
str wepnamecheck;
For(int i = 0; i < WEAPONLIST; i++)
	{
    wepnamecheck = MasterWepList[i][1];
    If(CheckInventory(wepnamecheck) || CheckInventory(StrParam(s:wepnamecheck,i:2)))
		{
        If(MasterWepList[i][3] & WF_MEDICAL)
            {
            //You have a medical weapon
            Return 1;   
            }
 
		}
	}
Return 0;
}

Function bool CheckForElementalGuard (str PainType)
{
if(!CheckProp(MasterProPList[21][PROP_NAME])){Return 0;}
//Check to see if the damage is any of the elements this property
//Suppresses.
For(int i = 0; i < PAINCOUNT; i++)
	{
	If(PainRecord[i][0] == PainType)
		{
        //found our weapon paintype, now compare elements
        if(PainRecord[i][1] & ELM_FIRE) Return 1;   
        if(PainRecord[i][1] & ELM_ICE) Return 1; 
        if(PainRecord[i][1] & ELM_AQUA) Return 1; 
        if(PainRecord[i][1] & ELM_ELEC) Return 1;    
		}
	}
Return 0;
}

Function bool CheckForGutsy (void)
{
if(!CheckProp(MasterProPList[24][PROP_NAME])){Return 0;}
/*
Check if you are under a major status, if so, then return positive.
*/
if(CheckInventory("PoisonMark")
||CheckInventory("BurnMark")
||CheckInventory("FrostbiteMark")){Return 1;}
Return 0;
}

Function bool CheckForDetermined (str PainType)
{
if(!CheckProp(MasterProPList[25][PROP_NAME])){Return 0;}
/*
Check if you are below full health, Determined kicks in at that point

also confirm if we are doing weapon damage.
*/
if(!CheckForWeaponDamage(PainType)){Return 0;}


int HPPer = GetHealthPercent(-1,0,0);
if(HPPer<100){Return 1;}
Return 0;
}

Function Bool CheckForWeaponDamage(str PainType)
{
//Confirm if the painstate can only come from a weapon.

//start with checking if it matches the currently wielded weapon
int ThisWeapon = STD_WhichWeapon();
If(MasterWepList[ThisWeapon][4] == PainType || MasterWepList[ThisWeapon][5] == PainType){Return 1;}

//No? the check all weapons
For(int i = 0; i < WEAPONLIST; i++)
	{
    If(MasterWepList[i][4] == PainType || MasterWepList[i][5] == PainType){Return 1;}
	}
return 0;
}

#LIBDEFINE MARKSMANSTART 640
#LIBDEFINE MARKSMANEND 960
Function bool CheckForMarksman (str PainType, int TargTID)
{
if(!CheckProp(MasterProPList[26][PROP_NAME])){Return 0;}
if(!CheckForWeaponDamage(PainType)){Return 0;}
/*
Check if the distance between the shooter and their target is great enough
for this property to work.
*/
if(xyzDistance(0, TargTID) > MARKSMANSTART){Return 1;}
Return 0;
}

Function bool CheckForHeater (str PainType)
{
if(!CheckProp(MasterProPList[27][PROP_NAME])){Return 0;}
/*
if this damage is wind or water based, return true.
*/
Return CheckForElement(PainType,ELM_WIND|ELM_AQUA);
}

Function bool CheckForRefrigerate (str PainType)
{
if(!CheckProp(MasterProPList[28][PROP_NAME])){Return 0;}
/*
if this damage is wind or water based, return true.
*/
Return CheckForElement(PainType,ELM_WIND|ELM_AQUA);
}

Function int CheckForSpecialist(void)
{
if(!CheckProp(MasterProPList[31][PROP_NAME])){Return -1;}
/*
Check if the given weapon is in the first slot, simply put, it's a weapon without "2" at the end
*/
int ThisWeapon = STD_WhichWeapon();
//log(s:GetWeapon(),s:" : ",s:MasterWepList[ThisWeapon][1]);
if(ThisWeapon < 0){Return -1;}
If(StrCmp(GetWeapon(),MasterWepList[ThisWeapon][1])==0){Return 1;}

return 0;
}

Function bool CheckForRecharge (str PainType)
{
if(!CheckProp(MasterProPList[32][PROP_NAME])){Return 0;}
/*
if this damage is Elec based, return true.
*/
Return CheckForElement(PainType,ELM_ELEC);
}

//================

function bool MSTD_ShowPropertyFXClient(void)
{
	return (getUserCvar(ConsolePlayerNumber(),"MSTD_ShowPropertyFX")>0);
}

function bool MSTD_ShowOverHealFXClient(void)
{
	return (getUserCvar(ConsolePlayerNumber(),"MSTD_ShowOverHealFX")>0);
}



Script "BladeParryWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "InBladeParry") > 0
    && MSTD_ShowPropertyFXClient()
	){
        SynchWarperRender(Player);
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Function void IsBladeParryActive(void)
{

Bool BladeParry;
if(CheckProp(MasterProPList[13][PROP_NAME]) && MSTD_ShowPropertyFXClient())
        {
        int ThisWeapon = STD_WhichWeapon();
        If((MasterWepList[ThisWeapon][3] & WF_MELEE) || (MasterWepList[ThisWeapon][3] & WF_BLADE)){BladeParry = 1;}
        }

if(BladeParry)
    {GiveInventory("BladeParryFX_On",1);}
else
    {GiveInventory("BladeParryFX_Off",1);}
}


Script "UnderShirtWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "UndershirtFlag") > 0
    && MSTD_ShowPropertyFXClient()
	){
        SynchWarperRender(Player);
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}



Script "overhealFXWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
		while(GetActorProperty(Player, APROP_HEALTH)>0
	&& CheckActorInventory(Player, "OverhealFlag") > 0
    && MSTD_ShowOverHealFXClient()
        )
	{
        SynchWarperRender(Player);
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "GutsyRageFXWarper" (void) CLIENTSIDE
{
	int Player = ACS_NamedExecuteWithResult("core_gettarget",0);
	
	// Loop if shield is active
	while(GetActorProperty(Player, APROP_HEALTH)>0
	    &&(
        CheckActorInventory(Player, "FrostbiteMark") > 0
        ||CheckActorInventory(Player, "BurnMark") > 0
        ||CheckActorInventory(Player, "PoisonMark") > 0
        ||CheckActorInventory(Player, "StunMark") > 0
        )
        && MSTD_ShowPropertyFXClient()
	){
        SynchWarperRender(Player);
		Delay(1);
	}
	
    // Remove
    SetActorState(0,"Death");
    Delay(17);
	Thing_Remove(0);
}

Script "ClassPassive_EXEChipSystem" (void)
{
/*
We will start by getting the element flag for our weapons
*/
int WepA_Num = GetUserCvar(PlayerNumber(),"Lo_WepA");
int WepB_Num = GetUserCvar(PlayerNumber(),"Lo_WepB");

str WepAPain = MasterWepList[WepA_Num][4];
str WepBPain = MasterWepList[WepB_Num][4];

int MinibombChip;
int RockCubeChip;

Switch(GetElement(WepAPain))
    {
    CASE ELM_FIRE: MinibombChip = 1; break;
    CASE ELM_ICE: MinibombChip = 2; break;
    CASE ELM_AQUA: MinibombChip = 3; break;
    CASE ELM_TOXIC: MinibombChip = 4; break;
    CASE ELM_ELEC: MinibombChip = 5; break;
    CASE ELM_WIND: MinibombChip = 6; break;
    CASE ELM_BREAK:	MinibombChip = 7; break;
    CASE ELM_EUCLID: MinibombChip = 8; break;
    CASE ELM_BLADE: MinibombChip = 9; break;
    }
GiveInventory("MiniBombChipFlag",MinibombChip);

Switch(GetElement(WepBPain))
    {
    CASE ELM_FIRE: RockCubeChip = 1; break;
    CASE ELM_ICE: RockCubeChip = 2; break;
    CASE ELM_AQUA: RockCubeChip = 3; break;
    CASE ELM_TOXIC: RockCubeChip = 4; break;
    CASE ELM_ELEC: RockCubeChip = 5; break;
    CASE ELM_WIND: RockCubeChip = 6; break;
    CASE ELM_BREAK:	RockCubeChip = 7; break;
    CASE ELM_EUCLID: RockCubeChip = 8; break;
    CASE ELM_BLADE: RockCubeChip = 9; break;
    }
GiveInventory("RockCubeChipFlag",RockCubeChip);

}