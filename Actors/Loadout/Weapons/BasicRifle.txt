
actor MegaRifleWep : WepMasterBase
{
Tag "MegaRifle"
Weapon.AmmoUse 1
Weapon.AmmoGive 35

Obituary "$OB_MegaRifle"
inventory.icon "BLASI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "MegaRifleClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop
Ready:
Ready.Wep:
PIDB A 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" A 0 A_Jumpifinventory("MegaRifleClip",1,"Ready.Core")
Goto Reload
//===============
Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop
Fire:
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/HardBuster3","Weapon")
"####" A 0 A_FireCustomMissile("MegaRifleShot",fRandom(-(BST_SPD),(BST_SPD)),1,4,4,0,fRandom(-(BST_SPD),(BST_SPD)))
"####" BC 1 
"####" A 2 
"####" A 0 A_WeaponReady(WRF_NOPRIMARY)
"####" C 0 A_REfire
"####" A 0 A_Takeinventory("WepBloomItem",999)
goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_Jumpifinventory("MegaRifleClip",1,"Ready")
Reload:

"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 32 
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_Giveinventory("MegaRifleClip",35)
"####" A 1 A_Refire("Fire")
Goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor MegaRifleWep2 : MegaRifleWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor MegaRifleClip : Ammo
{
inventory.amount 1
inventory.maxamount 35
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor MegaRifleShot : fastProjectileBase
{
Obituary "$OB_MegaRifle"
DamageType "SingleShot2"
Speed 80
Damage (10)
radius 3

MissileHeight 16
height 3
scale 1.0
Var Int User_TrailCount;
Var Int User_TP; //TrailPosition

reactiontime 10
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
TNT1 A 0 A_SpawnItemEx("MegarifleTrail",0,0,0,momx,momy,momz,0,9)
Spawn3:
TNT1 A 0 A_CountDown
TNT1 A 1 
Goto Spawn2
Death:
TNT1 A 1 A_Spawnitemex("BlasterPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}


actor MegarifleTrail : BasicExplosion
{
Obituary "$OB_MegaBuster"
damagetype "MegaBuster"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("MegarifleTrailX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("MegarifleTrailX",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("MegarifleTrailX",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("MegarifleTrailX",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("MegarifleTrailX",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 1 
stop
}
}

actor MegarifleTrailX : FXActor_B
{
scale 1.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
BSTG A 1
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor MegarifleTrailX1 : FXActor_A
{
scale 1.5
Alpha 0.9
States
{
Spawn:
BUST A 0
BUST A 1
stop
}
}