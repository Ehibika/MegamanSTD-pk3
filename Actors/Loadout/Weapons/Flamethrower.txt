

actor MegaFlamethrowerWep : WepMasterBase
{
Tag "MegaFlameThrower"
Weapon.AmmoUse 1
Weapon.AmmoGive 50
scale 2.0
Obituary "$OB_Burner"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "MegaFlameThrowerClip"
inventory.icon "FTONI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
FWPR B 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" B 0 A_Jumpifinventory("MegaFlameThrowerClip",1,"Ready.Core")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
hold:
"####" L 0 A_PlaySoundEx("weapon/waveburner","Weapon")
"####" B 0 A_FireCustomMissile("Heatspray1",0,1,4,0)
"####" C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
"####" B 0 A_Refire
"####" B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
goto Ready.Wep

NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("MegaFlameThrowerClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 42 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("MegaFlameThrowerClip",50)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor MegaFlamethrowerWep2 : MegaFlamethrowerWep
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor MegaFlameThrowerClip : Ammo
{
inventory.amount 1
inventory.maxamount 70
+INVENTORY.IGNORESKILL
}

Actor BurnerhitDelay : powerprotection
{
Powerup.duration 3
Damagefactor "Burner", 0
Damagefactor "BurnerRED", 0
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor Heatspray1 : FastProjectile
{

PROJECTILE
+NOINTERACTION
+BLOODSPLATTER
+THRUGHOST
+DONTBLAST
+DONTREFLECT
+MTHRUSPECIES
+FORCEPAIN
Radius 10
Height 12
damage (7)
damagetype "Burner"
RenderStyle "Add"
Obituary "$OB_MegaFlameThrower"
speed 74
scale 2.0
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 AA 0 A_SpawnItemEx("HeatSprayFx",random(-15,15),random(-15,15),random(-15,15),momx/4,momy/4,momz/4,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION) 
TNT1 A 0 A_SpawnItemEx("Heatspray2",0,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Stop
TNT1 A 1 A_Changeflag("NOINTERACTION",1)
stop
XDeath:
Crash:
TNT1 A 1 
stop
Death:
stop
}
}

actor Heatspray2 : Heatspray1
{
Radius 16
Height 12
damage (6)
speed 176
States
{
Spawn:
TNT1 C 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("Heatspray3",0,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION) 
TNT1 C 0 A_Stop
TNT1 C 1 A_Changeflag("NOINTERACTION",1)
stop
XDeath:
Crash:
TNT1 A 1 
stop
Death:
stop
}
}

actor Heatspray3 : Heatspray1
{
Radius 28
Height 12
damage (5)
speed 285
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_Stop
TNT1 A 1 A_Changeflag("NOINTERACTION",1)
stop
XDeath:
Crash:
TNT1 A 1 
stop
Death:
stop
}
}


//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor HeatSprayFx  //flames 
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
height 2
Radius 2
damage (0)
reactiontime 1
scale 2
RenderStyle "ADD"
Alpha 0.2
States
{
Spawn:
BRNF A 0 Thrustthingz(0,30,0,0)
BRNF ABCDEFGH 1 A_Fadein(0.1)
BRNF C 0 
stop
}
}
