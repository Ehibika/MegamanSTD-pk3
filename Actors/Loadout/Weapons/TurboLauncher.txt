actor TurboLauncherWep : WepMasterBase
{

Tag "TurboLauncher"
Weapon.AmmoUse 1
Weapon.AmmoGive 4
Obituary "$OB_TURBOLAUNCHER"
inventory.icon "TAGMI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "TurboLauncher_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
DW36 A 1 A_WeaponReady(WRF_ALLOWINPUTS)
"####" A 0 A_Jumpifinventory("TurboLauncher_Ammo",1,"Ready.Core")
Goto Reload
//===============

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
DW36 A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Rifle_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
DW36 A 1 A_Raise
Loop

Fire:
DW36 B 0 A_JumpIfNoAmmo("NoAmmo")
DW36 A 0 A_Jumpifinventory("ConShotTimer",1,"Fire.Buffer")
DW36 A 0 A_PlaySoundEx("weapon/AutoFirework","Weapon")
DW36 B 0 A_FireCustomMissile("TurboLauncherShot",0,2,0,2)

DW36 B 2 Offset(4,28)
DW36 C 2 Offset(8,24)
DW36 B 2 Offset(4,28)
DW36 A 0 A_Giveinventory("TurboLauncherCST",1)
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

Fire.Buffer:
"####" A 1 A_WeaponReady(WRF_NOPRIMARY|WRF_ALLOWRELOAD)
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_Jumpifinventory("TurboLauncher_Ammo",1,"Ready")
Goto Reload

Reload.Hold:
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_JumpIfNoAmmo("Reload.FirstLoop")
Goto Reload.loop

Reload.FirstLoop:
"####" A 32 A_WeaponReady(WRF_RELOADING) 
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_Giveinventory("WeaponSingleReloader",1)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
Reload.Loop:
"####" A 18 A_WeaponReady(WRF_CYCLERELOADING) 
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_Giveinventory("WeaponSingleReloader",1)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
goto Reload.Rise2

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor TurboLauncherWep2 : TurboLauncherWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor TurboLauncher_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 4
Ammo.BackpackMaxAmount 6
}

actor TurboLauncherCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 14
powerup.type "ConShotTimer"
}

Actor TurboLauncherProtect : powerprotection
{
Powerup.duration 3
Damagefactor "TurboLauncher", 0
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const int TurboLauncher_DMG = 60;
Const int TurboLauncher_RAD = 98;
Const int TurboLauncher_KBRAD = 128;
Const int TurboLauncher_RADMin = 32;
actor TurboLauncherShot : FastProjectileBase
{
Speed 80
Damage (0)
//+RIPPER
Obituary "$OB_TURBOLAUNCHER"
damagetype "TurboLauncher"
radius 8
Reactiontime 25
height 8
scale 4.5
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
DIVE BB 1 A_SpawnItemEx("TurboLauncherShotTrail",-80,0,-momz,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM)
TNT1 A 0 A_Countdown
DIVE AA 1 A_SpawnItemEx("TurboLauncherShotTrail",-80,0,-momz,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM)
TNT1 A 0 A_Countdown
loop

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("TurboLauncherExplosion",0,0,0,0,0,0,0,1)
NAPA D 0 ACS_NamedExecuteWithResult("Save_Boom")
NAPA D 0 A_RadiusGive("TurboLauncher_Knockback",TurboLauncher_KBRAD,RGF_PLAYERS)
TNT1 A 0 A_Explode(TurboLauncher_DMG,TurboLauncher_RAD,1,0,TurboLauncher_RADMin)
stop
}
}

actor TurboLauncherExplosion : BasicExplosion //now just the FX due to desynch
{
scale 3.0
States
{
Spawn:
MINB A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDDEEF 1 
stop
}
}


actor TurboLauncher_Knockback : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIf(CallACS("Boom_IsMine"),"Pickup2")
TNT1 A 0 A_JumpIf(CallACS("Boom_IsEnemy"),"Pickup2")
stop
Pickup2:
TNT1 A 0 ACS_NamedExecuteWithResult("BLASTKNOCKBACK2",BlastThrust,BlastThrustRad)
stop
}
}

actor TurboLauncherShotTrail : BasicExplosion
{
Obituary "$OB_LAZERRIFLE"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
//TNT1 A 0 A_SpawnItemEx("LazerRifleTrailX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("TurboLauncherTrail_1",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("TurboLauncherTrail_1",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("TurboLauncherTrail_2",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("TurboLauncherTrail_2",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)
TNT1 A 0 A_SpawnItemEx("TurboLauncherTrail_3",0,0,0,-momx*1.2,-momy*1.2,-momz*1.2,0,9)
TNT1 A 0 A_SpawnItemEx("TurboLauncherTrail_3",0,0,0,-momx*1.4,-momy*1.4,-momz*1.4,0,9)
TNT1 A 1 
stop
}
}

actor TurboLauncherTrail_1 : FXActor_B
{
scale 2.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
HTST B 1
stop
}
}

actor TurboLauncherTrail_2 : TurboLauncherTrail_1
{
States
{
Spawn:
CRBM A 0
HTST C 1
stop
}
}

actor TurboLauncherTrail_3 : TurboLauncherTrail_1
{
States
{
Spawn:
CRBM A 0
HTST D 1
stop
}
}