actor DrillHeadCannonWep : WepMasterBase
{

Tag "DrillHeadCannon"
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Obituary "$OB_DRILLHEADCANNON"
inventory.icon "TAGMI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "DrillHeadCannon_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
DW17 A 1 A_WeaponReady(WRF_ALLOWINPUTS)
"####" A 0 A_Jumpifinventory("DrillHeadCannon_Ammo",1,"Ready.Core")
Goto Reload
//===============

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
DW17 A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Launcher_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
DW17 A 1 A_Raise
Loop

Fire:
DW17 B 0 A_JumpIfNoAmmo("NoAmmo")
DW17 A 0 A_Jumpifinventory("ConShotTimer",1,"Fire.Buffer")
DW17 A 0 A_PlaySoundEx("weapon/AutoFirework","Weapon")
DW17 B 0 A_FireCustomMissile("DrillHeadCannonShot",0,2,0,2)

DW17 B 2 Offset(4,28)
DW17 C 2 Offset(8,24)
DW17 B 2 Offset(4,28)
DW17 A 0 A_Giveinventory("DrillHeadCannonCST",1)
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

Fire.Buffer:
"####" A 1 A_WeaponReady(WRF_NOPRIMARY|WRF_ALLOWRELOAD)
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_Jumpifinventory("DrillHeadCannon_Ammo",1,"Ready")
Goto Reload

Reload.Hold:
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_JumpIfNoAmmo("Reload.FirstLoop")
Goto Reload.loop

Reload.FirstLoop:
"####" A 32 A_WeaponReady(WRF_RELOADING) 
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_Giveinventory("WeaponSingleReloader",1)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
Reload.Loop:
"####" A 28 A_WeaponReady(WRF_CYCLERELOADING) 
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_Giveinventory("WeaponSingleReloader",1)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
goto Reload.Rise2

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor DrillHeadCannonWep2 : DrillHeadCannonWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor DrillHeadCannon_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 8
Ammo.BackpackMaxAmount 12
}

actor DrillHeadCannonCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 20
powerup.type "ConShotTimer"
}

Actor DrillHeadProtect : powerprotection
{
Powerup.duration 3
Damagefactor "DrillHead", 0
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const int DrillHeadCannon_DMG = 80;
Const int DrillHeadCannon_RAD = 128;
Const int DrillHeadCannon_RADMin = 32;
actor DrillHeadCannonShot : FastProjectileBase
{
Speed 64
Damage (0)
//+RIPPER
Obituary "$OB_DRILLHEADCANNON"
damagetype "DrillHead"
radius 8
Reactiontime 25
height 8
scale 4.5
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
SPRE BB 1 A_SpawnItemEx("DrillHeadShotTrail",0,0,0,0,0,0,0,9)
TNT1 A 0 A_Countdown
SPRE CC 1 A_SpawnItemEx("DrillHeadShotTrail",0,0,0,0,0,0,0,9)
TNT1 A 0 A_Countdown
loop

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("DrillHeadExplosion",0,0,0,0,0,0,0,1)
//TNT1 A 0 A_SpawnItemEx("DrillHeadSource",0,0,-32,0,0,0,0,1)
NAPA D 0 ACS_NamedExecuteWithResult("Save_Boom")
NAPA D 0 A_RadiusGive("DrillHeadCannon_Knockback",DrillHeadCannon_RAD,RGF_PLAYERS)
TNT1 A 0 A_Explode(DrillHeadCannon_DMG,DrillHeadCannon_RAD,1,0,DrillHeadCannon_RADMin)
stop
}
}

actor DrillHeadExplosion : BasicExplosion //now just the FX due to desynch
{
scale 3.0
States
{
Spawn:
MINB A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDDEEF 1 
stop
}
}

actor DrillHeadSource : BasicExplosion //
{
States
{
Spawn:
MINB A 0 
MINB A 0 ACS_NamedExecuteAlways("BLASTKNOCKBACK",0,90,32,DrillHeadCannon_RAD)
TNT1 AA 1 
stop
}
}

actor DrillHeadCannon_Knockback : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIf(CallACS("Boom_IsMine"),"Pickup2")
TNT1 A 0 A_JumpIf(CallACS("Boom_IsEnemy"),"Pickup2")
stop
Pickup2:
TNT1 A 0 ACS_NamedExecuteWithResult("BLASTKNOCKBACK2",BlastThrust,BlastThrustRad)
stop
}
}

actor DrillHeadShotTrail : BasicClientside
{
scale 2.5
renderstyle "Translucent"
Gravity 0.5
ReactionTime 8
States
{
Spawn:
TNT1 A 0
TNT1 A 1
HARD CDE 1 
stop
}
}