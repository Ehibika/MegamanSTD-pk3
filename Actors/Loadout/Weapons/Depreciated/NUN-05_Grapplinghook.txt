/*=============================
we offer many ways to quickly get from point A to point B out 
in the field, but amongst our available options, non are
nearly as exilerating and as inexpensive as the neumodian
grappling hook!

latches onto any non energy based surface and pulls you in
at an instant! and if you're feeling especially daring, ride
the momentum off of it to get even further!

*///===========================

Actor GrapplingHookFlag : inventory {inventory.amount 1 inventory.maxamount 2}

actor GrapplingHook : Fastprojectile
{
PROJECTILE
Radius 5
Height 5
scale 3
+NOTARGETSWITCH
//+SKYEXPLODE
+LOOKALLAROUND
+NOINTERACTION
+MTHRUSPECIES
Reactiontime 53
speed 220
gravity 2.5
var int user_PTID;
wallbouncefactor 0.6
States
{
Spawn:
MBAL A 0
NAPA A 0 A_GiveToTarget("HookOutFlag",1)
WADP B 0 A_SetUserVar("user_PTID",ACS_ExecuteWithResult(257,0))
WADP B 0 Thing_ChangeTID(0,user_PTID+1700)//Don't forget to change the TID for the script as well.
WADP B 0 ACS_NamedExecuteAlways("core_drawhookshot", 0, tid, user_PTID, 18)
GRAP A 0 A_Changeflag("NOINTERACTION",0)
spawn2: 
GRAP A 0
WAD1 AB 3
GRAP A 0 A_jumpifcloser(1500,"Spawn2")
GRAP A 0 A_TakeFromTarget("HookOutFlag",9)
Stop
XDeath:
Crash:
GRAP A 0 
Goto Terminate

Death:
GRAP A 0
GRAP A 0 A_JumpIfTargetInLOS("Hook",0,0,0,0)
Goto Terminate
Hook:
GRAP A 0 A_Spawnitemex("GrapplingHookPlant",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
stop

Finish:
NAPA A 0 //A_GiveToTarget("GravityUp")
NAPA A 0 A_TakeFromTarget("HookOutFlag",9)
NAPA B 0 A_GiveToTarget("GrapplingHookCooldown")
NAPA A 0
stop

Terminate:
NAPA A 0 //A_GiveToTarget("GravityUp")
NAPA A 0 A_TakeFromTarget("HookOutFlag",9)
NAPA A 0
stop
}
}

	actor GrapplingHook_BLUE : GrapplingHook{translation "227:227=198:198"}
	actor GrapplingHook_RED : GrapplingHook{ }


actor GrapplingHookPlant
{
PROJECTILE
Radius 5
Height 5
scale 3
+CANBOUNCEWATER
+SKYEXPLODE
+LOOKALLAROUND
+NOINTERACTION
+THRUACTOrS
Reactiontime 53
var int user_PTID;
Var int User_Hooktimer;
speed 175
gravity 2.5
wallbouncefactor 0.6
States
{
Spawn:
NAPA A 0
NAPA A 0 A_Stop
WADP B 0 A_SetUserVar("user_PTID",ACS_ExecuteWithResult(257,0))
WADP B 0 Thing_ChangeTID(0,user_PTID+1700)//Don't forget to change the TID for the script as well.
WADP B 0 ACS_NamedExecuteAlways("core_drawhookshot", 0, user_PTID, 18)
WADP B 0 ACS_NamedExecuteAlways("Teamstyle_hookshot",0,user_PTID,3.5,0)
WADP B 0 ACS_NamedExecuteAlways("Hook_Detatcher",0)
Spawn2:
NAPA A 0 //A_GiveToTarget("GravityDown")
WAD1 A 1 //ACS_ExecuteAlways(GrapplingHookPull,0,80)
TNT1 A 0 A_PlaySoundEx("weapon/WireAdp", "weapon")
NAPA A 0 A_Jumpifcloser(100,"Finish")
NAPA A 0 //A_GiveInventory("Reeltimer",1)
NAPA A 0 A_Setuservar("User_Hooktimer",User_Hooktimer+1)
NAPA A 0 A_GiveToTarget("HookOutFlag")
NAPA A 0 A_Jumpif(User_Hooktimer >= 100,"Finish")
NAPA A 0 A_JumpIfTargetInLOS("Spawn2",0)
QuickFinish: //refund cooldown if ended early
NAPA B 0 A_GiveToTarget("GrapplingHookCooldown")
WADP B 0 A_TakeFromTarget("GrapplingHookCooldownItem",52)
Goto Finish+3
Finish:
NAPA A 0
NAPA B 0 A_GiveToTarget("GrapplingHookCooldown")
NAPA A 0
WADP B 0 A_PlaySoundEx("weapon/adapterreturn","Weapon")
NAPA A 0 //A_GiveToTarget("GravityUp")
NAPA A 0 //A_GiveToTarget("GrapHookBounce")
NAPA A 0 A_TakeFromTarget("HookOutFlag",9)
WADP B 0 A_TakeFromTarget("WireAdaptorDelay",1)
NAPA A 0
stop
}
}

actor HookOutFlag : powerup
{
inventory.amount 1
powerup.duration 17 
}

Actor GrapHookWire : WireAdaptorHookFX replaces WireAdaptorHookFX
{
Translation "227:227=231:231"
}
//=======

actor GrapplingHookCooler // Coolers are a type of watcher actor that handle weapon cooldowns
{
+MISSILE
+RIPPER
+NOGRAVITY
+NOINTERACTION
Reactiontime 5
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("GrapplingHookCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("GrapplingHookCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0
stop
}
}

actor GrapplingHookCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("GrapplingHookCooldownItem",140)
TNT1 A 0 A_Spawnitemex("GrapplingHookCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor GrapplingHookCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 140 
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor GrapplingHookShooter : CustomInventory  //places a buster posie based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 0 A_FireCustomMissile("GrapplingHook",0,0,8,0)
stop
TeamRedFire:
BUST B 0 A_FireCustomMissile("GrapplingHook_RED",0,0,8,0)
TNT1 A 0
stop
TeamBlueFire:
BUST B 0 A_FireCustomMissile("GrapplingHook_BLUE",0,0,8,0)
TNT1 A 0
stop
}
}