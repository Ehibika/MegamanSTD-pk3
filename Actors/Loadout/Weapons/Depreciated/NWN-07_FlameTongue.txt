/*=============================
It's a flamethrower
*///===========================


actor FlameTongue : WepMasterBase
{
Tag "FlameTongue"
Weapon.AmmoUse 1
Weapon.AmmoGive 50
scale 2.0
Obituary "$OB_Burner"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "FlameTongueClip"
inventory.icon "FTONI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
FWPR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
FWPB B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER1) == True,"User1")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER2) == True,"User2")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER3) == True,"User3")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER4) == True,"User4")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_RELOAD) == True,"Reload")
"####" B 0 A_Jumpifinventory("FlameTongueClip",1,"Ready")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_Giveinventory("FlameShotTimer")
hold:
"####" B 0 A_Jumpifinventory("FlameShotTimer",1,1)
Goto FlameShot
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" L 0 A_PlaySoundEx("weapon/waveburner","Weapon")
"####" B 0 A_Giveinventory("FlameThrowerShooter")
"####" C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
"####" B 0 A_Refire
"####" B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
goto Ready
FlameShot:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/waveburner","Weapon")
"####" B 0 A_PlaySoundEx("weapon/firestorm","soundslot7")
"####" B 0 A_Giveinventory("FlameThrowerShooter")
"####" B 0 A_Giveinventory("FlameShotShooter")
"####" B 0 A_Giveinventory("FlameShotTimer")
"####" C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
"####" B 0 A_Refire("Hold")
"####" B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
goto Ready


NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("FlameTongueClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 42 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("FlameTongueClip",50)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor FlameTongue2 : FlameTongue
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor FlameTongueClip : Ammo
{
inventory.amount 1
inventory.maxamount 70
+INVENTORY.IGNORESKILL
}

Actor BurnerhitDelay : powerprotection
{
Powerup.duration 3
Damagefactor "Burner", 0
Damagefactor "BurnerRED", 0
}

actor FlameShotTimer : powerup {inventory.amount 1 powerup.duration 10}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor Heatspray1 : FastProjectile
{

PROJECTILE
+NOINTERACTION
+BLOODSPLATTER
+THRUGHOST
+DONTBLAST
+DONTREFLECT
+MTHRUSPECIES
+FORCEPAIN
Radius 10
Height 12
damage (7)
damagetype "Burner"
RenderStyle "Add"
Obituary "$OB_FLAMETONGUE"
speed 74
scale 2.0
States
{
Spawn:
TNT1 A 0
TNT1 AA 0 A_SpawnItemEx("HeatSprayFx",random(-15,15),random(-15,15),random(-15,15),momx/4,momy/4,momz/4,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION) 
TNT1 A 0 A_SpawnItemEx("Heatspray2",0,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_Stop
TNT1 A 1 A_Changeflag("NOINTERACTION",1)
stop
XDeath:
Crash:
TNT1 A 1 
stop
Death:
stop
}
}
	actor Heatspray1_RED : Heatspray1{}
	actor Heatspray1_BLUE : Heatspray1 {translation "227:227=198:198","229:229=192:192"}

actor Heatspray2 : Heatspray1
{
Radius 16
Height 12
damage (6)
speed 176
States
{
Spawn:
TNT1 C 0
TNT1 A 0 A_SpawnItemEx("Heatspray3",0,0,0,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION) 
TNT1 C 0 A_Stop
TNT1 C 1 A_Changeflag("NOINTERACTION",1)
stop
XDeath:
Crash:
TNT1 A 1 
stop
Death:
stop
}
}

actor Heatspray3 : Heatspray1
{
Radius 28
Height 12
damage (5)
speed 285
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_Stop
TNT1 A 1 A_Changeflag("NOINTERACTION",1)
stop
XDeath:
Crash:
TNT1 A 1 
stop
Death:
stop
}
}

actor FlameTongueShot : ProjectileBase
{
Speed 30
Damage (15)
radius 8
Damagetype "Burner"
//MissileHeight 16
height 8
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 2 
TNT1 A 0 A_SpawnItemEx("HeatSprayFx",random(-15,15),random(-15,15),random(-15,15),momx/4,momy/4,momz/4,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
Loop
Death:
TNT1 A 1 A_Spawnitemex("CrisperizerPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}
	actor FlameTongueShot_RED : FlameTongueShot{}
	actor FlameTongueShot_BLUE : FlameTongueShot {translation "227:227=198:198","229:229=192:192"}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor HeatSprayFx  //flames 
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
height 2
Radius 2
damage (0)
reactiontime 1
scale 2
RenderStyle "ADD"
Alpha 0.2
States
{
Spawn:
BRNF A 0 Thrustthingz(0,30,0,0)
BRNF ABCDEFGH 1 A_Fadein(0.1)
BRNF C 0 
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor FlameThrowerShooter : CustomInventory  //places a buster posie based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 0 A_FireCustomMissile("Heatspray1",0,1,4,0)
stop
TeamRedFire:
BUST B 0 A_FireCustomMissile("Heatspray1_RED",0,1,4,0)
TNT1 A 0
stop
TeamBlueFire:
BUST B 0 A_FireCustomMissile("Heatspray1_BLUE",0,1,4,0)
TNT1 A 0
stop
}
}

actor FlameShotShooter : CustomInventory  //places a buster posie based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 0 A_FireCustomMissile("FlameTongueShot",0,0,4,0)
stop
TeamRedFire:
BUST B 0 A_FireCustomMissile("FlameTongueShot_RED",0,0,4,0)
TNT1 A 0
stop
TeamBlueFire:
BUST B 0 A_FireCustomMissile("FlameTongueShot_BLUE",0,0,4,0)
TNT1 A 0
stop
}
}