/*=============================

Feeling dangerous? the crossbow is well suited for closer-but-still-kinda-far
encounters, with just one quickly loaded clip, this weapon can keep a steady
supply of bolts coming at your foe. however, if you give the crossbow a second,
it will prime the bolt with a special poison to weaken your victim, it prevents
them from healing also!

*///===========================

actor NMDCrossbow : WepMasterBase
{
Tag "Crossbow"
Weapon.AmmoUse 1
Weapon.AmmoGive 1
scale 2.0
Obituary "$OB_CROSSBOW"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "CrossbowClip"
inventory.icon "XBOWI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady

"####" B 0 A_Jumpifinventory("CrossbowPrimerActive",1,3)
"####" B 0 A_Spawnitemex("CrossbowPrimeHandler")
"####" B 0 A_GiveInventory("CrossbowPrimerActive",1)
"####" B 0 
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER1) == True,"User1")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER2) == True,"User2")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER3) == True,"User3")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER4) == True,"User4")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_RELOAD) == True,"Reload")
//"####" B 0 A_GiveInventory("CrossbowPrime",1)
"####" B 0 A_Jumpifinventory("CrossbowPrime",35,"Ready3")
"####" B 0 A_Jumpifinventory("CrossbowPrime",34,"Ready2")
"####" B 0 A_Jumpifinventory("CrossbowClip",1,"Ready")
Goto Reload
Ready2:
"####" B 0 A_PlaySoundEx("weapon/Trigger","Weapon")
Ready3:
"####" B 0
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_RELOAD) == True,"Reload")
Goto Ready3

Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
"####" B 1 A_TakeInventory("CrossbowPrime",999)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 A 0 A_GiveInventory("NoClipBar")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_jumpifinventory("CrossbowPrime",35,"PrimeFire")
"####" B 0 A_TakeInventory("CrossbowPrime",999)
"####" B 0 A_PlaySoundEx("weapon/slashclaw","Weapon")
"####" B 0 A_GiveInventory("CrossbowShooter")
"####" CCDDB 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH)
"####" B 6 
goto Reload

PrimeFire:
"####" B 0 A_TakeInventory("CrossbowPrime",999)
"####" B 0 A_PlaySoundEx("weapon/slashclaw","Weapon")
"####" B 0 A_GiveInventory("PrimedCrossbowShooter")
"####" CCDDB 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH)
"####" B 6 
goto Reload
// ===11 frames===
NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("CrossbowClip",1,"Ready")
Reload:
"####" B 0 A_TakeInventory("ZoomMode")
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 5 
"####" B 0 //A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("CrossbowClip",8)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 0 A_TakeInventory("CrossbowPrime",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}


actor NMDCrossbow2 : NMDCrossbow
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor CrossbowClip : Ammo
{
inventory.amount 1
inventory.maxamount 1
}

actor CrossbowPrime : inventory
{
inventory.amount 1
inventory.maxamount 35
}

actor CrossbowPrimerActive : Once{}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor CrossbowShot : fastProjectileBase
{
Speed 225
Damage (36)
radius 8
height 10
scale 3.5
Obituary "$OB_CROSSBOW"
damagetype "SingleShot3"
MissileType "CrossbowTrail"
States
{
Spawn:
TNT1 A 0 
ARRS T 0 
ARRS T 1 //A_Spawnitemex("CrossbowTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Loop
Crash:
ARRS T 0 A_Stop
ARRS T 1 A_Spawnitemex("CrossbowPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
Death:
ARRS T 0 A_Stop
ARRS T 1 A_Spawnitemex("CrossbowPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
ARRS TZTZTZTZ 2
stop
}
}

	actor CrossbowShot_BLUE : CrossbowShot
	{MissileType "CrossbowTrail_BLUE"
	}

	actor CrossbowShot_RED : CrossbowShot
	{MissileType "CrossbowTrail_RED"
	translation "202:202=33:33","196:196=48:48", "197:197=18:18","192:192=54:54", "198:198=42:42" , "196:196=170:170"
	}
	
actor PrimedCrossbowShot : fastProjectileBase
{
Speed 225
Damage (36)
radius 8
height 10
scale 3.5
Obituary "$OB_CROSSBOW"
damagetype "Poisonous"
MissileType "PoisonTrail"
States
{
Spawn:
TNT1 A 0 
ARRS T 0 
ARRS T 1 //A_Spawnitemex("CrossbowTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Loop
Crash:
ARRS T 0 A_Stop
ARRS T 1 A_Spawnitemex("CrossbowPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
ARRS TTTT 2 A_Spawnitemex("PoisonedFX1",Random(-10,10),Random(-10,10),Random(-10,10),0,0,Random(1,3),0,SXF_TRANSFERTRANSLATION)
stop
Death:
ARRS T 0 A_Stop
ARRS T 1 A_Spawnitemex("CrossbowPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
ARRS TTTT 2 A_Spawnitemex("PoisonedFX1",Random(-10,10),Random(-10,10),Random(-10,10),0,0,Random(1,3),0,SXF_TRANSFERTRANSLATION)
ARRS TZTZTZTZ 2
stop
}
}

actor PrimeCrossbowShot_BLUE : PrimedCrossbowShot
{MissileType "PoisonTrail_BLUE"}

actor PrimeCrossbowShot_RED : PrimedCrossbowShot
{translation "202:202=33:33","196:196=48:48", "197:197=18:18","192:192=54:54", "198:198=42:42" , "196:196=170:170"
MissileType "PoisonTrail_RED"}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor CrossbowPrimeHandler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_Jumpif(ACS_NamedExecuteWithResult("CrossbowCheck") == 0,"Spawn")
TNT1 A 0 A_JumpIfInTargetInventory("CrossbowPrime",35,"Spawn")
TNT1 A 0 A_JumpIfInTargetInventory("CrossbowClip",1,"Prime")
loop
Prime:
TNT1 A 0 A_GiveToTarget("CrossbowPrime",1)
Goto Spawn
Death:
DeathTake:
TNT1 A 0
stop
}
}


actor CrossbowPuff1 : FXActor_A
{
scale 1.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("weapon/Mirrordeflect","Weapon")
BIM1 AABCDEF 1
stop
}
}

actor PoisonTrail : FXActor_A
{
scale 0.6
Alpha 0.9
States
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_Changevelocity(random(-1,1),random(-1,1),random(1,2),CVF_RELATIVE)
SBUB AA 8
SBUB B 3
stop
}
}

	
	actor PoisonTrail_BLUE : PoisonTrail
	{
	Translation "192:192=126:126", "198:198=78:78"
	}
	
	actor PoisonTrail_RED : PoisonTrail
	{
	translation "192:192=107:107", "198:198=110:110"
	}

actor CrossbowTrail : FXActor_A
{
scale 1.0
Alpha 0.8
States
{
Spawn:
TNT1 A 0 
BASB UU 1
BASB UU 1 A_Fadeout
stop
}
}

	actor CrossbowTrail_BLUE : CrossbowTrail
	{
	}

	actor CrossbowTrail_RED : CrossbowTrail
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor CrossbowShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("CrossbowShot",0,1,4,8)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("CrossbowShot_RED",0,1,4,8)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("CrossbowShot_BLUE",0,1,4,8)
TNT1 A 1
stop
}
}


actor PrimedCrossbowShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("PrimedCrossbowShot",0,1,4,8)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("PrimeCrossbowShot_RED",0,1,4,8)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("PrimeCrossbowShot_BLUE",0,1,4,8)
TNT1 A 1
stop
}
}