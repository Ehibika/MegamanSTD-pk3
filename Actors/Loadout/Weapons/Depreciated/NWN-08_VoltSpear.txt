Actor VoltSpear : WepMasterBase
{
	Weapon.AmmoUse 0
	Weapon.AmmoGive 60	
	Weapon.AmmoType "VoltSpearClip"
	Inventory.Icon "VLTSI"
	Inventory.Pickupmessage "Zap 'em."
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.DONTBOB
	+WEAPON.NOAUTOFIRE
	+WEAPON.NOAUTOAIM
	Obituary "$OB_VoltSpear"
	Tag "Volt Spear"
	AttackSound "weapon/VoltSpear"
	Scale 2.0
	States
	{
	Spawn:
		WEAP X 1
		Stop
	Ready:
		THLR A 0 A_Jumpifinventory("WilyTeamFlag", 1, "RedReady")
		THLB A 0 
	RedReady:
		"####" B 0 A_Jumpifinventory("OverHeatFlag_C",1,"OverHeatReady")
		"####" A 1 A_WeaponReady
		"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER1) == True,"User1")
		"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER2) == True,"User2")
		"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER3) == True,"User3")
		"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER4) == True,"User4")
		"####" A 0 A_Jumpifinventory("Reload_Trigger", 1, "Reload")
		"####" A 0 A_Jumpifinventory("VoltSpearClip", 1, "Ready")
		Goto Reload
	OverHeatReady:
		"####" A 1 A_WeaponReady(WRF_NOPRIMARY)
		Goto Ready
		//===============
	Deselect:
		TNT1 A 0 A_TakeInventory("NoClipBar",9)
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
		"####" A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_GiveInventory("NoClipBar")
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
		"####" A 1 A_Raise
		Loop
	Fire:
		"####" A 0 A_JumpIfNoAmmo("NoAmmo")
		"####" B 0 A_GiveInventory("HeatCoolDelay_C")
		"####" A 0 A_GiveInventory("VoltSpearShooter")
		"####" CC 1
		"####" A 0 A_GiveInventory("WeaponHeat_C",2)
		"####" B 0 A_Jumpifinventory("WeaponHeat_C",100,"OverHeatStart")
		"####" A 0 A_ReFire
		"####" BCDEAAA 1
		Goto Ready
	OverHeatStart:
		"####" B 0 A_PlaySoundEx("weapon/MagnetMissile","Weapon")
		"####" B 0 A_GiveInventory("OverHeatFlag_C")
		Goto OverHeatReady
	NoAmmo:
		"####" A 1
		"####" A 0 A_Jumpifinventory("VoltSpearClip", 1, "Ready")
	Reload:
		"####" A 1 Offset(0, 32)
		"####" A 1 Offset(0, 46)
		"####" A 1 Offset(0, 58)
		"####" A 1 Offset(0, 70)
		"####" A 1 Offset(0, 82)
		"####" A 0 A_PlaySoundEx("weapon/rminestick", "Weapon")
		"####" A 44 
		"####" A 0 A_PlaySoundEx("weapon/Crashland", "Weapon")
		"####" A 1 Offset(0, 82)
		"####" A 1 Offset(0, 70)
		"####" A 1 Offset(0, 58)
		"####" A 1 Offset(0, 46)
		"####" A 1 Offset(0, 32)
		"####" A 0 A_Giveinventory("VoltSpearClip", 60)
		"####" A 0 A_TakeInventory("Reload_Trigger", 999)
		"####" A 1 A_Refire("Fire")
		Goto Ready
	Flash:
		TNT1 A 5 
		Stop
	NoFlash:
		TNT1 A 1
		Stop
	}
}

Actor VoltSpear2 : VoltSpear
{
	Weapon.SlotNumber 2
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
Actor VoltSpearClip : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 60
}

actor WeaponHeat_C : inventory
{
inventory.amount 1
inventory.maxamount 100
}

actor OverHeatFlag_C : inventory
{
inventory.amount 1
inventory.maxamount 1
}

Actor HeatCoolDelay_C : Powerup {inventory.amount 1 inventory.maxamount 1 powerup.duration 20}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor VoltSpearPuff : BulletPuff
{
scale 3.5
Obituary "$OB_VoltSpear"
DamageType "VoltSpear"

+PUFFONACTORS
+ALWAYSPUFF
+PUFFGETSOWNER
+MTHRUSPECIES
+HITTRACER
alpha 1.0
States
{
Spawn:
CRBM A 0
//CRBM A 1 A_Explode(8 + CallACS("DamageRate",4,Range2,55),10,0,0,10)
CRBM A 0 A_CheckFlag("GHOST","Terminate",AAPTR_TRACER)
SPKH DDDCCBBA 1
stop
Terminate:
TNT1 A 0
stop
}
}

	actor VoltSpearPuff_BLUE : VoltSpearPuff
	{}

	actor VoltSpearPuff_RED : VoltSpearPuff
	{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"}
	
actor VoltSpearPuff2 : VoltSpearPuff
{
States
{
Spawn:
CRBM A 0
TNT1 A 1
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Utility actors|--------------------------------
//[+]========================================================================[+]
actor HeatDrainer_C : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
	TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"Death")
	TNT1 A 0 A_JumpIfInTargetInventory("OverHeatFlag_C",1,"OverHeated")
	TNT1 A 5
	TNT1 A 0 A_JumpIfInTargetInventory("HeatCoolDelay_C",1,"Spawn")
	TNT1 A 0 A_TakeFromTarget("WeaponHeat_C",3)
	loop

OverHeated:
	TNT1 A 6
	TNT1 A 0 A_TakeFromTarget("WeaponHeat_C",2)
	TNT1 A 0 A_JumpIfInTargetInventory("WeaponHeat_C",1,"OverHeated")
	Goto Cooled

Cooled:
	TNT1 A 0 A_TakeFromTarget("OverHeatFlag_C",9)
	Goto Spawn

Death:
	DeathTake:
		TNT1 A 0
		stop
}
}
//[+]========================================================================[+]
//--------------------------------|Effect actors|--------------------------------
//[+]========================================================================[+]
Actor VoltTrail : FXActor_A
{
	+Missile
	+NOGRAVITY
	+NOINTERACTION
	Scale 0.4
	States
	{
	Spawn:
	MMFX X 0
	MMFX X 0 A_JumpifCloser(110,"Death")
		MMFX XXXXBCDE 1 BRIGHT
		Goto Death
		Death:
		MMFX X 0
		Stop
	}
}

Actor VoltTrail2 : FXActor_B
{
	+Missile
	+NOGRAVITY
	+NOINTERACTION
	Scale 1.3
	Alpha 0.25
	States
	{
	Spawn:
	MMFX X 0
	MMFX X 0 A_JumpifCloser(110,"Death")
		MMFX XXXXBCDE 1 BRIGHT
		Goto Death
		Death:
		MMFX X 0
		Stop
	}
}

actor VoltTrail2_BLUE : VoltTrail2
{translation "4:4=196:196", "215:215=192:192"
}

actor VoltTrail2_RED : VoltTrail2
{translation "4:4=51:51", "215:215=161:161"
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]



actor VoltSpearShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
		"####" A 0 A_RailAttack(VS_Mod, 18, 0, NONE, NONE, 0 , 25, "VoltSpearPuff2", 0, 0, VSR2, 7, 0.4, 0, "VoltTrail2", -4)
		"####" A 0 A_RailAttack(VS_Min, 18, 1, NONE, White, 0 , 25, "VoltSpearPuff", 0, 0, VSR1, 7, 0.4, 0, "VoltTrail", -4)
stop
TeamRedFire:
		"####" A 0 A_RailAttack(VS_Mod, 18, 0, NONE, NONE, 0 , 25, "VoltSpearPuff2", 0, 0, VSR2, 7, 0.4, 0, "VoltTrail2_RED", -4)
		"####" A 0 A_RailAttack(VS_Min, 18, 1, NONE, White, 0 , 25, "VoltSpearPuff_RED", 0, 0, VSR1, 7, 0.4, 0, "VoltTrail", -4)
TNT1 A 1
stop
TeamBlueFire:
		"####" A 0 A_RailAttack(VS_Mod, 18, 0, NONE, NONE, 0 , 25, "VoltSpearPuff2", 0, 0, VSR2, 7, 0.4, 0, "VoltTrail2_BLUE", -4)
		"####" A 0 A_RailAttack(VS_Min, 18, 1, NONE, White, 0 , 25, "VoltSpearPuff_BLUE", 0, 0, VSR1, 7, 0.4, 0, "VoltTrail", -4)
TNT1 A 1
stop
}
}