/*=============================
Lightweight and quite easy to handle, this handy explosive can lay waste to all
kinds of opponents and obstacles and it ricochets off walls to! the go-to hand
explosive for all corondian infantry or lone travelers that need some means to
reduce something or someone into tattered bits.

Some of our more bold personell have been using the explosive to give 
themselves a boost in the air, by throwing it beneath them and riding off the
blast, this is highly unadvised as it's simply dangerous and stupid.
*///===========================
Actor MiniBombFlag : inventory {inventory.amount 1 inventory.maxamount 2}


actor MiniBombProjectile 
{
PROJECTILE
damagetype "Explosion"
Obituary "$OB_MINIBOMB"
radius 11
height 20
scale 2.5
//-NOGRAVITY
//+DOOMBOuNCE
+MTHRUSPECIES
-ALLOWBOUNCEONACTORS
//-BounceOnFloors
+NOTARGETSWITCH
speed 50
gravity 2.2
Damage (20)
wallbouncefactor 0.6
reactiontime 55
States
{
Spawn:
MINB A 0
MINB A 1 A_SpawnItemEX("MiniBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
MINB A 0 A_ChangeFlag("DOOMBOuNCE",true)
MINB A 0 A_ChangeFlag("BounceOnFloors",false)
MINB ABCDE 1 A_SpawnItemEX("MiniBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB FGH 1 A_SpawnItemEX("MiniBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
Goto Spawn+2
Death:
AMIS A 0 A_CheckFloor("JumpCheck")
MINB A 0 A_SpawnItemEx("MiniBombExplosion")
stop
Crash:
MINB A 0 A_SpawnItemEx("MiniBombExplosion")
stop
XDeath:
MINB A 0 A_SpawnItemEx("MiniBombFakeExplosion")
stop
JumpCheck:
MINB A 0 A_Jumpifcloser(185,"blastjump")
MINB A 0 A_SpawnItemEx("MiniBombExplosion")
MINB A 0 A_SpawnItemEx("MiniBombThrustSource",0,0,-10)
stop
//---
blastjump:
MINB A 0 A_SpawnItemEx("MiniBombThrustSource",0,0,-10)
MINB A 0 A_GiveToTarget("BlastProtection")
MINB A 5 A_SpawnItemEx("MiniBombExplosion")
stop
}
}

	actor MiniBombProjectile_BLUE : MiniBombProjectile{}
	actor MiniBombProjectile_RED : MiniBombProjectile{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170"}
	
	// [+++]=============================[+++]
actor MiniBombThrustSource : BasicExplosion //now just the FX due to desynch
{
States
{
Spawn:
MINB A 0 
NAPC D 0 //A_Explode(35, 350, 1,0,36)
MINB A 0 ACS_NamedExecuteAlways("BLASTKNOCKBACK",0,90,50,200)
TNT1 AA 1 
stop
}
}
	
actor MiniBombExplosion : BasicExplosion
{
States
{
Spawn:
NAPC A 0 
NAPC D 0 A_Explode(30, 164, 1,0,56)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}

actor MiniBombFakeExplosion : BasicExplosion
{
States
{
Spawn:
NAPC A 0 
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor MiniBombTrail : FXActor_A
{
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 2
MINB IIIIII 1
MINB IIIIII 1 A_Fadeout
stop
}
}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor MiniBombCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("MiniBombCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("MiniBombCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0
stop
}
}

actor MiniBombCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("MiniBombCooldownItem",85)
TNT1 A 0 A_Spawnitemex("MiniBombCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor MiniBombCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 85
}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor MiniBombShooter : CustomInventory  //places a buster posie based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 0 A_FireCustomMissile("MiniBombProjectile",0,1,4,12)
stop
TeamRedFire:
BUST B 0 A_FireCustomMissile("MiniBombProjectile_RED",0,1,4,12)
TNT1 A 0
stop
TeamBlueFire:
BUST B 0 A_FireCustomMissile("MiniBombProjectile_BLUE",0,1,4,12)
TNT1 A 0
stop
}
}