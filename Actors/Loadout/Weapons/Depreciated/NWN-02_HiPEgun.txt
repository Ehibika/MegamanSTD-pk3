/*=============================
The High Precision Energy gun, or HiPE gun for short; is a midweight but
sufficiently strong pump action weapon with remarkable range. it's a slow weapon
but it deals strong damage from way, far away. this weapon demands precision 
however, so it's best used when dealing with another far away opponent.

*///===========================

actor HiPEgun : WepMasterBase
{
Tag "Hi.P.E Gun"
Weapon.AmmoUse 1
Weapon.AmmoGive 4

scale 2.0
Obituary "$OB_HIPEGUN"
inventory.icon "HIPEI"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "HiPEgunClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
WSNP B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
SNPR B 0 
RedReady:
"####" B 0 A_Jumpifinventory("ZoomMode",1,"Ready2")
"####" A 1 A_WeaponReady(WRF_ALLOWZOOM)
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER1) == True,"User1")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER2) == True,"User2")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER3) == True,"User3")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER4) == True,"User4")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_RELOAD) == True,"Reload")
"####" B 0 A_Jumpifinventory("HiPEgunClip",1,"Ready")
Goto Reload

	Ready2:  //zooom ready
	"####" A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_NOSWITCH|WRF_NOSECONDARY)
	"####" A 0 A_Giveinventory("FocusSpeedDown",1)
	"####" A 0 A_ZoomFactor(4.5)
	"####" A 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
	"####" A 0 A_Jumpifinventory("HiPEgunClip",1,"Ready")
	Goto Reload

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
"####" B 0 A_GiveInventory("CycleReloadDelay")
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/SlashClaw","SoundSlot6")
"####" B 0 A_PlaySoundEx("weapon/HardBuster5","SoundSlot7")
"####" B 0 A_GiveInventory("HiPEgunShooter")
"####" JJKK 1 

"####" A 4
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 2 Offset(0,32)
"####" A 2 Offset(0,46)
"####" A 2 Offset(0,58)
"####" A 6 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 2 Offset(0,58)
"####" A 2 Offset(0,46)
"####" A 2 Offset(0,32)
"####" A 2 
"####" B 0 A_GiveInventory("CycleReloadDelay")

"####" C 0 A_REfire
goto Ready
// ===22 frames===

NoAmmo:
"####" A 1
"####" B 0 A_Jumpifinventory("HiPEgunClip",1,"Ready")
Reload:
	"####" B 0 A_ZoomFactor(1.0,0)
	"####" B 0 A_TakeInventory("ZoomMode")
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 0 A_Giveinventory("HiPEgunReloadTimer1",1)
"####" B 0 A_Jumpifinventory("HiPEgunClip",1,"Reload2")
"####" A 19 A_WeaponReady(WRF_NOBOB|WRF_NOPRIMARY)
goto Reloadloop
Reload2:
"####" A 1 A_WeaponReady(WRF_NOBOB)
"####" B 0 A_Jumpifinventory("HiPEgunReloadTimer1",1,"Reload2")
goto Reloadloop
Reloadloop:
"####" A 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 0 A_Giveinventory("HiPEgunClip",1)
"####" B 0 A_Giveinventory("HiPEgunReloadTimer2",1)
"####" B 0 A_Jumpifinventory("HiPEgunClip",4,"ReloadLoopEnd")
"####" B 0 A_Jumpifinventory("HiPEgunClip",2,"ReloadReadyLoop")
"####" A 12 Offset(0,82)
Goto Reloadloop
ReloadReadyLoop:
"####" A 1 A_WeaponReady(WRF_NOBOB)
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER1) == True,"User1")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER2) == True,"User2")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER3) == True,"User3")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER4) == True,"User4")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_RELOAD) == True,"Reload")
"####" B 0 A_Jumpifinventory("HiPEgunReloadTimer2",1,"ReloadReadyLoop")
Goto Reloadloop
ReloadLoopEnd:
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 1 A_Refire("Fire")
Goto Ready

zoom:
"####" B 0 A_Jumpifinventory("ZoomMode",1,"DeZoom")

"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 0 A_ZoomFactor(4.5,0)
"####" B 0 A_GiveInventory("ZoomMode")
"####" A 7
goto Ready
DeZoom:
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 0 A_ZoomFactor(1.0,0)
"####" B 0 A_TakeInventory("ZoomMode",9)
"####" A 7
goto Ready

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}


actor HiPEgun2 : HiPEgun
{weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor HiPEgunClip : Ammo
{
inventory.amount 1
inventory.maxamount 4
}

actor HiPEgunReloadTimer1 : Powerup
{
inventory.amount 1
inventory.maxamount 1
Powerup.Duration 19
}

actor HiPEgunReloadTimer2 : Powerup
{
inventory.amount 1
inventory.maxamount 1
Powerup.Duration 14
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor HiPEgunShot : fastProjectileBase
{
Obituary "$OB_HIPEGUN"
DamageType "SingleShot3"

Speed 275
Damage (45)
radius 8
height 10
scale 3.5
//damagetype "Sniper"

MissileType "HiPEgunTrail"
States
{
Spawn:
TNT1 A 0 
TNT1 A 0  
TNT1 A 0
Spawn2:
CRBM A 0 
CRBM A 2
Loop
Death:
CRBM A 0 A_Stop
CRBM A 1 A_Spawnitemex("HiPEgunPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor HiPEgunShot_BLUE : HiPEgunShot
	{MissileType "HiPEgunTrail_BLUE"
	}

	actor HiPEgunShot_RED : HiPEgunShot
	{MissileType "HiPEgunTrail_RED"
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor HiPEgunPuff1 : FXActor_A
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("weapon/Mirrordeflect","Weapon")
BIM1 AABCDEF 1
stop
}
}

actor HiPEgunTrail : FXActor_B
{
scale 2.5
Alpha 0.8
States
{
Spawn:
TNT1 A 0 
BASB UU 1
BASB UU 1 A_Fadeout
stop
}
}

	actor HiPEgunTrail_BLUE : HiPEgunTrail
	{
	}

	actor HiPEgunTrail_RED : HiPEgunTrail
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor HiPEgunShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("HiPEgunShot",0,1,4,8)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("HiPEgunShot_RED",0,1,4,8)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("HiPEgunShot_BLUE",0,1,4,8)
TNT1 A 1
stop
}
}