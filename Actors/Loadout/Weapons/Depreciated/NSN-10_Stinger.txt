/*=============================
It's silent, it's swift, and of course, it's increadibly deadly! the stinger
is the perfect weapon for any discerning assassin of the stealthy persuasion.
launching a volley of poisonous darts, continuous damage from this weapon will
leave a lingering effect your foes won't forget!

that is, if they live to tell the tale. because once your target is envenomed.
this weapon's impact grows!

*///===========================

actor Stinger : WepMasterBase
{
tag "Stinger"
Weapon.AmmoUse 1
Weapon.AmmoGive 15
scale 2.0
Obituary "$OB_STINGER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "StingerClip"
inventory.icon "STNGI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Weapon.slotnumber 2
States
{
Spawn:
WEAP X 1
stop
Ready:
BS2R B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BS2B B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER1) == True,"User1")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER2) == True,"User2")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER3) == True,"User3")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER4) == True,"User4")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_RELOAD) == True,"Reload")
"####" B 0 A_Jumpifinventory("StingerClip",1,"Ready")
Goto Reload

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/RapidBuster2","Weapon")
"####" B 0 A_GiveInventory("StingerShooter")
"####" CD 1 
"####" B 0 A_REfire
"####" B 3 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
goto Ready

NoAmmo:
"####" B 1
"####" B 3 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
"####" B 0 A_Jumpifinventory("StingerClip",1,"Ready")
Reload:
"####" B 0 A_ZoomFactor(1.0)
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 24 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("StingerClip",15)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}


actor Stinger2 : Stinger
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor StingerClip : Ammo
{
inventory.amount 1
inventory.maxamount 15
}

actor StingCount : inventory
{
inventory.amount 1
inventory.maxamount 9
}

Actor StingBuffer : Powerup {inventory.amount 1 inventory.maxamount 1 Powerup.Duration 10}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor StingerShot : fastProjectileBase
{
Speed 135
Damage (4)
radius 6
height 10
scale 1.0
damagetype "Stinger"
MissileType "StingerTrail"
States
{
Spawn:
TNT1 A 0 
CRBM A 0 
PINM A 1 //A_Spawnitemex("CrossbowTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Loop
Crash:
TNT1 A 1 A_Spawnitemex("StingerPuff",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_Spawnitemex("PoisonedFX1",Random(-10,10),Random(-10,10),Random(-10,10),0,0,Random(1,3),0,SXF_TRANSFERTRANSLATION)
Stop

Death:
CRBM A 0 A_Stop
TNT1 A 1 A_Spawnitemex("StingerStickFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

actor StingerShot_BLUE : StingerShot
{MissileType "StingerTrail_BLUE"
}

actor StingerShot_RED : StingerShot
{translation "202:202=33:33","196:196=48:48", "197:197=18:18","192:192=54:54", "198:198=42:42" , "196:196=170:170","199:199=47:47"
MissileType "StingerTrail_RED"
}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor StingerPuff : FXActor_A
{
scale 1.0
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("weapon/OrbBounce","Weapon")
BIM1 AABCDEF 1
stop
}
}


actor StingerStickFX : FXActor_A
{
scale 1.0
States
{
Spawn:
TNT1 A 0 
PINM A 10 
PINM AZAZAZAZ 4 
stop
}
}

actor StingerTrail : FXActor_A
{
scale 1.0
Alpha 0.6
States
{
Spawn:
TNT1 A 0 
PINM A 1
stop
}
}

	actor StingerTrail_BLUE : StingerTrail
	{
	}

	actor StingerTrail_RED : StingerTrail
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}
//[+]========================================================================[+]
//--------------------------------|Utility Actors|------------------------------
//[+]========================================================================[+]
actor StingWatcher : WatcherBase 
{
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveToTarget("StingBuffer")
Spawn2:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("StingBuffer",1,"Spawn2")
TNT1 A 0 A_TakeFromTarget("StingCount",99)
Death:
DeathTake:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor StingerShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("StingerShot",frandom(-ST_SPD,ST_SPD),1,4,8,0,frandom(-ST_SPD,ST_SPD))
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("StingerShot_RED",frandom(-ST_SPD,ST_SPD),1,4,8,0,frandom(-ST_SPD,ST_SPD))
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("StingerShot_BLUE",frandom(-ST_SPD,ST_SPD),1,4,8,0,frandom(-ST_SPD,ST_SPD))
TNT1 A 1
stop
}
}