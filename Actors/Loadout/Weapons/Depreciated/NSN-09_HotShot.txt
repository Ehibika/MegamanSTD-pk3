/*=============================

The hotshot is a bustergun ready to turn up the heat on your enemies, firing
bouncing spheres of flaming projectiles at a steady rate, this weapon can easily
overwhelm your foes, especially powerfull when you've got the high ground!

*///===========================

actor HotShot : WepMasterBase
{
Tag "HotShot"
Weapon.AmmoUse 1
Weapon.AmmoGive 14
scale 2.0
Obituary "$OB_HOTSHOT"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "HotShotClip"
inventory.icon "HOTSI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Weapon.slotnumber 2
States
{
Spawn:
WEAP X 1
stop
Ready:
FWPR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
FWPB B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER1) == True,"User1")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER2) == True,"User2")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER3) == True,"User3")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_USER4) == True,"User4")
"####" B 0 A_Jumpif(CallACS("NEUM_ButtonCheck",BT_RELOAD) == True,"Reload")
"####" B 0 A_Jumpifinventory("HotShotClip",1,"Ready")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/coilfire1","Weapon")
"####" B 0 A_GiveInventory("HotShotShooter")
"####" CCC 1 A_WeaponReady(WRF_NOPRIMARY)
"####" B 3 
"####" C 0 A_REfire
goto Ready
//==5 frames==
NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("HotShotClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 22 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("HotShotClip",12)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor HotShot2 : HotShot
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor HotShotClip : Ammo
{
inventory.amount 1
inventory.maxamount 14
}

actor ScorchCount : inventory
{
inventory.amount 1
inventory.maxamount 3
}

Actor ScorchBuffer : Powerup {inventory.amount 1 inventory.maxamount 1 Powerup.Duration 17}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor HotShotFireball : ProjectileBase
{
Speed 20
Damage (10)
+NOEXPLODEFLOOR
//+BOUNCEONWALLS
-NOGRAVITY
//Bouncefactor 1
wallBouncefactor 1
radius 16
Reactiontime 60
Gravity 2.5
Obituary "$OB_HOTSHOT"
Damagetype "HotShot"
height 16
scale 3.5
States
{
Spawn:
TNT1 A 0 
HTST A 0 ThrustThingZ(0, 60, 0, 1)
HTST A 1
Spawn2:
TNT1 AA 1 A_Spawnitemex("HotShotGraphic",0,0,0,velX,vely,velz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
TNT1 A 0 A_Countdown
TNT1 A 0 A_Checkfloor("ReBounce")
TNT1 A 0 A_Spawnitemex("HotShotTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
loop
ReBounce:
HTST A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
HTST A 0 A_Playsoundex("weapon/coilbounce1","Soundslot6")
TNT1 A 1 ThrustThingZ(0, 60, 0, 1)
//TNT1 A 0 A_Spawnitemex("HotShotTrail",0,0,0,velX,vely,velz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
Goto Spawn2
Death:
BSTG A 1 A_Spawnitemex("HotShotPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor HotShotFireball_BLUE : HotShotFireball
	{
	}

	actor HotShotFireball_RED : HotShotFireball
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"
	}
	
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor HotShotPuff1 : FXActor_A
{
scale 2
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}

actor HotShotGraphic : FXActor_A
{
scale 2.5
States
{
Spawn:
HTST A 0
HTST A 2
stop
}
}

actor HotShotTrail : FXActor_B
{
scale 1.4
Alpha 0.9
States
{
Spawn:
CRBM A 0
HTST BBCD 2
//BSTG AAAAA 1 A_Fadeout
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Utility Actors|------------------------------
//[+]========================================================================[+]
actor ScorchWatcher : WatcherBase 
{
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveToTarget("ScorchBuffer")
Spawn2:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("ScorchBuffer",1,"Spawn2")
TNT1 A 0 A_TakeFromTarget("ScorchCount",99)
Death:
DeathTake:
TNT1 A 0
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor HotShotShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("HotShotFireball",0,1,4,4)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("HotShotFireball_RED",0,1,4,4)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("HotShotFireball_BLUE",0,1,4,4)
TNT1 A 1
stop
}
}