

actor CrushingSprayerWep : WepMasterBase
{
Tag "CrushingSprayer"
Weapon.AmmoUse 1
Weapon.AmmoGive 6
scale 2.0
Obituary "$OB_CRUSHINGSPRAYER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "CrushingSprayer_Ammo"
inventory.icon "FTONI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
FWPR B 1 A_WeaponReady(WRF_ALLOWINPUTS)
"####" B 0 A_Jumpifinventory("CrushingSprayer_Ammo",1,"Ready.Core")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Sprayer_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/napalm","Weapon")
"####" B 0 A_FireCustomMissile("CrushingSprayerBurst",0,1,0,0)
"####" B 0 A_FireCustomMissile("CrushingSprayerDamagerStart",0,0,0,0)
"####" A 0 A_Giveinventory("CrushingSprayerCST")
"####" C 2 A_WeaponReady(WRF_FIRING)
goto Ready.Wep

NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("CrushingSprayer_Ammo",1,"Ready")
Goto Reload

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor CrushingSprayerWep2 : CrushingSprayerWep
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor CrushingSprayer_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 6
+INVENTORY.IGNORESKILL
}


Actor CrushingSprayhitDelay : powerprotection
{
Powerup.duration 3
Damagefactor "CrushingSpray", 0
}

actor CrushingSprayerCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 25
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

Const Int CrushingSprayerRange = 30;
Const Int CrushingSprayerFXRange = 20;
Const float CrushingSprayer_DMG = 100;

actor CrushingSprayerDamagerStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("CrushingSprayerDamager",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor CrushingSprayerDamager : FastProjectileBase
{
    PROJECTILE
    damagetype "CrushingSpray"
    Obituary "$OB_CRUSHINGSPRAYER"
    +SKYEXPLODE
    +EXPLODEONWATER
    +RIPPER
    +SERVERSIDEONLY
    RenderStyle None
    Damage (0) 
    Radius 10
    Height 10
    speed 15 
    scale 2.5
    states
    {
        Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch == CrushingSprayerRange%5,"Spawn.Pause")
        TNT1 A 0 A_JumpIf(Pitch > CrushingSprayerRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CrushingSprayerDamager",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(CrushingSprayer_DMG,32,0,0,32)
        TNT1 A 1
        stop
        Spawn.Pause:
        TNT1 A 1 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CrushingSprayerDamager",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(CrushingSprayer_DMG,32,0,0,32)
        TNT1 A 1
        stop

    Death:
        TNT1 A 0 A_Explode(CrushingSprayer_DMG,32,0,0,32)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(CrushingSprayer_DMG,32,0,0,32)
        stop

        
    }
}

actor CrushingSprayerBurstStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("CrushingSprayerBurst",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor CrushingSprayerBurst : FastProjectileBase
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    +THRUACTORS
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > CrushingSprayerFXRange,"Death2")
        TNT1 A 0 A_JumpIf(Pitch > (CrushingSprayerFXRange/3)*2,"Spawn3")
        TNT1 A 0 A_JumpIf(Pitch > (CrushingSprayerFXRange/3),"Spawn2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CrushingSprayerBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 AAA 0 A_SpawnItemEx("CrushingSprayerFx1",random(-5,5),random(-5,5),random(-5,5),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Spawn2:
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CrushingSprayerBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 AAA 0 A_SpawnItemEx("CrushingSprayerFX_A",random(-30,30),random(-30,30),random(-30,30),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 AAA 0 A_SpawnItemEx("CrushingSprayerFx2",random(-5,5),random(-5,5),random(-5,5),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Spawn3:
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CrushingSprayerBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 AAA 0 A_SpawnItemEx("CrushingSprayerFx3",random(-15,15),random(-15,15),random(-15,15),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 //A_SpawnItemEx("CrushingSprayerEndFX",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 //A_SpawnItemEx("CrushingSprayerEndFX",0,0,0,0,0,0,0,1)
        stop
    }
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

//const float SSNP_A = 0.2;

actor CrushingSprayerFx1  
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
height 2
Radius 2
damage (0)
reactiontime 1
scale 2.5
RenderStyle "Translucent"
Alpha 0.8
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
MUDB PO 1 
MUDB O 0 A_Fadeout(0.4)
MUDB O 0 A_ScaleVelocity(0.5)
Spawn2:
MUDB O 5 A_Fadeout(0.15)
loop
}
}

actor CrushingSprayerFx2 : CrushingSprayerFx1
{
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
TNT1 A 2
MUDB NN 1 
MUDB O 0 A_Fadeout(0.4)
MUDB O 0 A_ScaleVelocity(0.5)
Spawn2:
MUDB N 5 A_Fadeout(0.15)
loop
}
}

actor CrushingSprayerFx3 : CrushingSprayerFx1
{
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
TNT1 A 4
MUDB MM 1 
MUDB O 0 A_Fadeout(0.4)
MUDB O 0 A_ScaleVelocity(0.5)
Spawn2:
MUDB M 5 A_Fadeout(0.15)
loop
}
}

actor CrushingSprayerFX_A : FXActor_A
{
scale 1.0
Translation "192:192=90:90", "196:196=88:88", "198:198=94:94"
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 5
DDIG CCCC 3 A_Fadeout
stop
}
}
