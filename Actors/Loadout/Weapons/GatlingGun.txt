const float GATT_SPD = 2.6; 

actor GatlingGunWep : WepMasterBase
{
Tag "GatlingGun"
Weapon.AmmoUse 1
Weapon.AmmoGive 100

Obituary "$OB_GATLINGGUN"
inventory.icon "BLASI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "GatlingGun_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop
Ready:
Ready.Wep:
PIDB A 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" A 0 A_Jumpifinventory("GatlingGun_Ammo",1,"Ready.Core")
Goto Reload
//===============
Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Launcher_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop
Fire:
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/HardBuster3","Weapon")
"####" A 0 A_FireCustomMissile("GatlingGunShot",fRandom(-(GATT_SPD),(GATT_SPD)),1,4,4,0,fRandom(-(GATT_SPD),(GATT_SPD)))
"####" AA 0 A_FireCustomMissile("GatlingGunShot",fRandom(-(GATT_SPD),(GATT_SPD)),0,4,4,0,fRandom(-(GATT_SPD),(GATT_SPD)))
"####" BC 1 A_WeaponReady(WRF_NOPRIMARY)
"####" A 3 
"####" C 0 A_REfire
"####" A 0 A_Takeinventory("WepBloomItem",999)
goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_Jumpifinventory("GatlingGun_Ammo",1,"Ready")
Reload:

"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 32 
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_Giveinventory("GatlingGun_Ammo",100)
"####" A 1 A_Refire("Fire")
Goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor GatlingGunWep2 : GatlingGunWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor GatlingGun_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 100
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor GatlingGunShot : fastProjectileBase
{
Obituary "$OB_GATLINGGUN"
DamageType "GatlingGunP"
Speed 64
Damage (10)
radius 3

height 3
scale 1.0
Var Int User_TrailCount;
Var Int User_TP; //TrailPosition

reactiontime 20
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
TNT1 A 0 A_SpawnItemEx("GatlingGunTrail",0,0,0,momx,momy,momz,0,9)
Spawn3:
TNT1 A 0 A_CountDown
TNT1 A 1 
Goto Spawn2
Death:
TNT1 A 1 A_Spawnitemex("BlasterPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}


actor GatlingGunTrail : ExplosionBase
{
Obituary "$OB_GATLINGGUN"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 1 
stop
}
}

actor GatlingGunTrailX : FXActor_B
{
scale 1.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
BSTG A 1
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor GatlingGunTrailX1 : FXActor_A
{
scale 1.5
Alpha 0.9
States
{
Spawn:
BUST A 0
BUST A 1
stop
}
}