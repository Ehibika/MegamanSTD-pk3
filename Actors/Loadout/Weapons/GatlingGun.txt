const float GATT_SPD = 2.6; 

actor GatlingGunWep : WepMasterBase
{
Tag "GatlingGun"
Weapon.AmmoUse 1
Weapon.AmmoGive 180

Obituary "$OB_GATLINGGUN"
inventory.icon "BASHI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "GatlingGun_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop
Ready:
Ready.Wep:
DW07 A 1 A_WeaponReady(WRF_ALLOWINPUTS)
"####" A 0 A_Jumpifinventory("GatlingGun_Ammo",1,"Ready.Core")
Goto Reload
//===============
Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
TNT1 A 0 A_TakeInventory("WeaponCharge",999)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,HeavyWep_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop
Fire:
"####" A 0 A_WeaponReady(WRF_FIRING) //Reset Aim from reload interupt
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"NoAmmo")
"####" A 0 A_PlaySoundEx("weapon/BasherShot","Weapon")
"####" A 0 A_Giveinventory("GattlingGunCST")
"####" A 0 A_Jumpifinventory("WeaponCharge",7,"Fire.MaxSpeed")
"####" A 0 A_Jumpifinventory("WeaponCharge",3,"Fire.Speed2")
"####" A 1 A_Jump(256,"Fire.Speed1")
goto Fire.Speed1

Fire.Speed1:
"####" B 1 A_FireCustomMissile("GatlingGunShot",fRandom(-(BST_SPD),(BST_SPD)),1,4,4,0,fRandom(-(BST_SPD),(BST_SPD)))
"####" CA 1 A_WeaponReady(WRF_FIRING)
"####" A 5 A_Giveinventory("WeaponCharge")
"####" A 1 A_Jump(256,"Fire.EndCycle")
goto Fire.EndCycle


Fire.Speed2:
"####" B 1 A_FireCustomMissile("GatlingGunShot",fRandom(-(BST_SPD),(BST_SPD)),1,4,4,0,fRandom(-(BST_SPD),(BST_SPD)))
"####" C 1 A_FireCustomMissile("GatlingGunShot",fRandom(-(GATT_SPD),(GATT_SPD)),0,4,4,0,fRandom(-(GATT_SPD),(GATT_SPD)))
"####" A 0 A_Giveinventory("WeaponCharge")
"####" A 4 A_WeaponReady(WRF_FIRING)
"####" A 1 A_Jump(256,"Fire.EndCycle")
goto Fire.EndCycle

Fire.MaxSpeed:
"####" B 1 A_FireCustomMissile("GatlingGunShot",fRandom(-(BST_SPD),(BST_SPD)),1,4,4,0,fRandom(-(BST_SPD),(BST_SPD)))
"####" CA 1 A_FireCustomMissile("GatlingGunShot",fRandom(-(GATT_SPD),(GATT_SPD)),0,4,4,0,fRandom(-(GATT_SPD),(GATT_SPD)))
"####" A 1 A_WeaponReady(WRF_FIRING)
"####" A 1 A_Jump(256,"Fire.EndCycle")
goto Fire.EndCycle

Fire.EndCycle:
"####" C 1 A_REfire
"####" C 1 A_TakeInventory("WeaponCharge",999)
"####" A 1 A_Jump(256,"Ready.Wep")
goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_TakeInventory("WeaponCharge",999)
"####" A 0 A_Jumpifinventory("GatlingGun_Ammo",1,"Ready")
Goto Reload

Reload.Hold:
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_TakeInventory("WeaponCharge",999)
"####" A 0 A_JumpIfNoAmmo("Reload.FirstLoop")
Goto Reload.loop

Reload.FirstLoop:
"####" A 17 A_WeaponReady(WRF_RELOADING) 
"####" A 0 A_PlaySoundEx("weapon/turretup","Weapon")
"####" A 0 ACS_NamedExecuteWithResult("WeaponReload",6)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
Reload.Loop:
"####" A 11 A_WeaponReady(WRF_CYCLERELOADING) 
"####" A 0 A_PlaySoundEx("weapon/turretup","Weapon")
"####" A 0 ACS_NamedExecuteWithResult("WeaponReload",6)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
goto Reload.Rise2

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor GatlingGunWep2 : GatlingGunWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor GatlingGun_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 180
Ammo.BackpackMaxAmount 300
}

actor GattlingGunCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 3
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor GatlingGunShot : fastProjectileBase
{
Obituary "$OB_GATLINGGUN"
DamageType "GatlingGunP"
Speed 80
Damage (5)
radius 3

height 3
scale 1.0
Var Int User_TrailCount;
Var Int User_TP; //TrailPosition

reactiontime 20
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
TNT1 A 0 A_SpawnItemEx("GatlingGunTrail",0,0,0,momx,momy,momz,0,9)
Spawn3:
TNT1 A 0 A_CountDown
TNT1 A 1 
Goto Spawn2
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_Spawnitemex("BlasterPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}


actor GatlingGunTrail : BasicExplosion
{
Obituary "$OB_GATLINGGUN"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("GatlingGunTrailX",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 1 
stop
}
}

actor GatlingGunTrailX : FXActor_B
{
scale 1.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
AXLB A 1
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor GatlingGunTrailX1 : FXActor_A
{
scale 1.5
Alpha 0.9
States
{
Spawn:
BUST A 0
AXLB A 1
stop
}
}