actor RepairLauncherWep : WepMasterBase
{

Tag "RepairLauncher"
Weapon.AmmoUse 1
Weapon.AmmoGive 7
Obituary "$OB_REPAIRLAUNCHER"
inventory.icon "TAGMI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "RepairLauncher_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
0G_H A 1 A_WeaponReady(WRF_ALLOWRELOAD)
//0G_H B 0 A_FireCustomMissile("RepairLauncherAimPath",0,1,0,2,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 0 A_Jumpifinventory("RepairLauncher_Ammo",1,"Ready.Core")
Goto Reload
//===============

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
0G_H A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Launcher_MSPD*FVM)
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Launcher_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
0G_H A 1 A_Raise
Loop

Fire:
0G_H B 0 A_JumpIfNoAmmo("NoAmmo")
0G_H A 0 A_Jumpifinventory("ConShotTimer",1,"Fire.Buffer")
0G_H A 0 A_PlaySoundEx("weapon/AutoFirework","Weapon")
0G_H B 0 A_FireCustomMissile("RepairLauncherShot",0,1,0,2,0,(Aim_ArcOverHead/2)+(Aim_ArcScale*Sin(-pitch)))//

0G_H B 2 Offset(4,28)
0G_H C 2 Offset(8,24)
0G_H B 2 Offset(4,28)
0G_H A 0 A_Giveinventory("RepairLauncherCST",1)
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

Fire.Buffer:
"####" A 1 A_WeaponReady(WRF_NOPRIMARY|WRF_ALLOWRELOAD)
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_Jumpifinventory("RepairLauncher_Ammo",1,"Ready")
Reload:

"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 32 
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_Giveinventory("RepairLauncher_Ammo",7)
"####" A 1 A_Refire("Fire")
Goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor RepairLauncherWep2 : RepairLauncherWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor RepairLauncher_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 7
}

actor RepairLauncherCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 20
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const Int RepairLauncherAOE = 96;
Const int RGI_RepairLauncher = 2;

actor RepairLauncherAimPath : GolfBallAimInstantStarter3 { Speed 35 Gravity 1.5}

actor RepairLauncherShot : ProjectileBase
{
//+BOUNCEONWALLS
//+BOUNCEONCEILINGS
//+MBFBOUNCER
+THRUACTORS
-NOGRAVITY
radius 5
height 5
scale 2.5
Var int User_GravTime;
speed 64
gravity 1.5
bouncefactor 0.9
damagetype "Explosion"
Obituary "$OB_REPAIRLAUNCHER"
Damage (0)
wallbouncefactor 0.95
reactiontime 75
States
{
Spawn:
HBAL E 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
HBAL E 0 A_CountDown
0G_P AB 1 A_SpawnItemEX("RepairLauncherTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
Goto Spawn2

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
HBAL EE 0 A_Stop
HBAL E 0 A_SpawnItemEx("RepairLauncherHealer")
TNT1 A 0 A_SpawnItemEx("RepairLauncherImpactRing",0,0,0,0,0,0,0,1)
TNT1 A 1 A_SpawnItemEx("RepairLauncherBlastFX",0,0,0,0,0,0,0,1)
stop
}
}

actor RepairLauncherHealer : FastProjectileBase
{
    //PROJECTILE
    damagetype "RepairLauncher"
    Obituary "$OB_REPAIRLAUNCHER"
    +MISSILE
    +SKYEXPLODE
    +EXPLODEONWATER
    +RIPPER
    +SERVERSIDEONLY
    RenderStyle None
    Damage (0) 
    Radius 10
    Height 10
    speed 15 
    scale 2.5
    states
    {
        Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteAlways("STD_RadiusGive",0,RGI_RepairLauncher,RepairLauncherAOE,RG_TEAM)
        TNT1 A 5 
        stop    
    }
}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
// [+++]=============================[+++]
actor RepairLauncherBlastFX : BasicExplosion //now just the FX due to desynch
{
Scale 2.5
States
{
Spawn:
MINB A 0 
NAPC D 0 //A_Explode(35, 350, 1,0,36)
AQBA L 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
0G_P CDEFGHIJ 1
stop
}
}

actor RepairLauncherTrail : FXActor_A
{
scale 1.5
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 2
MINB IIIIII 1
MINB IIIIII 1 A_Fadeout
stop
}
}


actor RepairLauncherImpactRing : GravityHoldFX
{
	reactiontime 15
	var int user_angle;
	RenderStyle "Translucent"
	States
	{
		Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("ProjectileCodeSyncTarget",1)
		TNT1 A 1
		SpawnLoop:
		TNT1 A 0 A_SpawnItemEx("HealFX2", RepairLauncherAOE, 0, 24, 5, 0, 0, user_angle, SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SpawnItemEx("HealFX2", -RepairLauncherAOE, 0, 24, -5, 0, 0, user_angle, SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_SetUserVar("user_angle", user_angle + 12)
		TNT1 A 0 A_Countdown
		loop
		Death:
		TNT1 A 0 
		stop
	}
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor RadiusGiveItem_2 : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpifInventory("RepairLauncherhealDelay",1,"Nope")
TNT1 A 0 A_JumpifInventory("PoisonMark",1,"HealBlock")
//TNT1 A 0 HealThing(CallACS("HealRamp",5,10))
TNT1 A 0 ACS_NamedExecuteAlways("MSTD_HealPointer",0,0,CallACS("HealRamp",45,90))
TNT1 A 0 A_GiveInventory("ScrewShotHealFeedback",1)
TNT1 A 0 A_GiveInventory("RepairLauncherhealDelay",1)
stop
HealBlock:
TNT1 A 0 A_GiveInventory("DetoxLevel",6)
TNT1 A 0 A_GiveInventory("DetoxEfectSpawn",1)
TNT1 A 0 A_GiveInventory("RepairLauncherhealDelay",1)
TNT1 A 5
stop
Nope:
TNT1 A 0
Stop
}
}