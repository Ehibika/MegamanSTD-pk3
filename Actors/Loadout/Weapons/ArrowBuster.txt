/*=============================

*///===========================

actor ArrowBusterWep : WepMasterBase
{
Tag "Arrow Buster"
Weapon.AmmoUse 1
Weapon.AmmoGive 10

Obituary "$OB_ARROWBUSTER"
inventory.icon "XBOWI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "ArrowBuster_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop
Ready:
Ready.Wep:
FABU A 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" A 0 A_Jumpifinventory("ArrowBuster_Ammo",1,"Ready.Core")
Goto Reload

//===============
Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Buster_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop
Fire:
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" C 0 A_Jumpifinventory("isBot",1,"BotRefire")
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"NoAmmo")
"####" A 0 A_Jumpifinventory("RefireBuffer",1,"Hold")
"####" "#" 0 A_PlaySoundEx("weapon/HardBuster6","Weapon")
"####" "#" 0 A_PlaySoundEx("weapon/coilexplode", "SoundSlot6")
//"####" A 0 A_FireCustomMissile("ArrowBusterRound",0,1,4,4,0,0)

"####" B 0 A_FireCustomMissile("ArrowBusterStart",0,0,0,0)
"####" B 0 A_FireCustomMissile("ArrowBusterDamager",0,1,0,0)
"####" A 0 A_Giveinventory("ArrowBusterCST")
"####" BC 2
Hold:
"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOPRIMARY)
"####" "#" 0 A_Jumpif(CallACS("MSTD_CheckAbilityInput",1)==1,"User1")
"####" C 0 A_Jumpifinventory("isBot",1,"BotRefire")
"####" C 0 A_Refire
goto Ready.Wep

NoAmmo:
"####" A 1
//"####" A 0 A_Takeinventory("WepBloomItem",999)
"####" A 0 A_Jumpifinventory("ArrowBuster_Ammo",1,"Ready")
Reload:
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 32 
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_Giveinventory("ArrowBuster_Ammo",10)
"####" A 1 A_Refire("Fire")
goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor ArrowBusterWep2 : ArrowBusterWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor ArrowBuster_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 10
}

actor ArrowBusterCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 25
powerup.type "ConShotTimer"
}

actor ArrowBuster_HitCounter : Inventory
{
inventory.amount 1
inventory.maxamount 5
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const Int ArrowBusterRange = 200;

//#region -=-depreciated-=-
actor ArrowBusterRound : fastProjectileBase
{
Obituary "$OB_ARROWBUSTER"
DamageType "ArrowBuster"
Speed 128
Damage (65)
radius 8
height 8
scale 1.0
Var Int User_TrailCount;
Var Int User_TP; //TrailPosition


reactiontime 20
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
TNT1 A 0 A_SpawnItemEx("ArrowBusterTrail",0,0,0,momx,momy,momz,0,9)
Spawn3:
TNT1 A 0 A_CountDown
TNT1 A 1 
Goto Spawn2
Death:
TNT1 A 1 A_Spawnitemex("ArrowBusterPuff",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}
//#endregion

actor ArrowBusterDamager : FastProjectileBase
{
    PROJECTILE
    damagetype "ArrowBuster"
    Obituary "$OB_ARROWBUSTER"
    +SKYEXPLODE
    +EXPLODEONWATER
    +HITTRACER
    RenderStyle None
    Damage (30+CallACS("SniperBusterRamp",3,5,70))
    Radius 10
    Height 10
    speed 3000 //15 * ArrowBusterRange(200)
    scale 2.5
    states
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1 
        TNT1 A 0 A_GiveToTarget("ArrowBuster_HitCounter",1)
        TNT1 A 0 //A_Log("Miss 2")
        stop
    
    Death:
    TNT1 A 1 A_JumpIf(IsPointerEqual(AAPTR_TRACER, AAPTR_NULL) == TRUE, "NoTarg")
    TNT1 A 0 //A_Log("Hit")
    TNT1 A 0 A_GiveToTarget("ArrowBuster_HitCounter",1)   
    stop
    NoTarg:
    TNT1 A 0 A_TakeFromTarget("ArrowBuster_HitCounter",99)
    TNT1 A 0 //A_Log("Miss")
    stop
    }
}

actor ArrowBusterStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,15,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("ArrowBusterRay",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor ArrowBusterRay : FastProjectilebase//FastProj to match damager hitbox
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > ArrowBusterRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("ArrowBusterRay",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("ArrowBusterRayFX",0,0,0,0,0,0,0,1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 A_SpawnItemEx("ArrowBusterPuff",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("ArrowBusterPuff",0,0,0,0,0,0,0,1)
        stop
    }
}





//[+]========================================================================[+]
//--------------------------------|Effect Actors|-------------------------------
//[+]========================================================================[+]
/*
actor ArrowBusterPuff : FXActor_A
{
scale 1.5
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}
*/

actor ArrowBusterPuff : BasicClientside//BulletPuff
{
scale 2.5
DamageType "ArrowBuster"
Obituary "$OB_ARROWBUSTER"

alpha 1.0
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileFXCode",1)
TNT1 A 0 A_PlaySoundEx("weapon/Mirrordeflect","Weapon")
Hard BBCCDDEE 1 
stop
}
}

actor ArrowBusterTrail : BasicExplosion
{
Obituary "$OB_ARROWBUSTER"
damagetype "MegaBuster"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("ArrowBusterTrail_FX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("ArrowBusterTrail_FX2",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("ArrowBusterTrail_FX2",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("ArrowBusterTrail_FX3",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("ArrowBusterTrail_FX3",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 1 
stop
}
}



actor ArrowBusterTrail_FX1 : FXActor_B
{
scale 3.0
Alpha 0.9
States
{
Spawn:
BUST A 0
ARRS T 1
stop
}
}

actor ArrowBusterTrail_FX2 : FXActor_B
{
scale 3.0
Alpha 0.9
States
{
Spawn:
CRBM A 0
CRBM A 1
stop
}
}

actor ArrowBusterTrail_FX3 : ArrowBusterTrail_FX2
{scale 2.0}

actor ArrowBusterRayFX : BasicClientside
{
scale 2.0
renderstyle "add"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
MBEA A 3
MBEA AAAAA 2 A_Fadeout
stop
}
}