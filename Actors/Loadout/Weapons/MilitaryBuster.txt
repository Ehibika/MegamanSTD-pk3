/*=============================

*///===========================

actor MilitaryBusterWep : WepMasterBase
{
Tag "Military Buster"
Weapon.AmmoUse 1
Weapon.AmmoGive 15

Obituary "$OB_MILITARYBUSTER"
inventory.icon "BLASI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "MilitaryBuster_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop
Ready:
Ready.Wep:
LBUS A 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" A 0 A_Jumpifinventory("MilitaryBuster_Ammo",1,"Ready.Core")
Goto Reload

//===============
Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Buster_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop
Fire:
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"NoAmmo")
"####" A 0 A_Jumpifinventory("RefireBuffer",1,"Hold")
"####" A 0 A_PlaySoundEx("weapon/HardBuster4","Weapon")
"####" A 0 A_FireCustomMissile("MilitaryBusterRound",0,1,4,4,0,0)
"####" A 0 A_Giveinventory("MilitaryBusterCST")
"####" BC 1
Hold:
"####" A 1 A_WeaponReady(WRF_NOPRIMARY|WRF_ALLOWRELOAD)
"####" "#" 0 A_Jumpif(CallACS("MSTD_CheckAbilityInput",1)==1,"User1")
"####" A 0 A_Jumpifinventory("isBot",1,"BotRefire")
"####" A 0 A_Refire
goto Ready.Wep

NoAmmo:
"####" A 1
//"####" A 0 A_Takeinventory("WepBloomItem",999)
"####" A 0 A_Jumpifinventory("MilitaryBuster_Ammo",1,"Ready")
Reload:
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 32 
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_Giveinventory("MilitaryBuster_Ammo",15)
"####" A 1 A_Refire("Fire")
goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor MilitaryBusterWep2 : MilitaryBusterWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor MilitaryBuster_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 15
}

actor MilitaryBusterCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 7
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor MilitaryBusterRound : fastProjectileBase
{
Obituary "$OB_Military"
DamageType "MilitaryBuster"
Speed 96
Damage (32)
scale 1.0
Var Int User_TrailCount;
Var Int User_TP; //TrailPosition
radius 10
height 10
reactiontime 30
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
TNT1 A 0 A_SpawnItemEx("MilitaryTrail",0,0,0,momx,momy,momz,0,9)
Spawn3:
TNT1 A 0 A_CountDown
TNT1 A 1 
Goto Spawn2
Death:
TNT1 A 1 A_Spawnitemex("MilitaryPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Effect Actors|-------------------------------
//[+]========================================================================[+]
actor MilitaryPuff1 : FXActor_A
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileFXCode",1)
BIM1 AABCDEF 1
stop
}
}

actor MilitaryTrail : BasicExplosion
{
Obituary "$OB_MILITARYBUSTER"
damagetype "MegaBuster"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("MilitaryTrail_FX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("MilitaryTrail_FX",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("MilitaryTrail_FX",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("MilitaryTrail_FX",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("MilitaryTrail_FX",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
//TNT1 A 0 A_SpawnItemEx("LazerTagBeamFX3",0,0,0,-momx*0.5,-momy*0.5,-momz*0.5,0,9)
//TNT1 A 0 A_SpawnItemEx("MilitaryTrail_FX",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
//TNT1 A 0 A_SpawnItemEx("LazerTagBeamFX3",0,0,0,-momx*0.7,-momy*0.7,-momz*0.7,0,9)
//TNT1 A 0 A_SpawnItemEx("MilitaryTrail_FX",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)
//TNT1 A 0 A_SpawnItemEx("LazerTagBeamFX3",0,0,0,-momx*0.9,-momy*0.9,-momz*0.9,0,9)
TNT1 A 1 
stop
}
}

actor MilitaryTrail_FX : FXActor_B
{
scale 2.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
XSHO A 1
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor MilitaryTrail_FX1 : FXActor_A
{
scale 2.5
Alpha 0.9
States
{
Spawn:
BUST A 0
XSHO A 1
stop
}
}

