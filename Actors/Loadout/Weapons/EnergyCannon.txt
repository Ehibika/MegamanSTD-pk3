actor EnergyCannonWep : WepMasterBase
{

Tag "EnergyCannon"
Weapon.AmmoUse 1
Weapon.AmmoGive 20
Obituary "$OB_ENERGYCANNON"
inventory.icon "BLASI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "EnergyCannon_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
0G_H A 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" A 0 A_Jumpifinventory("EnergyCannon_Ammo",1,"Ready.Core")
Goto Reload
//===============

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
0G_H A 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Launcher_MSPD*FVM)
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Launcher_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
0G_H A 1 A_Raise
Loop

Fire:
0G_H B 0 A_JumpIfNoAmmo("NoAmmo")
0G_H A 0 A_Jumpifinventory("ConShotTimer",1,"Fire.Buffer")
0G_H A 0 A_PlaySoundEx("weapon/AutoFirework","Weapon")
0G_H B 0 A_FireCustomMissile("EnergyCannonShot",0,2,0,2)

0G_H B 2 Offset(4,28)
0G_H C 2 Offset(8,24)
0G_H B 2 Offset(4,28)
0G_H A 2 A_WeaponReady(WRF_NOPRIMARY)
0G_H A 0 A_Giveinventory("EnergyCannonCST",1)
/*
0G_H D 10 A_PlaySound("weapon/AutoReload1",4,1.0)
0G_H D 0 A_PlaySound("weapon/AutoReload2",1,0.75)
0G_H D 2 Offset(-6,44)
0G_H D 2 Offset(-6,44)
0G_H D 6 A_WeaponReady(14)
0G_H A 8 A_PlaySound("weapon/AutoReload3",1,0.75)
*/
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

Fire.Buffer:
"####" A 1
"####" A 0 A_Refire("Fire")
Goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_Jumpifinventory("EnergyCannon_Ammo",1,"Ready")
Reload:

"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 32 
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_Giveinventory("EnergyCannon_Ammo",20)
"####" A 1 A_Refire("Fire")
Goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor EnergyCannonWep2 : EnergyCannonWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor EnergyCannon_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 20
}

actor EnergyCannonCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 17
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor EnergyCannonShot : FastProjectileBase
{
Speed 48
Damage (35)
Obituary "$OB_ENERGYCANNON"
damagetype "EnergyCannon"
renderstyle "Translucent"
+HITTRACER
radius 6
Reactiontime 3
height 6
scale 4.5
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
CHBU ABCD 1 A_SpawnItemEx("EnergyCannonTrail",-80,0,-momz,momx,momy,momz,0,9)
TNT1 A 0 A_Countdown
loop

Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_SpawnItemEx("EnergyCannonImpactFX",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("EnergyCannonImpactFX2",0,0,0,0,0,0,0,1)
TNT1 A 0 A_Explode(10,128,1,0,128)
//TNT1 AAAAAAAAAAAA 1 A_SpawnItemEx("EnergyCannonTrailFX6", 0, 0, 0, 256/24, 256/24, random2(256/24), random(0, 359),1)
stop
}
}


actor EnergyCannonTrail : ExplosionBase
{
Obituary "$OB_ENERGYCANNON"
damagetype "EnergyCannon"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("EnergyCannonTrailFX1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("EnergyCannonTrailFX2",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("EnergyCannonTrailFX2",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("EnergyCannonTrailFX3",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("EnergyCannonTrailFX3",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)
TNT1 A 1 
stop
}
}


actor EnergyCannonTrailFX1 : BasicClientside
{
scale 2.5
renderstyle "Translucent"
Gravity 0.5
ReactionTime 8
States
{
Spawn:
TNT1 A 0
CHBU G 1 
stop
}
}


actor EnergyCannonTrailFX2 : EnergyCannonTrailFX1
{
States
{
Spawn:
TNT1 A 0
CHBU F 2 
stop
}
}

actor EnergyCannonTrailFX3 : EnergyCannonTrailFX1
{
States
{
Spawn:
TNT1 A 0
CHBU E 2 
stop
}
}


actor EnergyCannonTrailFX6 : BasicClientside
{
scale 2.5
renderstyle "Translucent"
Gravity 0.5
ReactionTime 8
States
{
Spawn:
TNT1 A 0
CHBU DEF 2 
stop
}
}

actor EnergyCannonImpactFX : BasicClientside
{
renderstyle "Translucent"
scale 3.5
Gravity 0.5
ReactionTime 8
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
ASEX BCDEF 1
stop
}
}

actor EnergyCannonImpactFX2 : BasicClientside
{
renderstyle "Translucent"
scale 3.5
Gravity 0.5
ReactionTime 8
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapon/crackerexplode", "Weapon")
MXP3 ABCDEFGHIJKLMN 1
stop
}
}