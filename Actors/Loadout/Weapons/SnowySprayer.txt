

actor SnowySprayerWep : WepMasterBase
{
Tag "SnowySprayer"
Weapon.AmmoUse 1
Weapon.AmmoGive 45
scale 2.0
Obituary "$OB_SnowySpray"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "SnowySprayerClip"
inventory.icon "FTONI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
FWPR B 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" B 0 A_Jumpifinventory("SnowySprayerClip",1,"Ready.Core")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Sprayer_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")

hold:
"####" B 0
"####" B 0 A_Jumpifinventory("ConShotTimer",1,"hold.Effect")
"####" B 0 A_Jump(256,"hold.Bullet")
Goto hold.Bullet
hold.Bullet:
"####" L 0 A_PlaySoundEx("weapon/waveburner","Weapon")
"####" B 0 A_FireCustomMissile("SnowySprayBurst",0,1,0,0)
"####" B 0 A_FireCustomMissile("SnowySprayDamagerStart",0,0,0,0)
"####" B 0 A_GiveInventory("SnowySprayerCST",1)
"####" C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
"####" B 0 A_Refire("hold")
"####" B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
goto Ready.Wep
hold.Effect:
"####" L 0 A_PlaySoundEx("weapon/waveburner","Weapon")
"####" B 0 A_FireCustomMissile("SnowySprayBurst",0,0,0,0)
"####" C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
"####" B 0 A_Refire("hold")
"####" B 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
goto Ready.Wep


NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("SnowySprayerClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 42 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("SnowySprayerClip",45)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor SnowySprayerWep2 : SnowySprayerWep
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor SnowySprayerClip : Ammo
{
inventory.amount 1
inventory.maxamount 45
+INVENTORY.IGNORESKILL
}

Actor SnowySprayhitDelay : powerprotection
{
Powerup.duration 3
Damagefactor "SnowySpray", 0
}

actor SnowySprayerCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 8
powerup.type "ConShotTimer"
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

Const Int SnowySprayRange = 20;
Const float SnowySpray_DMG = 6;

actor SnowySprayDamagerStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("SnowySprayDamager",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor SnowySprayDamager : FastProjectileBase
{
    PROJECTILE
    damagetype "SnowySpray"
    Obituary "$OB_SnowySpray"
    +SKYEXPLODE
    +EXPLODEONWATER
    +RIPPER
    +SERVERSIDEONLY
    RenderStyle None
    Damage (0) 
    Radius 10
    Height 10
    speed 15 
    scale 2.5
    states
    {
        Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > SnowySprayRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("SnowySprayDamager",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(SnowySpray_DMG,32,0,0,32)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 A_Explode(SnowySpray_DMG,32,0,0,32)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(SnowySpray_DMG,32,0,0,32)
        stop

        
    }
}

actor SnowySprayBurstStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("SnowySprayBurst",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor SnowySprayBurst : FastProjectileBase
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    +THRUACTORS
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > SnowySprayRange,"Death2")
        TNT1 A 0 A_JumpIf(Pitch > (SnowySprayRange/3)*2,"Spawn3")
        TNT1 A 0 A_JumpIf(Pitch > (SnowySprayRange/3),"Spawn2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("SnowySprayBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_SpawnItemEx("SnowySprayFx1",random(-5,5),random(-5,5),random(-5,5),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Spawn2:
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("SnowySprayBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_SpawnItemEx("SnowySprayFx2",random(-5,5),random(-5,5),random(-5,5),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Spawn3:
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("SnowySprayBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_SpawnItemEx("SnowySprayFX_A",random(-15,15),random(-15,15),random(-15,15),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_SpawnItemEx("SnowySprayFx3",random(-5,5),random(-5,5),random(-5,5),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 //A_SpawnItemEx("SnowySprayEndFX",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 //A_SpawnItemEx("SnowySprayEndFX",0,0,0,0,0,0,0,1)
        stop
    }
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

//const float SSNP_A = 0.2;

actor SnowySprayFx1  
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
height 2
Radius 2
damage (0)
reactiontime 1
scale 1.5
RenderStyle "Translucent"
Alpha 0.8
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
ICMS A 0 acs_NamedExecuteWithResult("SprayerAlpha",0.2*FVM)
ICMS AB 2 //A_Fadein(0.1)
ICMS C 0 
stop
}
}

actor SnowySprayFx2 : SnowySprayFx1
{
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
ICMS A 0 acs_NamedExecuteWithResult("SprayerAlpha",0.2*FVM)
ICMS CD 2 //A_Fadein(0.1)
ICMS C 0 
stop
}
}

actor SnowySprayFx3 : SnowySprayFx1
{
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
ICMS A 0 acs_NamedExecuteWithResult("SprayerAlpha",0.2*FVM)
ICMS EF 2 //A_Fadein(0.1)
ICMS C 0 
stop
}
}

actor SnowySprayFX_A : FXActor_A
{
scale 1.5
Translation "192:192=90:90", "196:196=88:88", "198:198=94:94"
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(192,"End")
ICMS A 0 acs_NamedExecuteWithResult("SprayerAlpha",0.2*FVM)
TMPK AABBCCCD 1 
End:
TNT1 A 1
stop
}
}
