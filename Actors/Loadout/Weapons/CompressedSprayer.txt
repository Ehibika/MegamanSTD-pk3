

actor CompressedSprayerWep : WepMasterBase
{
Tag "CompressedSprayer"
Weapon.AmmoUse 1
Weapon.AmmoGive 6
scale 2.0
Obituary "$OB_COMPRESSEDSPRAYER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "CompressedSprayer_Ammo"
inventory.icon "FTONI"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop

Ready:
Ready.Wep:
FWPR B 1 A_WeaponReady(WRF_ALLOWRELOAD)
"####" B 0 A_Jumpifinventory("CompressedSprayer_Ammo",1,"Ready.Core")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,Sprayer_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/napalm","Weapon")
"####" B 0 A_FireCustomMissile("CompressedSprayerBurst",0,1,0,0)
"####" B 0 A_FireCustomMissile("CompressedSprayerDamagerStart",0,0,0,0)
"####" B 0 A_FireCustomMissile("CompressedBlastRecoiler",0,0,0,0)
"####" C 2 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
"####" B 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOPRIMARY)
goto Ready.Wep

NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("CompressedSprayer_Ammo",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 42 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("CompressedSprayer_Ammo",6)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready.Wep
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor CompressedSprayerWep2 : CompressedSprayerWep
{Weapon.SlotNumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor CompressedSprayer_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 6
+INVENTORY.IGNORESKILL
}


Actor CompressedSprayhitDelay : powerprotection
{
Powerup.duration 3
Damagefactor "CompressedSpray", 0
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

Const Int CompressedSprayerRange = 15;
Const Int CompressedSprayerFXRange = 10;
Const float CompressedSprayer_DMG = 20;

actor CompressedSprayerDamagerStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("CompressedSprayerDamager",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor CompressedSprayerDamager : FastProjectileBase
{
    PROJECTILE
    damagetype "CompressedSpray"
    Obituary "$OB_CompressedSprayer"
    +SKYEXPLODE
    +EXPLODEONWATER
    +RIPPER
    +SERVERSIDEONLY
    RenderStyle None
    Damage (0) 
    Radius 10
    Height 10
    speed 15 
    scale 2.5
    states
    {
        Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch == CompressedSprayerRange%5,"Spawn.Pause")
        TNT1 A 0 A_JumpIf(Pitch > CompressedSprayerRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CompressedSprayerDamager",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(CompressedSprayer_DMG,32,0,0,32)
        TNT1 A 1
        stop
        Spawn.Pause:
        TNT1 A 1 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CompressedSprayerDamager",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(CompressedSprayer_DMG,32,0,0,32)
        TNT1 A 1
        stop

    Death:
        TNT1 A 0 A_Explode(CompressedSprayer_DMG,32,0,0,32)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_Explode(CompressedSprayer_DMG,32,0,0,32)
        stop

        
    }
}

actor CompressedSprayerBurstStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 2
    Height 2

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("CompressedSprayerBurst",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor CompressedSprayerBurst : FastProjectileBase
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    +THRUACTORS
    RenderStyle None
    Damage (0)
    Radius 10
    Height 10
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > CompressedSprayerFXRange,"Death2")
        TNT1 A 0 A_JumpIf(Pitch > (CompressedSprayerFXRange/3)*2,"Spawn3")
        TNT1 A 0 A_JumpIf(Pitch > (CompressedSprayerFXRange/3),"Spawn2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CompressedSprayerBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 AAA 0 A_SpawnItemEx("CompressedSprayerFx1",random(-5,5),random(-5,5),random(-5,5),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Spawn2:
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CompressedSprayerBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        //TNT1 AAA 0 A_SpawnItemEx("CompressedSprayerFX_A",random(-30,30),random(-30,30),random(-30,30),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 AAA 0 A_SpawnItemEx("CompressedSprayerFx2",random(-5,5),random(-5,5),random(-5,5),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Spawn3:
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("CompressedSprayerBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 AAA 0 A_SpawnItemEx("CompressedSprayerFx3",random(-15,15),random(-15,15),random(-15,15),momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION)
        TNT1 A 0 A_Stop
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 //A_SpawnItemEx("CompressedSprayerEndFX",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 //A_SpawnItemEx("CompressedSprayerEndFX",0,0,0,0,0,0,0,1)
        stop
    }
}


actor CompressedBlastRecoiler : FastProjectileBase
{
reactiontime 1
damage (0)
radius 3
height 3
speed 128
States
{
Spawn:
TNT1 A 0
TNT1 A 1 //A_CountDown
stop
Death:
TNT1 A 0 A_SpawnItemEx("CompressedBlastSource",0,0,0,0,0,0,0,1)
stop
}
}

actor CompressedBlastSource : ExplosionBase //
{
States
{
Spawn:
MINB A 0 
MINB A 0 ACS_NamedExecuteAlways("BLASTKNOCKBACK",0,90,60,220)
TNT1 AA 1 
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]

//const float SSNP_A = 0.2;

actor CompressedSprayerFx1  
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
height 2
Radius 2
damage (0)
reactiontime 1
scale 2.5
RenderStyle "ADD"
Alpha 0.8
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
MXP1 ED 1 
MXP1 D 0 A_Fadeout(0.4)
//MXP1 D 0 A_ScaleVelocity(0.5)
Spawn2:
MXP1 D 5 A_Fadeout(0.15)
loop
}
}

actor CompressedSprayerFx2 : CompressedSprayerFx1
{
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
TNT1 A 2
MXP1 CB 1 
MXP1 D 0 A_Fadeout(0.4)
//MXP1 D 0 A_ScaleVelocity(0.5)
Spawn2:
MXP1 B 5 A_Fadeout(0.15)
loop
}
}

actor CompressedSprayerFx3 : CompressedSprayerFx1
{
States
{
Spawn:
ICMS A 0// Thrustthingz(0,30,0,0)
TNT1 A 4
MXP1 BA 1 
MXP1 D 0 A_Fadeout(0.4)
//MXP1 D 0 A_ScaleVelocity(0.5)
Spawn2:
MXP1 A 5 A_Fadeout(0.15)
loop
}
}

actor CompressedSprayerFX_A : FXActor_A
{
scale 1.0
Translation "192:192=90:90", "196:196=88:88", "198:198=94:94"
speed 7
States
{
Spawn:
TNT1 A 0
TNT1 A 5
DDIG CCCC 3 A_Fadeout
stop
}
}
