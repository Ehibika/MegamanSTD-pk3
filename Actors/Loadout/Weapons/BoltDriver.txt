Const float BT_SprAng = 2.6;
Const float BT_SprAngRev = SRev-BT_SprAng;

Const float BT_SprD_Ang = BT_SprAng/1.5;
Const float BT_SprD_AngRev = SRev-(BT_SprAng/1.5);

const float BT_SPD = 0.8; 


actor BoltDriverWep : WepMasterBase
{
Tag "BoltDriver"
Weapon.AmmoUse 1
Weapon.AmmoGive 6

Obituary "$OB_BOLTDRIVER"
inventory.icon "BLITI"
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "BoltDriver_Ammo"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
Var int User_Spread;
States
{
Spawn:
WEAP X 1
stop
Ready:
Ready.Wep:
PIDB A 1 A_WeaponReady(WRF_ALLOWINPUTS)
"####" A 0 A_Jumpifinventory("BoltDriver_Ammo",1,"Ready.Core")
Goto Reload
//===============
Deselect:
TNT1 A 0 A_TakeInventory("NoClipBar",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop
Select:
TNT1 B 0 ACS_NamedExecuteAlways("MSTD_MedivisionScript",0,1)
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,LightWep_MSPD*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" A 1 A_Raise
Loop
Fire:
/*

"####" A 0 A_FireCustomMissile("BoltDriverShot",fRandom(-(BT_SPD),(BT_SPD)),1,4,4,0,fRandom(-(BT_SPD),(BT_SPD)))
"####" A 0 A_FireCustomMissile("BoltDriverShot",-12+fRandom(-(BT_SPD),(BT_SPD)),0,4,4,0,fRandom(-(BT_SPD),(BT_SPD)))
"####" A 0 A_FireCustomMissile("BoltDriverShot",12+fRandom(-(BT_SPD),(BT_SPD)),0,4,4,0,fRandom(-(BT_SPD),(BT_SPD)))
"####" A 0 A_FireCustomMissile("BoltDriverShot",-24+fRandom(-(BT_SPD),(BT_SPD)),0,4,4,0,fRandom(-(BT_SPD),(BT_SPD)))
"####" A 0 A_FireCustomMissile("BoltDriverShot",24+fRandom(-(BT_SPD),(BT_SPD)),0,4,4,0,fRandom(-(BT_SPD),(BT_SPD)))
*/
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" A 0 A_Jumpifinventory("ConShotTimer",1,"NoAmmo")
"####" A 0 A_PlaySoundEx("enemy/hammerjoethrow","Weapon")
"####" A 0 A_FireCustomMissile("BoltDriverShot",0,2,0,8)
"####" A 0 A_FireCustomMissile("BoltDriverShot",BT_SprAng,0,0,8,0,0)
"####" A 0 A_FireCustomMissile("BoltDriverShot",BT_SprD_Ang,0,0,8,0,BT_SprD_Ang)
"####" A 0 A_FireCustomMissile("BoltDriverShot",0,0,0,8,0,BT_SprAng)
"####" A 0 A_FireCustomMissile("BoltDriverShot",BT_SprD_Ang,0,0,8,0,BT_SprD_AngRev)
"####" A 0 A_FireCustomMissile("BoltDriverShot",BT_SprAngRev,0,0,8,0,0)
"####" A 0 A_FireCustomMissile("BoltDriverShot",BT_SprD_AngRev,0,0,8,0,BT_SprD_AngRev)
"####" A 0 A_FireCustomMissile("BoltDriverShot",0,0,0,8,0,BT_SprAngRev)
"####" A 0 A_FireCustomMissile("BoltDriverShot",BT_SprD_AngRev,0,0,8,0,BT_SprD_Ang)

//--
"####" A 0 A_Giveinventory("BoltDriverCST")
"####" BC 2 
"####" A 4 
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 0 A_PlaySoundEx("weapon/adapterreturn","Weapon")
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 2  A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH)
"####" C 0 A_REfire
goto Ready.Wep

NoAmmo:
"####" A 1
"####" A 0 A_Jumpifinventory("BoltDriver_Ammo",1,"Ready")
Goto Reload

Reload.Hold:
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_JumpIfNoAmmo("Reload.FirstLoop")
Goto Reload.loop

Reload.FirstLoop:
"####" A 17 A_WeaponReady(WRF_STARTRELOADING) 
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_Giveinventory("WeaponSingleReloader",1)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
Reload.Loop:
"####" A 10 A_WeaponReady(WRF_CYCLERELOADING) 
"####" A 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" A 0 A_Giveinventory("WeaponSingleReloader",1)
"####" A 1 A_JumpIf(CallACS("CheckAmmoFull")==0,"Reload.Loop")
"####" A 0 A_PlaySoundEx("weapon/Crashland","Weapon")
goto Reload.Rise2

Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

actor BoltDriverWep2 : BoltDriverWep
{Weapon.slotnumber 2}
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]

actor BoltDriver_Ammo : Ammo
{
inventory.amount 1
inventory.maxamount 6
Ammo.BackpackMaxAmount 8
}

actor BoltDriverCST : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 22
powerup.type "ConShotTimer"
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
Const int BoltDriver_MinHeal = 11;
Const int BoltDriver_MaxHeal = 20;

actor BoltDriverShot : fastProjectileBase
{
Obituary "$OB_BOLTDRIVER"
DamageType "BoltDriver"
Speed 160
Damage (10)
radius 3

MissileHeight 16
height 3
scale 1.0
Var Int User_TrailCount;
Var Int User_TP; //TrailPosition
    +HITTRACER
    -MTHRUSPECIES
reactiontime 20
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
TNT1 A 0 A_SpawnItemEx("BoltDriverTrail",0,0,0,momx,momy,momz,0,9)
Spawn3:
TNT1 A 0 A_CountDown
TNT1 A 1 
Goto Spawn2
    Death:
    TNT1 A 0 A_JumpIf(Callacs("HealerShotIdentify")==1,"TeamHeal")
    AQBA L 0 A_GiveInventory("ProjectileDCode",1)
    TNT1 A 1 A_Spawnitemex("BlasterPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
    stop

    TeamHeal:
    TNT1 A 1 A_Spawnitemex("BoltDriverHealer",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_TRANSFERPOINTERS)
    stop
}
}

actor BoltDriverHealer : ProjectileBase
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+RIPPER
+NOINTERACTION
reactiontime 60
damage (0)
radius 3
height 3
scale 3.0
speed 0
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_JumpIf(IsPointerEqual(AAPTR_TRACER,AAPTR_NULL) == False, "Heal")
Goto Death

Heal:
TNT1 A 0 A_JumpIfinventory("CannotUseHealing",1, "Death",AAPTR_TRACER)
TNT1 A 0 A_JumpifInventory("PoisonMark",1,"HealBlock",AAPTR_TRACER)
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"Death",AAPTR_TRACER)
TNT1 A 0 A_SetArg(0,CallACS("HealRamp",BoltDriver_MinHeal,BoltDriver_MaxHeal,AAPTR_TRACER))
//TNT1 A 0 A_SetArg(1,CallACS("DamageRate",20,50,10))
TNT1 A 0 ACS_NamedExecuteAlways("MSTD_Basic(Over)Heal",0,AAPTR_TRACER,args[0]-args[1],50)
TNT1 A 0 A_GiveInventory("BoltDriverHealFeedback",1,AAPTR_TRACER)
TNT1 A 5
Goto Death
HealBlock:
TNT1 A 0 A_GiveInventory("DetoxLevel",15,AAPTR_TRACER)
TNT1 A 0 A_GiveInventory("DetoxEfectSpawn",1,AAPTR_TRACER)
TNT1 A 5
stop

Death:
TNT1 A 0 
stop
}
}


actor BoltDriverTrail : BasicExplosion
{
Obituary "$OB_BOLTDRIVER"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("BoltDriverTrailX",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("BoltDriverTrailX1",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("BoltDriverTrailX1",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("BoltDriverTrailX2",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("BoltDriverTrailX3",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)
TNT1 A 1 
stop
}
}

actor BoltDriverTrailX : FXActor_A
{
scale 2.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
RSS1 I 1
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor BoltDriverTrailX1 : FXActor_B
{
scale 1.5
Alpha 0.9
States
{
Spawn:
BUST A 0
HEAL A 1
stop
}
}

actor BoltDriverTrailX2 : BoltDriverTrailX1
{
States
{
Spawn:
BUST A 0
HEAL B 1
stop
}
}

actor BoltDriverTrailX3 : BoltDriverTrailX1
{
States
{
Spawn:
BUST A 0
HEAL C 1
stop
}
}

actor BoltDriverHealFeedback : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 AAAA 0 A_SpawnItemEx("HealFX3",random(-24,24),random(-32,32),random(30,34),0,0,Random(1,4))
TNT1 A 0 A_PlaySoundEx("item/energyup","SoundSlot7")
TNT1 A 0
stop

Nope:
TNT1 A 0
stop
}
}