/*=============================
if gravity free explosives are somethng you need, look no further than the
Neumodian quallity Wrecker. this beast of a weapon has a 4 rocket clip and
can fire all of them in one rapid volley! when you need to lay on the pain against
your opponents than this weapon won't dissappoint.

whatever you do, it's highly discouraged that you shoot one of the rockets at
your own feet.

*///===========================

actor Wrecker : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 4
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Obituary "$OB_BAZZOKA"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "WreckerRoundClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 16 A_WeaponReady
"####" B 0 A_jumpifinventory("WreckerRoundClip",1,"Ready")
Goto Reload
"####" A 0 A_PlaySound("weapon/rminestick")
"####" B 0 A_GiveInventory("WreckerRoundClip")
Goto Ready
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 Offset(0,32)
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" C 0
"####" B 0 A_Giveinventory("BlastProtection")
"####" B 0 A_PlaySoundEx("weapon/Magnetmissile","Weapon")
"####" B 0 A_GiveInventory("WreckerRoundShooter")
"####" CDD 4
"####" B 15 A_WeaponReady(WRF_NOPRIMARY)
"####" B 0 A_Refire
goto Ready
NoAmmo:
"####" B 1
"####" B 0 A_jumpifinventory("WreckerRoundClip",1,"Ready")
"####" B 0
Reload:
"####" B 0 Offset(0,32)
"####" B 2 //A_WeaponReady(WRF_NOBOB)
"####" B 0 Offset(0,46)
"####" B 2 //A_WeaponReady(WRF_NOBOB)
"####" B 0 Offset(0,58)
"####" B 2// A_WeaponReady(WRF_NOBOB)
"####" B 0 Offset(0,70)
"####" B 2 //A_WeaponReady(WRF_NOBOB)
"####" B 0 Offset(0,82)
"####" B 2 //A_WeaponReady(WRF_NOBOB)
ReloadLoop:
"####" B 0 A_PlaySound("weapon/rminestick")
"####" B 0 A_GiveInventory("WreckerRoundClip")
//"####" B 0 A_jumpifinventory("WreckerRoundClip",1,"ReloadEnd")
"####" B 35 //A_WeaponReady(WRF_NOBOB)
//Goto ReloadLoop
//ReloadEnd:
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor WreckerRoundClip : Ammo
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.IGNORESKILL
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor WreckerRound : ProjectileBase
{
Obituary "%o got Struck  by %k's Assault missile!"
Speed 65
Damage (20 + ACS_ExecuteWithResult(DamageRate,35,300,20))
Damagetype "Explosion"
radius 6
height 5
scale 4.5
States
{
Spawn:
TNT1 A 0
AMIS A 1 
TNT1 A 0 A_Spawnitemex("CrisperizerTrail",-20,0,0,VELX/2,VELY/2,VELZ/2,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
goto spawn+1
Crash:
AMIS A 1 A_Spawnitemex("WreckerRoundBlast",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
XDeath:
AMIS A 1 A_Spawnitemex("WreckerRoundFakeBlast")
stop
Death:
AMIS A 1 A_Spawnitemex("WreckerRoundBlast",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
//AMIS A 0 A_CheckFloor("JumpCheck")
//stop
JumpCheck:
AMIS A 0 A_Jumpifcloser(140,"blastjump")
stop
//---
blastjump:
MINB A 0 ACS_ExecuteAlways(BlastKnockback,0,80,185)
TNT1 A 5
//AMIS A 0 A_GiveToTarget("BlastJumperItem")
//AMIS A 0 A_GiveToTarget("BlastJumperItem2")
stop
}
}

	actor WreckerRound_RED : WreckerRound // standard
	{
	translation "192:192=160:160", "198:198=42:42", "196:196=215:215", "197:197=226:226"
	}
	
	actor WreckerRound_BLUE : WreckerRound // standard
	{
	}
	
actor WreckerRoundBlast : ExplosionBase
{
States
{
Spawn:
BOMB B 0
BOMB B 0 A_PlaySoundEx("weapon/Wrecker","Weapon")
BOMB B 1 A_Explode(15,200,1,0,100)
BOMB B 0 A_SpawnitemEx("WreckerBombProjectile",0,0,20,random(10,30),0,10,72,SXF_TRANSFERTRANSLATION)
BOMB B 0 A_SpawnitemEx("WreckerBombProjectile",0,0,20,random(10,30),0,10,144,SXF_TRANSFERTRANSLATION)
BOMB B 0 A_SpawnitemEx("WreckerBombProjectile",0,0,20,random(10,30),0,10,216,SXF_TRANSFERTRANSLATION)
BOMB B 0 A_SpawnitemEx("WreckerBombProjectile",0,0,20,random(10,30),0,10,288,SXF_TRANSFERTRANSLATION)
BOMB B 0 A_SpawnitemEx("WreckerBombProjectile",0,0,20,random(10,30),0,10,360,SXF_TRANSFERTRANSLATION)
ASEX ABCDDEEF 1 
stop
}
}


actor WreckerRoundFakeBlast : ExplosionBase //when hitting shields
{
States
{
Spawn:
BOMB B 0
BOMB B 0 A_PlaySoundEx("weapon/Wrecker","Weapon")
ASEX ABCDDEEF 1 
stop
}
}


actor WreckerBombProjectile 
{
PROJECTILE
damagetype "Explosion"
Obituary "$OB_WreckerBOMB"
radius 11
height 20
scale 2.5
-NOGRAVITY
+DOOMBOuNCE
+MTHRUSPECIES
-ALLOWBOUNCEONACTORS
-BounceOnFloors
+NOTARGETSWITCH
+THRUACTORS
speed 50
gravity 2.2
Damage (0)
wallbouncefactor 0.6
reactiontime 55
States
{
Spawn:
MINB A 0
MINB A 0 ThrustThingZ(0, 25, 0, 1)
MINB A 0 
MINB ABCDEFGH 1 A_SpawnItemEX("MiniBombTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
Goto Spawn+2
Death:
MINB A 0 A_SpawnItemEx("WreckerBombExplosion")
stop
Crash:
MINB A 0 A_SpawnItemEx("WreckerBombExplosion")
stop
XDeath:
MINB A 0 A_SpawnItemEx("WreckerBombFakeExplosion")
stop
}
}

	actor WreckerBombProjectile_BLUE : WreckerBombProjectile{}
	actor WreckerBombProjectile_RED : WreckerBombProjectile{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170"}
	
	// [+++]=============================[+++]
actor WreckerBombExplosion : ExplosionBase
{
States
{
Spawn:
NAPC A 0 
NAPC D 0 A_Explode(35, 280, 1,0,36)
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}

actor WreckerBombFakeExplosion : ExplosionBase
{
States
{
Spawn:
NAPC A 0 
NAPC A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
ASEX ABCDEF 1 
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor WreckerRoundTrail 
{
PROJECTILE
+NOINTERACTION
+DONTBLAST
+CLIENTSIDEONLY
Speed 0
radius 5
height 5
scale 1.5
Renderstyle "Translucent"
Alpha 0.7
States
{
Spawn:
ICMS A 0
ICMS AAABBCC 1
ICMS DDDDDD 1
ICMS EF 1 A_Fadeout
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor WreckerRoundShooter : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("WreckerRound",0,1,8,0)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("WreckerRound_RED",0,1,8,0)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("WreckerRound_BLUE",0,1,8,0)
TNT1 A 1
stop
}
}