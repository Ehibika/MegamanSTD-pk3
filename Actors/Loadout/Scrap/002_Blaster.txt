/*=============================
For those seeking more boom for their buck, meet the Blaster, this high powered
firearm launches a single large and strong round with each pull of the trigger.
while it has to reload each round one by one, it's potentially fast fire rate and
considerable power makes this weapon a must have for quick do-or-die confrontations.

*///===========================

actor Blaster : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 6
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "BlasterClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_jumpifinventory("BlasterClip",6,"Ready")
Goto Reload
"####" A 0 A_PlaySound("weapon/rminestick")
"####" B 0 A_GiveInventory("BlasterClip")
Goto Ready
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 Offset(0,32)
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" C 0
"####" B 0 A_PlaySoundEx("weapon/pshot","Weapon")
"####" B 0 A_GiveInventory("BlasterShooter")
"####" CDD 1
Hold:
"####" B 1
"####" B 1 A_Refire("Hold")
"####" B 17 A_WeaponReady
goto Ready
NoAmmo:
"####" B 1
"####" B 0 A_jumpifinventory("BlasterClip",1,"Ready")
"####" B 16
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
ReloadLoop:
"####" B 0 A_PlaySound("weapon/rminestick")
"####" B 0 A_GiveInventory("BlasterClip")
"####" B 0 A_jumpifinventory("BlasterClip",6,"ReloadEnd")
"####" B 17 A_WeaponReady(WRF_NOSWITCH|WRF_NOBOB)
Goto ReloadLoop
ReloadEnd:
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor BlasterClip : Ammo
{
inventory.amount 1
inventory.maxamount 6
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor BlasterShot : fastProjectileBase
{
Speed 115
Damage (8 + ACS_ExecuteWithResult(DamageRate,16,150,90))
radius 14
height 28
scale 2.2
States
{
Spawn:
TNT1 A 0 
TNT1 A 0
Spawn2:
CRBM A 0 
CHBU ABCD 2
Loop
Death:
CHBU A 1 A_Spawnitemex("BlasterPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor BlasterShot_BLUE : BlasterShot
	{
	}

	actor BlasterShot_RED : BlasterShot
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}
	
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor BlasterPuff1 : FXActor_A
{
scale 4
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor BlasterShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("BlasterShot",0,1,4,8)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("BlasterShot_RED",0,1,4,8)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("BlasterShot_BLUE",0,1,4,8)
TNT1 A 1
stop
}
}