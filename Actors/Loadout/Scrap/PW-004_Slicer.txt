/*=============================

The slicer may look like one of those toy disk launchers but it's anything but
childsplay, harnessing the power of earth ninjas, which to our reports are 
supposedly unbetable by anything other than themselves; this gun can launch fast
moving suriken blades one by one! they are affected by gravity so keep that in 
mind.

*///===========================

actor Slicer : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 14
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Obituary "$OB_SLICER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "SlicerClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
"####" B 0 A_Jumpifinventory("SlicerClip",1,"Ready")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/rollingcutter","Weapon")
"####" B 0 A_GiveInventory("SlicerShooter")
"####" B 1 
/*
"####" B 0 A_GiveInventory("SlicerShooter")
"####" B 2 
"####" B 0 A_GiveInventory("SlicerShooter")
"####" B 2 
"####" B 0 A_GiveInventory("SlicerShooter")
*/
"####" D 2 A_WeaponReady(WRF_NOPRIMARY)
Hold:
"####" B 1 
"####" C 0 A_REfire
goto Ready
NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("SlicerClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 22 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("SlicerClip",12)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor SlicerClip : Ammo
{
inventory.amount 1
inventory.maxamount 5
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor SlicerShot : ProjectileBase
{
Speed 75
Damage (10 + ACS_ExecuteWithResult(DamageRate,25,350,15))
radius 5

height 5
Gravity 2.3
scale 1.5
States
{
Spawn:
TNT1 A 0 
SLCR A 2 A_Spawnitemex("SlicerTrail_1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
SLCR B 2 A_Spawnitemex("SlicerTrail_2",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
SLCR A 2 A_Spawnitemex("SlicerTrail_1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
SLCR B 2 A_Spawnitemex("SlicerTrail_2",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_CHANGEFLAG("NOGRAVITY",0)
Spawn2:
SLCR A 2 A_Spawnitemex("SlicerTrail_1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
SLCR B 2 A_Spawnitemex("SlicerTrail_2",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Loop
Crash:
BSTG A 1 A_Spawnitemex("SlicerPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
Death:
TNT1 A 0 A_CHANGEFLAG("NOGRAVITY",1)
SLCR B 10
SLCR BZBZBZBZ 2
stop
}
}

	actor SlicerShot_BLUE : SlicerShot
	{
	}

	actor SlicerShot_RED : SlicerShot
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"
	}
	
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor SlicerPuff1 : FXActor_A
{
scale 2
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}

actor SlicerTrail_1 : FXActor_B
{
scale 1.4
Alpha 0.9
States
{
Spawn:
CRBM A 0
SLCR AAAAAA 2 A_Fadeout
stop
}
}

actor SlicerTrail_2 : FXActor_B
{
scale 1.4
Alpha 0.9
States
{
Spawn:
CRBM A 0
SLCR BBBBBB 2 A_Fadeout
stop
}
}


//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor SlicerShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("SlicerShot",0,1,4,4)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("SlicerShot_RED",0,1,4,4)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("SlicerShot_BLUE",0,1,4,4)
TNT1 A 1
stop
}
}