/*=============================
This Frightfull little dart gun packs poison filled darts to interfear with your
enemies ability to recover, it's neither the fastest nor the strongest but it's
utility greatly makes up for it!

*///===========================

actor Stinger : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Weapon.SlotNumber 2
Weapon.SelectionOrder 2
scale 2.0
Obituary "$OB_STINGER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "StingerClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BS2R B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BS2B B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
"####" B 0 A_Jumpifinventory("StingerClip",1,"Ready")
Goto Reload

Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/HardBuster1","Weapon")
"####" B 0 A_GiveInventory("StingerShooter")
"####" CCDD 1 

"####" B 10 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
"####" C 0 A_REfire
goto Ready

NoAmmo:
"####" B 1
"####" B 10 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
"####" B 0 A_Jumpifinventory("StingerClip",1,"Ready")
Reload:
"####" B 0 A_ZoomFactor(1.0)
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 24 A_WeaponReady(WRF_NOPRIMARY|WRF_NOBOB)
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("StingerClip",1)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}



//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor StingerClip : Ammo
{
inventory.amount 1
inventory.maxamount 1
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor StingerShot : fastProjectileBase
{
Speed 65
Damage (15 + ACS_ExecuteWithResult(DamageRate,10,300,150))
radius 6
height 10
scale 3.5
damagetype "Poisonous"
MissileType "PoisonTrail"
States
{
Spawn:
TNT1 A 0 
CRBM A 0 
TRIC A 1 A_Spawnitemex("CrossbowTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Loop
Death:
CRBM A 0 A_Stop
CRBM A 1 A_Spawnitemex("CrossbowPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 AAAA 2 A_Spawnitemex("PoisonedFX1",Random(-10,10),Random(-10,10),Random(-10,10),0,0,Random(1,3),0,SXF_TRANSFERTRANSLATION)
stop
}
}

actor StingerShot_BLUE : StingerShot
{MissileType "PoisonTrail_BLUE"}

actor StingerShot_RED : StingerShot
{translation "202:202=33:33","196:196=48:48", "197:197=18:18","192:192=54:54", "198:198=42:42" , "196:196=170:170"
MissileType "PoisonTrail_RED"}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor StingerPuff1 : FXActor_A
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("weapon/Mirrordeflect","Weapon")
BIM1 AABCDEF 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor StingerShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("StingerShot",0,1,4,8)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("StingerShot_RED",0,1,4,8)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("StingerShot_BLUE",0,1,4,8)
TNT1 A 1
stop
}
}