/*=============================
at times, enemies are just too numerous and defenses are just too tight. so what better way to
deal with those scenarios than a high caliber, timed explosive!
*///===========================
Actor TimeBombFlag : inventory {inventory.amount 1 inventory.maxamount 2}


actor TimeBombThrow
{
PROJECTILE
damagetype "HyperBomb"
Obituary "$OB_TIMEBOMB"
- NOGRAVITY
+THRUACTORS
+FORCEXYBILLBOARD
//+BOUNCEONWALLS
//+BOUNCEONCEILINGS
+GHOST
Radius 7
Height 20
scale 2.0
speed 25
Gravity 1.5
reactiontime 30
bouncefactor 0.7
States
{
Spawn:
TBOM A 0
TBOM A 0 ThrustThingZ(0, 32, 0, 1)
Spawn2:
TBOM A 3
loop
Death:
BOMB A 0 A_Spawnitemex("TimeBomb",0,0,10,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor TimeBombThrow_BLUE : TimeBombThrow{Translation "64:64=202:202","62:62=198:198" ,"192:192=192:192" ,"198:198=198:198"}
	actor TimeBombThrow_RED : TimeBombThrow{Translation "64:64=54:54","62:62=42:42" ,"192:192=42:42" ,"198:198=54:54"}


actor TimeBomb
{
damagetype "Explosion"
Obituary "$OB_TIMEBOMB"
+MISSILE
+NOGRAVITY
+NOTARGETSWITCH
+NOBLOODDECALS
+FORCEXYBILLBOARD
+SHOOTABLE
+BLOODSPLATTER
+GHOST
+NOEXPLODEFLOOR
Bloodtype "BombHit"
Radius 6
Height 10
scale 2.0
health 150
painchance 256
speed 0
Var int user_Timer;
States
{
Spawn:
TBOM A 0
TBOM A 0 A_Stop
TBOM A 0 A_PlaySoundEx("weapon/Crashland", "Weapon")
TBOM A 10 A_ChangeFlag("NOGRAVITY",1)
TBOM EDC 10 A_PlaysoundEX("weapon/BombCount","Soundslot6")
TBOM A 0 A_PlaysoundEX("weapon/BombTrigger","Soundslot6")
TBOM LBLBLBLB 2
BOMB A 0 A_SpawnItemEx("TimeBombExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
Stop
Death:
TNT1 A 1 
NAPC A 0 A_PlaySoundEx("misc/metdie","Soundslot5")
ASEX ABCDEF 1 
stop
}
}

actor TimeBombExplosion 
{
PROJECTILE
+NOINTERACTION
+NOCLIP
+DONTBLAST
+FORCEXYBILLBOARD
Speed 0
//
Damagetype "Explosion"
Obituary "$OB_TIMEBOMB"
radius 8
height 8
scale 5.5
States
{
Spawn:
NAPC A 0 
NAPC A 0 A_PlaySoundEx("weapon/wrapexplode","Soundslot5")
NAPC A 0 A_Explode(150,300,1,0,180)
MINB A 0 ACS_NamedExecuteAlways("BLASTKNOCKBACK",0,90,90,300)
ASEX ABCDEF 1 
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor BombHit
{
  Radius 1
  Height 1
  Scale 1.5
  Renderstyle Add
  +NOGRAVITY
  +NOINTERACTION
  States
  {
  Spawn:
    MMFX A 0 
    MMFX A 1  A_Stop
    MMFX A 5  A_PlaySoundex ("misc/metdie","soundslot6")
    stop
  }
}


actor TimeBombCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("TimeBombCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("TimeBombCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0
stop
}
}

actor TimeBombCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("TimeBombCooldownItem",210)
TNT1 A 0 A_Spawnitemex("TimeBombCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor TimeBombCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 210
}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor TimeBombShooter : CustomInventory  //places a buster posie based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 0 A_FireCustomMissile("TimeBombThrow",0,1,4,12)
stop
TeamRedFire:
BUST B 0 A_FireCustomMissile("TimeBombThrow_RED",0,1,4,12)
TNT1 A 0
stop
TeamBlueFire:
BUST B 0 A_FireCustomMissile("TimeBombThrow_BLUE",0,1,4,12)
TNT1 A 0
stop
}
}