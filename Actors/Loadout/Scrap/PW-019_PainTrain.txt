/*=============================
Another hefty member of the over 300lbs club, the painTrain is a missile volley weapon
that launches guided missiles that track your reticle while the trigger is held. on
release the missiles disengage tracking and rush forward and high speeds, taking anyone in 
their paths on a one way trip to fragsvill! destination: back to the spawn room!

Take care not to blow yourself up in the process though! 
*///===========================

actor PainTrain : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 25
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Obituary "$OB_PainTrain"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "PainTrainClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
"####" B 0 A_Jumpifinventory("PainTrainClip",1,"Ready")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 1 Offset(0,32)
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/Revup","Weapon")
"####" B 0 A_takeInventory("Reload_Trigger",999)
"####" B 0 A_takeInventory("WeaponCharge",999)
Revup:
"####" B 0 A_Jumpifinventory("WeaponCharge",25,"Fire1")
"####" B 1 A_Giveinventory("WeaponCharge",1)
"####" B 2 A_REfire("Revup")
"####" B 0 A_StopSoundEx("Weapon")
"####" B 0 A_takeInventory("Reload_Trigger",999)
"####" B 0 A_takeInventory("WeaponCharge",999)
goto Ready
Fire1:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/Rapidbuster","Weapon")
"####" BCD 2 
"####" B 0 A_GiveInventory("PainTrainShooter")
"####" D 3 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
"####" B 0 A_Giveinventory("shootingspeeddown",1)
//"####" B 0 A_takeInventory("PainTrainClip",1)

"####" C 0 A_REfire("Fire1")
"####" B 0 A_Giveinventory("Rush_Trigger",1)
"####" B 0 A_takeInventory("Reload_Trigger",999)
"####" B 0 A_takeInventory("WeaponCharge",999)
goto Ready
NoAmmo:
"####" B 1
"####" B 0 A_Giveinventory("Rush_Trigger",1)
"####" B 0 A_Jumpifinventory("PainTrainClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 5
Reloadloop:
"####" BB 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/turretup","Weapon")
"####" B 0 A_Giveinventory("PainTrainClip",1)
"####" B 0 A_Jumpifinventory("PainTrainClip",25,"Ready")
"####" B 0 A_Jumpifinventory("PainTrainClip",5,2)
"####" B 1 Offset(0,82)
Goto Reloadloop
"####" B 1 A_WeaponReady(WRF_NOBOB)
Goto Reloadloop

"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor PainTrainClip : Ammo
{
inventory.amount 1
inventory.maxamount 25
+INVENTORY.IGNORESKILL
}

Actor Rush_Trigger : Powerup {inventory.amount 1 powerup.duration 6}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]


actor PainTrainShot : ProjectileBase
{
Speed 20
Damage (5 + ACS_ExecuteWithResult(DamageRate,5,200,95))
radius 12

//MissileHeight 16
height 5
scale 2.1
States
{
Spawn:
TNT1 A 0 
PBUS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
Spawn1:
AMIS A 0 A_GiveToTarget("TargetMarker3", 1)
AMIS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
AMIS A 0 A_SpawnItemEX("BazookaRoundTrail",-35,0,0,-5,random(-3,3),3,0,SXF_TRANSFERTRANSLATION,0)
AMIS A 0 A_ChangeVelocity(Cos(pitch)*25, 0, -sin(pitch)*25,CVF_RELATIVE|CVF_REPLACE)
AMIS A 0 A_JumpifinTargetInventory("Rush_Trigger",1,"Rush")

AMIS A 0 A_GiveToTarget("TargetMarker3", 1)
AMIS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
AMIS A 0 A_ChangeVelocity(Cos(pitch)*25, 0, -sin(pitch)*25,CVF_RELATIVE|CVF_REPLACE)
AMIS A 0 A_JumpifinTargetInventory("Rush_Trigger",1,"Rush")

AMIS A 0 A_GiveToTarget("TargetMarker3", 1)
AMIS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
AMIS A 0 A_ChangeVelocity(Cos(pitch)*25, 0, -sin(pitch)*25,CVF_RELATIVE|CVF_REPLACE)
AMIS A 0 A_JumpifinTargetInventory("Rush_Trigger",1,"Rush")

AMIS A 0 A_GiveToTarget("TargetMarker3", 1)
AMIS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
AMIS A 0 A_ChangeVelocity(Cos(pitch)*25, 0, -sin(pitch)*25,CVF_RELATIVE|CVF_REPLACE)
AMIS A 0 A_JumpifinTargetInventory("Rush_Trigger",1,"Rush")
loop
Rush:
AMIS A 0 A_GiveToTarget("TargetMarker3", 1)
AMIS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
AMIS A 0 A_ChangeVelocity(Cos(pitch)*25, 0, -sin(pitch)*25,CVF_RELATIVE|CVF_REPLACE)
"####" A 0 A_PlaySoundEx("weapon/Magnetmissile","Weapon")
Rush2:
AMIS AA 1 A_ScaleVelocity(1.2)
AMIS AA 0 A_SpawnItemEX("BazookaRoundTrail",-35,0,0,-5,random(-3,3),3,0,SXF_TRANSFERTRANSLATION,0)
loop
Death:
AMIS A 1 A_Spawnitemex("PainTrainBlast",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
Crash:
AMIS A 1 A_Spawnitemex("PainTrainBlast")
stop
XDeath:
AMIS A 1 A_Spawnitemex("PainTrainFakeBlast")
stop
}
}

	actor PainTrainShot_BLUE : PainTrainShot
	{//MissileType "PainTrainTrail"
	}

	actor PainTrainShot_RED : PainTrainShot
	{//MissileType "PainTrainTrail_RED"
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"
	}
	


actor PainTrainBlast : ExplosionBase
{
scale 2.2
States
{
Spawn:
BOMB B 0
BOMB B 0 A_PlaySoundEx("weapon/Hyperbomb","Weapon")
BOMB B 1 A_Explode(15,120,1,0,50)
BOMB BCDEFG 1
BOMB HIJKL 1 A_Fadeout
stop
}
}


actor PainTrainFakeBlast : ExplosionBase //when hitting shields
{
scale 2.2
States
{
Spawn:
BOMB B 0
BOMB B 0 A_PlaySoundEx("weapon/Hyperbomb","Weapon")
BOMB BCDEFG 1
BOMB HIJKL 1 A_Fadeout
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor PainTrainPuff1 : FXActor_A
{
scale 2.8
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}

actor PainTrainTrail : FXActor_B
{
scale 1.8
Alpha 0.9
States
{
Spawn:
CRBM A 0
BASH B 2
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor PainTrainTrail_RED : PainTrainTrail
{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]


actor PainTrainShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("PainTrainShot",0,1,8,0)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("PainTrainShot_RED",0,1,8,0)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("PainTrainShot_BLUE",0,1,8,0)
TNT1 A 1
stop
}
}