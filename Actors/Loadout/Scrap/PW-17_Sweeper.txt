/*=============================
When you need raw power at a moment's notice, look no further than the sweeper!
it may seem like your typical bustergun but this little beast can drop some of 
the toughest foes around in one clip! it's an ammo hungry little sucker and
it's fast rate of fire does make it difficult to keep steady, but once you feel
it's destructive power, it's hard to settle for any less!

*///===========================

actor Sweeper : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 10
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Obituary "$OB_SWEEPER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "SweeperClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
"####" B 0 A_Jumpifinventory("SweeperClip",1,"Ready")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/basshot","Weapon")
"####" B 0 A_GiveInventory("SweeperShooter")
"####" D 2 A_WeaponReady(WRF_NOPRIMARY)
"####" C 0 A_REfire
goto Ready
NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("SweeperClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 22 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("SweeperClip",10)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor SweeperClip : Ammo
{
inventory.amount 1
inventory.maxamount 10
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor SweeperShot : fastProjectileBase
{
Speed 5
Damage (5 + ACS_ExecuteWithResult(DamageRate,4,300,15))
radius 5

MissileHeight 5
height 12
scale 1
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_ScaleVelocity(30)
TNT1 A 0  A_Changevelocity(0,frandom(-6,6),frandom(-6,6),CVF_RELATIVE)
Spawn2:
TNT1 AA 1 
TNT1 A 0 //A_Spawnitemex("SweeperTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
Loop
Death:
BSTG A 1 A_Spawnitemex("SweeperPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor SweeperShot_BLUE : SweeperShot
	{MissileType "SweeperTrail"
	}

	actor SweeperShot_RED : SweeperShot
	{MissileType "SweeperTrail_RED"
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"
	}
	
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor SweeperPuff1 : FXActor_A
{
scale 1
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}

actor SweeperTrail : FXActor_B
{
scale 0.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
BSTG A 2
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor SweeperTrail_RED : SweeperTrail
{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor SweeperShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("SweeperShot",0,1,4,4)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("SweeperShot_RED",0,1,4,4)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("SweeperShot_BLUE",0,1,4,4)
TNT1 A 1
stop
}
}