/*=============================

Feeling dangerous? the crossbow is well suited for closer-but-still-kinda-far
encounters, with just one quickly loaded clip, this weapon can keep a steady
supply of bolts coming at your foe. however, if you give the crossbow a second,
it will prime the bolt with a special poison to weaken your victim, it prevents
them from healing also!

*///===========================

actor Blastbow : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 1
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Obituary "$OB_Blastbow"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "BlastbowClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady

"####" B 0 A_Jumpifinventory("BlastbowPrimerActive",1,3)
"####" B 0 A_Spawnitemex("BlastbowPrimeHandler")
"####" B 0 A_GiveInventory("BlastbowPrimerActive",1)
"####" B 0 

"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
//"####" B 0 A_GiveInventory("BlastbowPrime",1)
"####" B 0 A_Jumpifinventory("BlastbowPrime",70,"Ready3")
"####" B 0 A_Jumpifinventory("BlastbowPrime",69,"Ready2")
"####" B 0 A_Jumpifinventory("BlastbowClip",1,"Ready")
Goto Reload
Ready2:
"####" B 0 A_PlaySoundEx("weapon/Trigger","Weapon")
Ready3:
"####" B 0
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
Goto Ready3

Deselect:
"####" B 1 A_TakeInventory("BlastbowPrime",999)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_jumpifinventory("BlastbowPrime",70,"PrimeFire")
"####" B 0 A_TakeInventory("BlastbowPrime",999)
"####" B 0 A_PlaySoundEx("weapon/slashclaw","Weapon")
"####" B 0 A_GiveInventory("BlastbowShooter")
"####" CCDDB 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH)
//"####" B 6 
goto Reload

PrimeFire:
"####" B 0 A_TakeInventory("BlastbowPrime",999)
"####" B 0 A_PlaySoundEx("weapon/slashclaw","Weapon")
"####" B 0 A_GiveInventory("PrimedBlastbowShooter")
"####" CCDDB 1 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSWITCH)
//"####" B 12 
goto Reload

NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("BlastbowClip",1,"Ready")
Reload:
"####" B 0 A_TakeInventory("ZoomMode")
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 7 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("BlastbowClip",8)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 0 A_TakeInventory("BlastbowPrime",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}


//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor BlastbowClip : Ammo
{
inventory.amount 1
inventory.maxamount 1
}

actor BlastbowPrime : inventory
{
inventory.amount 1
inventory.maxamount 70
}

actor BlastbowPrimerActive : Once{}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor BlastbowShot : fastProjectileBase
{
Speed 180
Damage (20 + ACS_ExecuteWithResult(DamageRate,10,300,180))
radius 8
height 10
scale 3.5
damagetype "Sniper"
States
{
Spawn:
TNT1 A 0 
CRBM A 0 
TRIC A 1 A_Spawnitemex("BlastbowTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Loop
Death:
CRBM A 0 A_Stop
CRBM A 1 A_Spawnitemex("BlastbowPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor BlastbowShot_BLUE : BlastbowShot
	{
	}

	actor BlastbowShot_RED : BlastbowShot
	{
	translation "202:202=33:33","196:196=48:48", "197:197=18:18","192:192=54:54", "198:198=42:42" , "196:196=170:170"
	}
	
actor PrimedBlastbowShot : fastProjectileBase
{
Speed 140
Damage (20 + ACS_ExecuteWithResult(DamageRate,10,300,180))
radius 8
height 10
scale 3.5
damagetype "Incendiary"
MissileType "BlazeTrail"
States
{
Spawn:
TNT1 A 0 
CRBM A 0 
TRIC A 1 //A_Spawnitemex("BlastbowTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
Loop
Death:
CRBM A 0 A_Stop
CRBM A 1 A_Spawnitemex("BlastbowPuff2",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
NAPC D 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
TNT1 A 2 A_Explode(5,130,1,0,130)
stop
}
}

actor PrimedBlastbowShot_BLUE : PrimedBlastbowShot
{MissileType "BlazeTrail_BLUE"}

actor PrimedBlastbowShot_RED : PrimedBlastbowShot
{translation "192:192=229:229", "198:198=227:227"
MissileType "BlazeTrail_RED"}
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor BlastbowPrimeHandler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_JumpIfInTargetInventory("BlastbowPrime",70,"Spawn")
TNT1 A 0 A_JumpIfInTargetInventory("BlastbowClip",1,"Prime")
loop
Prime:
TNT1 A 0 A_GiveToTarget("BlastbowPrime",1)
Goto Spawn
Death:
DeathTake:
TNT1 A 0
stop
}
}

actor BlastbowPuff1 : FXActor_A
{
scale 1.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("weapon/firestorm","Weapon")
BIMI AABCDEF 1
stop
}
}

actor BlastbowPuff2 : FXActor_A
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("weapon/firestorm","Weapon")
ASEX AABCDEF 1
stop
}
}

actor BlazeTrail : FXActor_A
{
scale 0.6
Alpha 0.9
States
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_Changevelocity(frandom(-1,1),frandom(-1,1),frandom(1,2),CVF_RELATIVE)
SMLF AABC 2
stop
}
}



	actor BlazeTrail_BLUE : BlazeTrail
	{
	//translation "192:192=196:196", "198:198=205:205"
	}
	
	actor BlazeTrail_RED : BlazeTrail
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}

actor BlastbowTrail : FXActor_A
{
scale 3.5
Alpha 0.6
States
{
Spawn:
TNT1 A 0 
LASH AAA 1
LASH AAA 1 A_Fadeout
stop
}
}

	actor BlastbowTrail_BLUE : BlastbowTrail
	{
	}

	actor BlastbowTrail_RED : BlastbowTrail
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor BlastbowShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("BlastbowShot",0,1,4,8)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("BlastbowShot_RED",0,1,4,8)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("BlastbowShot_BLUE",0,1,4,8)
TNT1 A 1
stop
}
}


actor PrimedBlastbowShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("PrimedBlastbowShot",0,1,4,8)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("PrimedBlastbowShot_RED",0,1,4,8)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("PrimedBlastbowShot_BLUE",0,1,4,8)
TNT1 A 1
stop
}
}