/*=============================
They dodge left? curve it left! they juke right? Curve it right! the Pointbuster
is a bustergun with specialized guided projectiles that give you full control over
it's destination, and don't let their slow speed fool you. these shots back quite
the punch!

*///===========================

actor PointBuster : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Obituary "$OB_POINTBUSTER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "PointBusterClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
//+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
"####" B 0 A_Jumpifinventory("PointBusterClip",1,"Ready")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/HardBuster1","Weapon")
"####" B 0 A_GiveToTarget("TargetMarker3", 1)
"####" B 0 A_GiveInventory("PointBusterShooter")
"####" CDD 2 A_WeaponReady(WRF_NOPRIMARY)
"####" B 7 
"####" C 0 A_REfire
goto Ready
NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("PointBusterClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 28 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("PointBusterClip",8)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor PointBusterClip : Ammo
{
inventory.amount 1
inventory.maxamount 8
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor PointBusterShot : ProjectileBase
{
Speed 25
Damage (10 + ACS_ExecuteWithResult(DamageRate,25,200,95))
radius 12

//MissileHeight 16
height 5
scale 3.5
States
{
Spawn:
TNT1 A 0 
PBUS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
Spawn1:
BUST T 0 A_GiveToTarget("TargetMarker3", 1)
PBUS A 1 ACS_ExecuteAlways(989, 0, ACS_ExecuteWithResult(257)+100)
BUST T 0 A_SpawnitemEx("PointBusterTrail",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
BUST T 0 A_ChangeVelocity(Cos(pitch)*25, 0, -sin(pitch)*25,CVF_RELATIVE|CVF_REPLACE)
Loop
Death:
PBUS A 1 A_Spawnitemex("PointBusterPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor PointBusterShot_BLUE : PointBusterShot
	{//MissileType "PointBusterTrail"
	//Translation "42:42=198:198", "217:217=197:197"
	}

	actor PointBusterShot_RED : PointBusterShot
	{//MissileType "PointBusterTrail_RED"
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"
	}
	
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor PointBusterPuff1 : FXActor_A
{
scale 2
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}

actor PointBusterTrail : FXActor_A
{
scale 1.4
Alpha 0.9
States
{
Spawn:
BUST T 0
PBUS A 2
PBUS AAAAAAA 1 A_Fadeout
stop
}
}

actor PointBusterTrail_RED : PointBusterTrail
{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"}

actor TargetTracer3 //: Fastprojectile 
{
Radius 16
Height 1
PROJECTILE
Speed 4000
Damage(0)
RenderStyle "NONE"
+DONTSPLASH
+DONTBLAST
+THRUGHOST
+SKYEXPLODE
 +NoInteraction
 +NOCLIP
States
{
Spawn:
TNT1 A 0 
TNT1 A 1 A_SpawnItemEx("TargetAimPoint", 0, 0, 0)  //Thing_SpawnNoFog(0,201,0,ACS_ExecuteWithResult(257, 0)+100) 
stop
}
}

Actor TargetMarker3 : CustomInventory
{
Inventory.amount 1
Inventory.MaxAmount 1
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 1
stop
Use:
TNT1 A 0 A_FireCustomMissile("TargetTracer3",0,0,0,16,0,0)
//TNT1 A 0 A_SpawnItemEx("TargetTracer", 0, 8, 32, Cos(pitch)*1000, 0, sin(-pitch)*1000,0)
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor PointBusterShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("PointBusterShot",0,1,4,4)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("PointBusterShot_RED",0,1,4,4)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("PointBusterShot_BLUE",0,1,4,4)
TNT1 A 1
stop
}
}