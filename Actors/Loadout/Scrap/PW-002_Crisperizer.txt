/*=============================
Slow but toasty, the Crisperizer is a buster gun that fires heated blasts to 
roast any opponent, these shots aren't as quick as the regular buster gun but
they are noticeably larger!

*///===========================

actor Crisperizer : WepMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 8
Weapon.SlotNumber 1
Weapon.SelectionOrder 1
scale 2.0
Obituary "$OB_CRISPERIZER"
Inventory.Pickupmessage "$PU_MEGABUSTER"
weapon.ammotype "CrisperizerClip"
+WEAPON.AMMO_OPTIONAL
+WEAPON.DONTBOB
+WEAPON.NOAUTOFIRE
+WEAPON.NOAUTOAIM
States
{
Spawn:
WEAP X 1
stop
Ready:
BUSR B 0 A_Jumpifinventory("WilyTeamFlag",1,"RedReady")
BUST B 0 
RedReady:
"####" B 1 A_WeaponReady
"####" B 0 A_Jumpifinventory("Reload_Trigger",1,"Reload")
"####" B 0 A_Jumpifinventory("CrisperizerClip",1,"Ready")
Goto Reload
Deselect:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop
Select:
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop
Fire:
"####" B 0 A_JumpIfNoAmmo("NoAmmo")
"####" B 0 A_PlaySoundEx("weapon/magmabazooka","Weapon")
"####" B 0 A_GiveInventory("CrisperizerShooter")
"####" CDD 2 A_WeaponReady(WRF_NOPRIMARY)
"####" B 11 
"####" C 0 A_REfire
goto Ready
NoAmmo:
"####" B 1
"####" B 0 A_Jumpifinventory("CrisperizerClip",1,"Ready")
Reload:
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 0 A_PlaySoundEx("weapon/rminestick","Weapon")
"####" B 28 
"####" B 0 A_PlaySoundEx("weapon/Crashland","Weapon")
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_Giveinventory("CrisperizerClip",8)
"####" B 0 A_TakeInventory("Reload_Trigger",999)
"####" B 1 A_Refire("Fire")
Goto Ready
Flash:
TNT1 A 5 
stop
NoFlash:
TNT1 A 1
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor CrisperizerClip : Ammo
{
inventory.amount 1
inventory.maxamount 8
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor CrisperizerShot : ProjectileBase
{
Speed 65
Damage (25 + ACS_ExecuteWithResult(DamageRate,5,300,60))
radius 8
Damagetype "Crisperizer"
//MissileHeight 16
height 8
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0  //A_Changevelocity(0,random(-4,4),random(-4,4),CVF_RELATIVE)
TNT1 A 0
Spawn2:
CRIS A 1 
TNT1 A 0 A_Spawnitemex("CrisperizerTrail",0,0,0,VELX/2,VELY/2,VELZ/2,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEVELOCITY)
Loop
Death:
BSTG A 1 A_Spawnitemex("CrisperizerPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

	actor CrisperizerShot_BLUE : CrisperizerShot
	{//MissileType "CrisperizerTrail"
	
	}

	actor CrisperizerShot_RED : CrisperizerShot
	{//MissileType "CrisperizerTrail_RED"
	translation "192:192=160:160", "198:198=42:42", "196:196=215:215", "197:197=226:226"
	}
	
//[+]========================================================================[+]
//--------------------------------|Special Actors|------------------------------
//[+]========================================================================[+]
actor CrisperizerPuff1 : FXActor_A
{
scale 3
States
{
Spawn:
BIM1 AABCDEF 1
stop
}
}

actor CrisperizerTrail : FXActor_A
{
scale 1.8
Alpha 0.9
States
{
Spawn:
CRBM A 0
CRIS BCDE 2
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor CrisperizerTrail_RED : CrisperizerTrail
{translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47", "241:241=191:191"}
//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor CrisperizerShooter : CustomInventory 
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 1 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 1 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
BUST B 1 A_FireCustomMissile("CrisperizerShot",0,1,4,4)
stop
TeamRedFire:
BUST B 1 A_FireCustomMissile("CrisperizerShot_RED",0,1,4,4)
TNT1 A 1
stop
TeamBlueFire:
BUST B 1 A_FireCustomMissile("CrisperizerShot_BLUE",0,1,4,4)
TNT1 A 1
stop
}
}