actor Ability1_megamanEXE : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_GUTSPUNCH"
Tag "HyperBomb Ability"
weapon.ammotype2 "AbilityAmmo"
inventory.icon "HARDSI"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
XB20 A 0 
"####" A 1 //A_WeaponReady
goto Fire

Fire:
XB20 A 0 A_PlaySoundEx("weapon/napalm","Weapon")
"####" A 0 A_Quake(2,14,0,8,"none")
"####" A 0 A_FireCustomMissile("GutsPunch",0,0,-8,0)
"####" A 5 A_GiveInventory("GutsPunchBreakFlag",1)
"####" A 0 A_TakeInventory("GutsPunchBreakFlag",999)
"####" DE 1
"####" F 16
"####" ED 2
"####" C 0 A_GiveInventory("GutsPunchCooldown")
"####" C 0 A_GiveInventory("RefireBuffer")
"####" C 0 A_Jump(256,"End")
goto End

AltFire:
"####" A 1
End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" C 0 
Goto End
}
}
//====

actor UseAbility1_megamanEXE : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("GutsPunchAbility",1)
fail
}
}

actor GutsPunchAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("GutsPunchCooldownItem",1,"Nope")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================


actor GutsPunch
{
PROJECTILE
+MTHRUSPECIES
+FORCEXYBILLBOARD
+BRIGHT
damagetype "GutsPunch"
Obituary "$OB_GUTSPUNCH"
reactiontime 1
damage (85)
Radius 20
Height 20
speed 96
scale 2.5
States
{
Spawn:
MMFX C 0
XP00 E 0 A_GiveInventory("ProjectileCode",1)
MMFX C 1 A_CountDown
wait
Death:
MMFX C 0 A_Explode(85,96,0,0,96)
goto XDeath
XDeath:
MMFX C 0 A_GiveInventory("ProjectileDCode",1)
MMFX C 0 A_Stop
MMFX C 2
MMFX B 2
MMFX CD 2
stop
}
}

actor GutsPunchBreakFlag : Once {}
actor GutsPunchBreaker : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInventory("GutsPunchBreakFlag",1,"FireBrokenShards")
stop
FireBrokenShards:
TNT1 A 0 A_FireCustomMissile("GutsPunchDebris",0,0,8,-8)//A_SpawnItemEx("GutsPunchDebris",0,-16,16,20)
TNT1 A 0 A_FireCustomMissile("GutsPunchDebris2",0,0,8,8)//A_SpawnItemEx("GutsPunchDebris",0,16,16,30)
TNT1 A 0 A_FireCustomMissile("GutsPunchDebris2",0,0,-8,-8)//A_SpawnItemEx("GutsPunchDebris",0,-16,48,30)
TNT1 A 0 A_FireCustomMissile("GutsPunchDebris",0,0,-8,8)//A_SpawnItemEx("GutsPunchDebris",0,16,48,20)
stop
}
}

actor GutsPunchDebris : FastProjectileBase
{
Obituary "$OB_SUPERARM"
Damage (35)
Radius 10
Height 10
Speed 96
scale 2.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode")
XU00 E 1
wait
Death:
TNT1 AAA 0 A_SpawnItemEx("RockCubeFX",0,0,0,random(1,5),random(-10,10),random(8,12),random(0,359),1)
stop
}
}
actor GutsPunchDebris2 : GutsPunchDebris {speed 80}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor GutsPunchCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("GutsPunchCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("GutsPunchCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AltCooldownFlag",99)
TNT1 A 0
stop
}
}

actor GutsPunchCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("GutsPunchCooldownItem",85)
TNT1 A 0 A_Giveinventory("AltCooldownFlag")
TNT1 A 0 A_Spawnitemex("GutsPunchCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor GutsPunchCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 85
}