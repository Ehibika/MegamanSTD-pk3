actor Ability2_BrightMan : AbilityMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 0
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_BRIGHTBULB"
Tag "Bright Bulb"
weapon.ammotype "BrightBulb_Charges"
weapon.ammotype2 "BrightBulb_Charges"
inventory.icon "BRIGHSI"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
FSTB A 0 
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_PlaySoundEx("weapon/adapterreturn","Weapon")
"####" A 0 A_GiveInventory("WeaponCharge",1)
"####" A 0 A_Gunflash("Flash.AimHandler")
Ready1:
"####" AA 1 A_WeaponReady
goto Ready1

Deselect:
TNT1 A 0 A_TakeInventory("AbilityActiveFlag",9)
TNT1 A 0 A_TakeInventory("WeaponCharge",999)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop


Fire:
"####" A 0 A_PlaySoundEx("weapon/xbshoot","Weapon")
"####" B 0 A_FireCustomMissile("BrightBulb",0,1,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 0 A_TakeInventory("WeaponCharge",999)
"####" BCA 2
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,110)
"####" A 0 A_GiveInventory("BrightBulbCooldown")
"####" A 0 A_GiveInventory("RefireBuffer")
"####" A 0 A_Jump(256,"End")
goto End

AltFire:
"####" A 0 A_PlaySoundEx("weapon/xbshoot","Weapon")
"####" B 0 A_FireCustomMissile("BrightBulb2",0,1,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" A 0 A_TakeInventory("WeaponCharge",999)
"####" BCA 2
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,110)
"####" A 0 A_GiveInventory("BrightBulbCooldown")
"####" A 0 A_GiveInventory("RefireBuffer")
"####" A 0 A_Jump(256,"End")
goto End

NoAmmo:
"####" "#" 1
"####" "#" 0 A_Jump(256,"Ready1")
Goto Ready1

//AltFire:
//"####" A 1
End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End

	Flash.AimHandler:
        TNT1 A 1 A_FireCustomMissile("BrightBulbAimInstantStarter",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
		TNT1 A 1 A_JumpIfInventory("WeaponCharge",1,"Flash.AimHandler")
        stop
}
}
//====

actor UseAbility2_BrightMan : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("BrightBulbAbility",1)
fail
}
}

actor BrightBulbAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("BrightBulb_Charges",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor BrightBulb_Charges : Ammo
{
inventory.amount 1
inventory.maxamount 2
}
//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

	actor BrightBulbAim : GolfBallAim
    { 
    Speed 48 
    Gravity 1.0
    wallbouncefactor 0.8
    BounceFactor 1.0
    BounceCount 1
    +DOOMBOUNCE
    +BOUNCEONCEILINGS
    -BOUNCEONFLOORS
    +MBFBOUNCER
    }

	actor BrightBulbAimInstantStarter : BrightBulbAim
	{
        +CLIENTSIDEONLY
		translation "198:198=220:220", "192:192=229:229"
		States
		{
		Spawn:
			TNT1 A 0
			TNT1 A 0 ACS_NamedExecuteWithResult("Arc_Limit_Start")
			TNT1 A 0 A_JumpIf(!CallACS("Arc_ClientSide"),"Perish")
			TNT1 A 0 A_SpawnItemEx("BrightBulbAimInstantBuffer",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY)
			TNT1 A 1
			stop
		}
	}
	actor BrightBulbAimInstantBuffer : BrightBulbAim
	{
		+CLIENTSIDEONLY
		States
		{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(!CallACS("Arc_ClientSide"),"Perish")
			TNT1 A 0 A_SpawnItemEx("BrightBulbAimInstant",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY)
			TNT1 A 1
			stop
		}
	}

	actor BrightBulbAimInstant : BrightBulbAim
	{
		+CLIENTSIDEONLY
		States
		{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_JumpIf(CallACS("Arc_Limit"),"Nope")
			TNT1 A 0 //A_JumpIfInTargetInventory("BetweenShots",1,"Nope")
			TNT1 A 0 A_JumpIf(!CallACS("Arc_ClientSide"),"Perish")
			TNT1 A 0 A_SpawnItemEx("BallTrailAim",(10*momx)/-20.0,-(10*momy)/-20.0,(10*momz)/-20.0,0,0,0,0,SXF_ABSOLUTEANGLE | SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION)
			TNT1 A 0 A_SpawnItemEx("BallTrailAim",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION | SXF_TRANSFERTRANSLATION )
			TNT1 A 0 A_SpawnItemEx("BrightBulbAimInstant",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION | SXF_ABSOLUTEVELOCITY)
			TNT1 A 1
			stop
		Nope:
			TNT1 A 1
			stop
		}
	}

Const int BrightBulb_Rad = 480;

actor BrightBulb 
{
PROJECTILE
damagetype "BrightBulb"
Obituary "$OB_BRIGHTBULB"
radius 4
height 4
scale 2.0
-NOGRAVITY
+DOOMBOUNCE
+BOUNCEONCEILINGS
+BOUNCEONFLOORS
+THRUACTORS
+MTHRUSPECIES
+USEBOUNCESTATE
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
+MBFBOUNCER
+CANBOUNCEWATER
speed 48
gravity 1.0
Damage (0)
wallbouncefactor 0.8
BounceFactor 1.0
reactiontime 25
States
{
Spawn:
FSTB A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
FSTB LMNO 1 A_SpawnItemEx("BrightBulbParticle",Random2(15),Random2(15),Random2(15),-momx*0.2,-momy*0.2,-momz*0.2,0,9)
FSTB A 0 //A_CountDown
Goto Spawn2

Bounce:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
FSU1 A 1 A_SpawnItemEX("BrightBulb2",0,0,0,momx*0.4,momy*0.4,momz*0.4,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Bounce.Floor:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
FSU1 A 1 A_SpawnItemEX("BrightBulb2",0,0,0,momx*0.2,momy*0.2,momz*0.3,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
FSU1 A 1 A_SpawnItemEX("BrightBulb2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
MINB A 0 A_SpawnItemEx("BrightBulbExplosion")
stop
}
}

actor BrightBulb2 : BrightBulb
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_PlaySoundEX("weapon/Trigger","Soundslot6")
TNT1 A 0 A_PlaySoundEX("weapon/BombAlarm","Soundslot7")
Spawn2:
FSTB LMNO 1 A_GiveInventory("BrightBulbGroundSlowFunc",1) 
loop
Death:
MINB A 0 A_SpawnItemEx("BrightBulbExplosion",0,0,0,0,0,0,0,1)
stop
}
}


actor BrightBulbExplosion : BasicExplosion2
{
-SOLID
+NOGRAVITY
PROJECTILE
+DONTBLAST
+NOINTERACTION
damage 0
radius 1
height 1
States
{
Spawn:
TNT1 A 0
TNT1 A 5 A_SpawnItemEx("FlashPopFX1",0,0,-16,0,0,0,0,1)
TNT1 A 0 A_PlaySoundEx("weapon/centaurflash","Weapon")
TNT1 A 0 A_SetArg(2,(ceilingz-z<=16)*-4+(z-floorz==0)*4)
TNT1 AAAAAAA 0 A_SpawnItemEx("BrightBulbShardFX",0,0,Args[2],random(-10+abs(Args[2]),10-abs(Args[2])),random(-10+abs(Args[2]),10-abs(Args[2])),random(-2+abs(Args[2]),2+abs(Args[2])),0,1,32)
TNT1 A 0 A_JumpIf(ACS_NamedExecuteWithResult("CSLT_flashstopperlight",1500) > 0,"Spawn2")
TNT1 A 1
stop

Spawn2:
TNT1 A 1 A_GiveToTarget("FlashPingSoundCue")
stop
}
}

actor BrightBulbGroundSlowFunc : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
FSU1 A 0 A_CountDown
TNT1 A 0 A_SpawnItemEx("BrightBulbParticle",Random2(15),Random2(15),Random2(15),-momx*0.2,-momy*0.2,-momz*0.2,0,9)
FSU1 A 0 A_CheckFloor("SlowDown")
stop
SlowDown:
FSU1 A 0 A_ScaleVelocity(0.3)
Goto Spawn2
stop
}
}

actor BrightBulbParticle
{
+FORCEXYBILLBOARD
+BRIGHT
Renderstyle "Add"
Alpha 0.95
Scale 2.25
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Jump(256,"Spawn1","Spawn2","Spawn3","Spawn4")
Spawn1:
FSTB UVWX 2
stop
Spawn2:
FSTB UVVWWXX 1
stop
Spawn3:
FSTB VUUVWX 1
stop
Spawn4:
FSTB VUVWX 1
stop
}
}

actor BrightBulbShardFX
{
+MISSILE
+CLIENTSIDEONLY
+THRUACTORS
+DOOMBOUNCE
+RANDOMIZE
BounceFactor 0.3
WallBounceFactor 0.3
renderstyle translucent
Radius 4
Height 4
Speed 20
scale 2.5
States
{
Spawn:
FSTB O 0
FSTB O 0 A_Jump(256,1,2,3,4)
FSTB OPQRSOPQRSOPQRSOPQRSOPQRSOPQRSOPQRSOPQRSOPQRSOPQRSOPQRS 4
stop
Death:
FSTB S 0 A_Jump(256,1,2,3)
FSTB SSSSSSSSSSSS 2
FSTB SSSSS 1 A_FadeOut(0.175)
stop
}
}

actor FlashPopFX1 : BasicClientside
{
scale 1.5
ReactionTime 15
States
{
Spawn:
TNT1 A 0 
NDFX A 1
NDFX AA 1 A_SetScale(ScaleX+1.5)
Spawn2:
NDFX A 1 A_SetScale(ScaleX+1.5)
NDFX A 0 A_Fadeout(0.3)
NDFX A 0 A_CountDown
Loop
}
}


actor FlashPingSoundCue : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
ROLO R 0 A_PlaySoundEx("misc/BombCount","Weapon")
stop

Nope:
ROLO R 0 
stop

}
}



//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor BrightBulbCooler : WatcherBase 
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("BrightBulbCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("BrightBulbCooldownItem",1,"Spawn")
TNT1 A 0 A_GiveToTarget("BrightBulb_Charges",1)
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0 A_JumpIfInTargetInventory("BrightBulb_Charges",2,"Death")
TNT1 A 0 A_GiveToTarget("BrightBulbCooldownItem",420)
Goto Spawn
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0 A_TakeFromTarget("AbilityChargesCooldownFlag",99)
TNT1 A 0
stop
}
}

actor BrightBulbCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIfInventory("AbilityChargesCooldownFlag",1,"Nope")

TNT1 A 0 A_Giveinventory("BrightBulbCooldownItem",420)
TNT1 A 0 A_Spawnitemex("BrightBulbCooler")
TNT1 A 0 A_Giveinventory("AbilityChargesCooldownFlag")

TNT1 A 0 A_JumpIfInventory("BrightBulb_Charges",1,"Nope")
TNT1 A 0 A_Giveinventory("AbilityCooldownFlag")
stop
Nope:
TNT1 A 0
stop
}
}

actor BrightBulbCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 420
}