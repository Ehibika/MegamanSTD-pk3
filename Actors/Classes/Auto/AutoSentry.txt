actor Ability2_AutoSTD : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "AutoSentry Ability"
weapon.ammotype2 "AbilityAmmo"
inventory.icon "TRETI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
+WEAPON.NOAUTOFIRE
States
{

Ready:
HSPR B 0 
"####" B 0 A_FireCustomMissile("SentryMarker",0,0,0,Aim_OverHead)
"####" B 1 A_WeaponReady
goto ready

Fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("SentryPlacer",0,0,0,Aim_OverHead)
"####" B 1 A_GiveInventory("AutoSentryCooldown")
"####" C 0 A_GiveInventory("RefireBuffer")
//"####" A 0 ACS_NamedExecuteWithResult("MSTD_Chatline",CLT_ABILITY,Random(0,1))
"####" IJKLM 2
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility2_AutoSTD : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("AutoSentryAbility",1)
fail
}
}

actor AutoSentryAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AutoSentryCooldownItem",1,"Nope")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================

Actor SpawnDeny : Powerup {inventory.amount 1 inventory.maxamount 1 Powerup.Duration 3}
Actor TurretReticleHold : Powerup {inventory.amount 1 inventory.maxamount 1 Powerup.Duration 10}

actor AutoSentry : BasicDeployable
{
-SHOOTABLE
-SOLID
+THRUACTORS
+DONTRIP
+SLIDESONWALLS
+MISSILE
+NOGRAVITY
-QUICKTORETALIATE
-FRIENDLY
+THRUSPECIES
+MTHRUSPECIES
+NOTARGETSWITCH

+SEEKERMISSILE
+FLOORHUGGER
//==
speed 1
DamageFactor "Sentryshot", 0
scale 1.5
height 20
Radius 22
Health 100
gravity 999
reactiontime 256
PainChance 256
states
{

Spawn:
//Sentry has to warm up first
TRET A 0 
TNT1 D 0 ACS_NamedExecuteAlways("GiveEntiTID",0,2)
TRET A 0 A_GiveInventory("ProjectileCode",1)
Spawn.Warmup:
TRET A 1 A_CheckRange(60,"Spawn.Warmup2")
Wait
Spawn.Warmup2:
TRET A 0 A_ChangeFlag("SHOOTABLE",1)
TRET A 0 A_ChangeFlag("SOLID",1)
TRET A 0 A_ChangeFlag("THRUACTORS",0)
TRET A 1 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT) //set the owner to the master pointer
Spawn.Warmup3:
TRET A 1 A_jumpifinventory("WeaponCharge",BSEN_Warm+1,"Spawn.Basic")
TRET A 1 A_jumpifinventory("WeaponCharge",BSEN_Warm,"Spawn.Ready")
TRET A 0 A_PlaySoundEx("weapon/heat1", "Weapon")
TRET A 0 A_GiveInventory("WeaponCharge",1) 
TRET A 0 A_jumpifinventory("WeaponCharge",BSEN_Warm,"Spawn.Ready")
loop
Spawn.Ready:
TRET A 1
TRET A 0 //A_CheckFlag("NOGRAVITY", "Terminate", AAPTR_MASTER)
TRET A 0 A_ChangeFlag("QuickToRetaliate",true)
TRET A 0 A_GiveInventory("WeaponCharge",1) 
TRET A 0  A_PlaySoundEx("misc/gravitywarn","Weapon")
Goto Spawn.Basic

Spawn.Basic:
TRET A 0 A_Stop
Goto Scan

Scan:
TRET A 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
Scan1:
TRET A 1 A_SetAngle(5+angle)
TRET A 0 A_Stop
TRET A 0 A_Lookex(LOF_NOSOUNDCHECK,1,BSEN_DetectRange,BSEN_DetectRange,90,"TargetCheck")
Goto Scan1

loop
Targetcheck:
TRET A 1 A_Jumpifcloser(BSEN_DetectRange,"Targetcheck2")
Goto Scan1
Targetcheck2:
TRET A 1 A_Jumpif(CallACS("TrapSensorIdentify")==1,"Scan1") //You Are A Teammate
TRET A 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_TARGET)
Goto Spotted

Spotted:
TRET A 1
//TRET A 0 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_TARGET)// set the target as our tracer, and the owner as our target again.
TRET A 0 A_PlaySoundEx("weapon/flashstopper","Weapon")
TRET AAA 1 A_FaceTracer(0,0)
TRET A 0 A_PlaySoundEx("weapon/flashstopper","Weapon")
TRET AAA 1 A_FaceTracer(0,0)
TRET A 0 A_PlaySoundEx("weapon/flashstopper","Weapon")
TRET AAA 1 A_FaceTracer(0,0)
TRET A 1 
Missile:
TRET A 0 //A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT) //reset target to master
TRET A 0 A_FaceTracer(0,0)
TRET A 0 A_Changevelocity(0,0,0,CVF_REPLACE) //Prevents movement
TNT1 A 0 A_Stop
TRET A 1 A_GiveInventory("TurretReticleSpawner",1,AAPTR_TRACER)
TRET A 0 A_SpawnItemEx("AutoSentryBullet", cos(-pitch)*15,0,20+(sin(pitch)*15), cos(pitch+1), 0, sin(pitch+1), 0, SXF_MULTIPLYSPEED|SXF_TRANSFERPOINTERS)
TRET A 0 A_PlaySoundEx("weapon/BasherShot","Body")

Afterfire:
TRET AAA 1 A_GiveInventory("TurretReticleSpawner",1,AAPTR_TRACER)//A_Changevelocity(0,0,0,CVF_REPLACE) //Prevents movement
TRET A 0 A_Jumpifinventory("CutterFlag",999,2,AAPTR_TRACER) //
TRET A 0 A_JumpIfTargetInLOS ("Missile", 0, JLOSF_PROJECTILE,BSEN_ForgetRange  )//JLOSF_PROJECTILE
TRET A 0 
TRET A 0 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_NULL)
Goto Scan

Deploy.Pain:
TRET A 1 A_Stop//
TRET A 0 A_SpawnItemEx("PainFX",-5,0,3,0,0,0)
TRET A 0 A_PlaySoundEx("misc/mm4pain", "Voice")
TRET A 0 A_JumpIfTargetInLOS ("Missile", 0, JLOSF_PROJECTILE,BSEN_DetectRange  )
TRET A 0 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT) //reset target to master
TRET A 0 A_jumpifinventory("WeaponCharge",BSEN_Warm+1,"Scan")
Goto Warmup

Deploy.Death:
Death:
TRET A 0 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT) //reset target to master
TNT1 A 0 A_PlaySoundEx("weapon/napalm", "Weapon")
TNT1 A 0 A_SpawnItemEX("ExplosionEffect1",0,0,32,0,0,0,0,1)
TNT1 A 4
stop
Perish:
Terminate:
TNT1 A 0 
TNT1 A 4
stop
}
}

Actor AutoSentry_B : AutoSentry{Designatedteam 0 Species "LightMember" }
Actor AutoSentry_R : AutoSentry{Designatedteam 1 Species "WilyMember" }
Actor AutoSentry_G : AutoSentry{Designatedteam 2 Species "CossackMember" }
Actor AutoSentry_O : AutoSentry{Designatedteam 3 Species "KingMember" }

//[+]========================================================================[+]
//--------------------------------|Projectiles|------------------------------
//[+]========================================================================[+]

actor AutoSentryBullet : fastProjectileBase
{
Obituary "$OB_AUTOSENTRY"
DamageType "AutoSentry"
Speed 96
Damage (4)
radius 3

MissileHeight 16
height 3
scale 1.0
Var Int User_TrailCount;
Var Int User_TP; //TrailPosition

reactiontime 8
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
TNT1 A 0 A_SpawnItemEx("AutoSentryTrail",0,0,0,momx,momy,momz,0,9)
Spawn3:
TNT1 A 0 A_CountDown
TNT1 A 1 
Goto Spawn2
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_Spawnitemex("BlasterPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}




actor AutoSentryTrail : BasicExplosion
{
Obituary "$OB_AUTOSENTRY"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("AutoSentryTrailX",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("AutoSentryTrailX1",0,0,0,-momx*0.1,-momy*0.1,-momz*0.1,0,9)
TNT1 A 0 A_SpawnItemEx("AutoSentryTrailX1",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("AutoSentryTrailX2",0,0,0,-momx*0.3,-momy*0.3,-momz*0.3,0,9)
TNT1 A 0 A_SpawnItemEx("AutoSentryTrailX2",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 1 
stop
}
}

actor AutoSentryTrailX : FXActor_A
{
scale 1.5
Alpha 1.0
States
{
Spawn:
BUST A 0
RSS1 J 1
stop
}
}

actor AutoSentryTrailX1 : AutoSentryTrailX
{
Alpha 0.8
States
{
Spawn:
CRBM A 0
RSS1 J 1
stop
}
}

actor AutoSentryTrailX2 : AutoSentryTrailX
{
Alpha 0.4
States
{
Spawn:
CRBM A 0
RSS1 J 1
stop
}
}




//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]

actor AreaMarker
{
PROJECTILE
+NOINTERACTION
+NOCLIP
+CLIENTSIDEONLY
radius 8
height 8
Damage (0)
scale 5.0
Reactiontime 35
Renderstyle "ADD"
Alpha 0.8
var int user_Rot;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileFXCode",1)
TNT1 A 0 A_setuservar("user_Rot",0)
spawn2:
TNT1 A 0 A_SpawnItemEx("AreaMarkerDot",BSEN_DetectRange,0,5,3,momy/4,0,user_Rot,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AreaMarkerDot",BSEN_DetectRange,0,30,3,momy/4,0,user_Rot+6,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AreaMarkerDot",BSEN_DetectRange,0,55,3,momy/4,0,user_Rot,SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_setuservar("user_Rot",user_Rot+12)
TNT1 A 0 A_jumpif(user_Rot >= 360,"Death")
loop
Death:
TNT1 A 1
stop
}
}

actor AreaMarkerDot  // 
{
PROJECTILE
+NOINTERACTION
+NOGRAVITY
//+FLOORHUGGER
+NOTARGETSWITCH
height 5
Radius 5
damage (0)
reactiontime 1
scale 1.5
RenderStyle "ADD"
Alpha 1
States
{
Spawn:
LASH A 0
LASH A 2
stop
}
}


actor TurretRetical : FXActor_A
{
+DONTSPLASH
-CLIENTSIDEONLY
scale 3.2
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 ACS_NamedExecuteAlways("TurretReticleWarper", 0, 0)
Shield:
RETC AAA  1 A_Warp(AAPTR_TARGET,-2,0,40,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE)
loop
}
}

	actor TurretRetical_BLUE : TurretRetical
	{
	}

	actor TurretRetical_RED : TurretRetical
	{
	translation "192:192=54:54", "198:198=42:42" , "196:196=170:170", "199:199=47:47"
	}

//[+]========================================================================[+]
//--------------------------------|Utility Actors|------------------------------
//[+]========================================================================[+]


actor SentryMarker :FastProjectile 
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
height 20
Radius 22
damage (0)
speed 150
reactiontime 1
scale 1.5
RenderStyle "ADD"
Alpha 1
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop

Death:
TNT1 A 0 A_SpawnItemEx("AreaMarker",0,0,-(z-floorz),0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SentryMarkerFX",0,0,-(z-floorz),0,0,0,0,1)
stop
}
}



actor SentryMarkerFX : BasicGraphicEffect  // 
{
scale 1.5
States
{
Spawn:
TNT1 A 0 
TREG A 1
stop
}
}


actor RejectAudioCue : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 
"####" K 1 ACS_Execute(979,0)
stop
}
}


actor SentryPlacer :FastProjectile 
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
height 20
Radius 22
damage (0)
speed 150
reactiontime 1
scale 1.5
RenderStyle "ADD"
Alpha 1
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop

Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_GiveInventory("SpawnAutoSentryItem")
TNT1 A 1
stop
}
}


actor AutoSentryCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("AutoSentryCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("AutoSentryCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0
stop
}
}

actor AutoSentryCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("AutoSentryCooldownItem",350)
TNT1 A 0 A_Spawnitemex("AutoSentryCooler")
TNT1 A 0 A_Giveinventory("AbilityCooldownFlag")
stop
Nope:
TNT1 A 0
stop
}
}

actor AutoSentryCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 350
}

//[+]========================================================================[+]
//--------------------------------|Item Shooters|------------------------------
//[+]========================================================================[+]
actor SpawnAutoSentryItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SetPitch(z-floorZ)

TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")

TNT1 A 0 A_SpawnItemEx("AutoSentry",0,0,-(z-floorz),0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//SXF_TRANSFERPOINTERS
stop
BFire:
TNT1 A 0 A_SpawnItemEx("AutoSentry_B",0,0,-(z-floorz),0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//
stop
RFire:
TNT1 A 0 A_SpawnItemEx("AutoSentry_R",0,0,-(z-floorz),0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
GFire:
TNT1 A 0 A_SpawnItemEx("AutoSentry_G",0,0,-(z-floorz),0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
YFire:
TNT1 A 0 A_SpawnItemEx("AutoSentry_O",0,0,-(z-floorz),0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
}
}


actor AutoSentryChecker : CustomInventory  //checks if there is space to spawn the sentry
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
 "####" E 1 A_SpawnitemEx("SentryMarker", 120, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)

stop
TeamRedFire:
 "####" E 1 A_SpawnitemEx("SentryMarker_RED", 120, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
TNT1 A 0
stop
TeamBlueFire:
 "####" E 1 A_SpawnitemEx("SentryMarker_BLUE", 120, 0, 0, momx, momy, momz, 0, SXF_ABSOLUTEMOMENTUM, 0)
TNT1 A 0
stop
}
}


actor AutoSentryPlanter : CustomInventory  //places a sentry based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
//---otherwise, just place a blank one
GDTB A 0 A_SpawnItemEX("AutoSentry",120,0,0,0,0,0,0,0)
stop
TeamRedFire:
GDTB A 0 A_SpawnItemEX("AutoSentry_R",120,0,0,0,0,0,0,0)
TNT1 A 0
stop
TeamBlueFire:
GDTB A 0 A_SpawnItemEX("AutoSentry_B",120,0,0,0,0,0,0,0)
TNT1 A 0
stop
}
}

actor AutoSentryPlanter2 : CustomInventory  //places a sentry based on the team.
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")
//---otherwise, just place a blank one
GDTB A 0 A_SpawnItemEX("SentryPlacer",80,0,0,0,0,0,0,SXF_SETMASTER)
stop
TeamRedFire:
GDTB A 0 A_SpawnItemEX("SentryPlacer_RED",80,0,0,0,0,0,0,SXF_SETMASTER)
TNT1 A 0
stop
TeamBlueFire:
GDTB A 0 A_SpawnItemEX("SentryPlacer_BLUE",80,0,0,0,0,0,0,SXF_SETMASTER)
TNT1 A 0
stop
}
}

actor TurretReticleSpawner : CustomInventory 
{
States
{
Pickup:
BLTR C 0
BLTR C 0 A_Jumpifinventory("TurretReticleHold",1,"ReticleHold")
BLTR C 0 A_Jumpifinventory("WilyTeamFlag",1,"TeamRedFire")
BLTR C 0 A_Jumpifinventory("LightTeamFlag",1,"TeamBlueFire")

PRMN A 0 A_SpawnitemEx("TurretRetical",-2,0,40,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
TNT1 A 0 A_GiveInventory("TurretReticleHold")
stop
TeamRedFire:
PRMN A 0 A_SpawnitemEx("TurretRetical_BLUE",-2,0,40,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
TNT1 A 0 A_GiveInventory("TurretReticleHold")
stop
TeamBlueFire:
PRMN A 0 A_SpawnitemEx("TurretRetical_RED",-2,0,40,momx,momy,momz,0,SXF_ABSOLUTEMOMENTUM)
TNT1 A 0 A_GiveInventory("TurretReticleHold")
stop
ReticleHold:
TNT1 A 0 A_GiveInventory("TurretReticleHold")
stop
}
}