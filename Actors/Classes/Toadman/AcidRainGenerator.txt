actor Ability1_ToadMan : AbilityMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_MEGABUSTER"
Tag "Acid Rain Generator"
weapon.ammotype1 "AcidRainGenerator_Charges"
inventory.icon "RAINSI"
Dropitem ""
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
+WEAPON.NOAUTOFIRE
States
{
Ready:
TARM B 0 
"####" B 0 A_FireCustomMissile("AcidRainGeneratorVisualSpawner",0,0,0,0)
"####" A 1 A_WeaponReady
goto ready

Fire:
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" C 0 A_FireCustomMissile("AcidRainGeneratorSpawner",0,1,0,0) //Why can't Toad Man rain :<
"####" GH 2 
"####" H 6
"####" HG 2 
"####" A 6  
"####" C 0 A_GiveInventory("AcidRainGeneratorCooldown")
"####" C 0 A_GiveInventory("RefireBuffer")
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" A 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility1_ToadMan : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("AcidRainGeneratorAbility",1)
fail
}
}

actor AcidRainGeneratorAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("AcidRainGenerator_Charges",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor AcidRainGenerator_Charges : Ammo
{
inventory.amount 1
inventory.maxamount 2
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor AcidRainGeneratorVisualSpawner  : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
+CLIENTSIDEONLY
reactiontime 1
damage (0)
radius 3
height 3
speed 100
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_SpawnItemEx("AcidRainGeneratorVisual",0,0,-(z-floorz)+0,0,0,0,0,1)
stop
}
}

actor AcidRainGeneratorVisual : BasicGraphicEffect
{
renderstyle "add"
alpha 0.5
scale 2.0
States
{
Spawn:
TGEN A 0
TGEN A 0 A_GiveInventory("ProjectileFXCode",1)
TGEN A 2
stop
}
}

actor AcidRainGeneratorSpawner  : FastProjectile
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 100
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

Spawn2:
TNT1 A 0
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
DMIT A 0 A_GiveInventory("SpawnAcidRainGeneratorItem",1)
stop
}
}

actor AcidRainGenerator  : BasicDeployable
{
height 32
radius 18
scale 2.0
+NOTARGETSWITCH
+DONTRIP
painsound "misc/devilpain"
painchance 256

damagefactor "StandardSaberEdge", 0.3
damagefactor "StandardSaberTip", 0.3
damagefactor "BeamRapireEdge", 0.3
damagefactor "BeamRapireTip", 0.3
damagefactor "TriangularBladeEdge", 0.3
damagefactor "TriangularBladeTip", 0.3

damagefactor "AcidRain", 0.0 

//Break Weakness
damagefactor "DrillHead", 10.0
damagefactor "CrushingSpray", 10.0
damagefactor "SuperAdaptor", 10.0


translation "0:255=0:255"

//Item 1
+FORCEYBILLBOARD
-SHOOTABLE
+SOLID
+THRUSPECIES
+THRUGHOST
+DONTBLAST
+DONTREFLECT
+MISSILE
species "MovingPlatform"
+THRUSPECIES

//Concrete Shot
+NOTAUTOAIMED
+CANTSEEK
+NOBLOOD
+DONTRIP
Health 100
Mass 9999
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode")
TNT1 D 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,2,2)
TNT1 A 0 A_Stop
TNT1 A 0 Thing_ChangeTID(0,ACS_NamedExecuteWithResult("core_gettarget")+4500)
TNT1 A 0 A_ChangeFlag(MISSILE, 0)
TNT1 A 0 A_ChangeFlag(THRUGHOST,0)
TNT1 A 0 A_ChangeFlag(SHOOTABLE,1)
Spawn2:
TGEN ABC 12
Spawn3: 
TGEN D 3 A_PlaySoundEx("weapon/rainflush","Voice")
TGEN A 3
TNT1 A 0 A_SpawnItemEx("RainFlushGeneratorRain",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
loop

Deploy.Pain:
TNT1 A 2 A_Pain
goto Spawn2

Deploy.Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_ChangeFlag(SHOOTABLE,0)
TNT1 A 0 A_ChangeFlag(SOLID,0)
TNT1 A 2 A_Pain
stop
}
}


Actor AcidRainGenerator_B : AcidRainGenerator{Designatedteam 0 Species "LightMember" }
Actor AcidRainGenerator_R : AcidRainGenerator{Designatedteam 1 Species "WilyMember" }
Actor AcidRainGenerator_G : AcidRainGenerator{Designatedteam 2 Species "CossackMember" }
Actor AcidRainGenerator_O : AcidRainGenerator{Designatedteam 3 Species "KingMember" }

actor SpawnAcidRainGeneratorItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SetPitch(z-floorZ)

TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")

TNT1 A 0 A_SpawnItemEx("AcidRainGenerator",0,0,-(z-floorz)+0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//SXF_TRANSFERPOINTERS
stop
BFire:
TNT1 A 0 A_SpawnItemEx("AcidRainGenerator_B",0,0,-(z-floorz)+0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//
stop
RFire:
TNT1 A 0 A_SpawnItemEx("AcidRainGenerator_R",0,0,-(z-floorz)+0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
GFire:
TNT1 A 0 A_SpawnItemEx("AcidRainGenerator_G",0,0,-(z-floorz)+0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
YFire:
TNT1 A 0 A_SpawnItemEx("AcidRainGenerator_O",0,0,-(z-floorz)+0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
}
}

actor RainFlushGeneratorRain
{
damagetype "AcidRain"
Obituary "$OB_RAINFLUSH"
+NOGRAVITY
+MISSILE
+SKYEXPLODE
+NOINTERACTION
+DONTBLAST
+DONTSPLASH
height 1
radius 1
-SOLID
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_Explode(10,400,0,0,400)
TNT1 A 0 A_GiveInventory("ProjectileCode")
//TNT1 A 0 A_PlaySoundEx("weapon/rainflush","Voice")
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
TNT1 A 1 A_SpawnItemEx("AcidRainSprinkleFX", random(-32,480), random(-256,256), 256, -30, 0, -30, 90, SXF_TRANSFERTRANSLATION)
stop
}
}

actor AcidRainSprinkleFX
{
PROJECTILE
+RIPPER
+NOINTERACTION
+THRUACTORS
+DONTBLAST
+DONTREFLECT
+CLIENTSIDEONLY
+DontSplash
Reactiontime 1
Damage (0)
Radius 3
Height 4
Speed 30
Scale 2.5
Args 15
States
{
Spawn:
TARM R 0
TARM R 0
TARM RR 1 A_JumpIf(ceilingz-z>5,"Spawn2")
TARM R 0 A_JumpIf(momz==0,"Death")
TARM R 0 A_CountDownArg(0,"SpawnD")
TARM SS 1 A_JumpIf(ceilingz-z>5,"Spawn2")
TARM S 0 A_JumpIf(momz==0,"Death")
TARM S 0 A_CountDownArg(0,"SpawnD")
Goto Spawn+2
Spawn2:
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX1",0,0,0,-30,0,-30,0,SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION)
stop
SpawnD:
TARM R 1// A_CountDown
stop
Death:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("AcidRainSprinkleFX1",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERTRANSLATION)
TNT1 A 2
stop
}
}

actor AcidRainSprinkleFX1 : BasicClientSide
{
+Missile
-NoInteraction
+DontSplash
Radius 3
Height 4
States
{
Spawn:
TARM R 0
TARM RSRSRSRSRSRSRSRS 2 
stop
Death:
TARM LMN 1 A_Stop
stop
}
}
//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor AcidRainGeneratorCooler : WatcherBase 
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("AcidRainGeneratorCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("AcidRainGeneratorCooldownItem",1,"Spawn")
TNT1 A 0 A_GiveToTarget("AcidRainGenerator_Charges",1)
TNT1 A 0 A_JumpIfInTargetInventory("AcidRainGenerator_Charges",2,"Death")
TNT1 A 0 A_GiveToTarget("AcidRainGeneratorCooldownItem",525)
Goto Spawn
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AltCooldownFlag",99)
TNT1 A 0
stop
}
}

actor AcidRainGeneratorCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("AcidRainGeneratorCooldownItem",525)
TNT1 A 0 A_Giveinventory("AltCooldownFlag")
TNT1 A 0 A_Spawnitemex("AcidRainGeneratorCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor AcidRainGeneratorCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 525
}