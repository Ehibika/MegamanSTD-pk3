Const float SCFL_SprAng = 1.6;
Const float SCFL_SprAngRev = SRev-SCFL_SprAng;

Const float SCFL_SprD_Ang = SCFL_SprAng/1.5;
Const float SCFL_SprD_AngRev = SRev-(SCFL_SprAng/1.5);

actor Ability2_Marino : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_SCATTEREDFLOWER"
Tag "Dash Ability"
weapon.ammotype2 "AbilityAmmo"
inventory.icon "YAMASI"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
HSPR B 0 
"####" B 1 Offset(0,110)
"####" B 1 Offset(0,96)
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,32)
"####" B 0 A_GiveInventory("WeaponCharge")
Ready1:
"####" B 1 A_WeaponReady
goto Ready1


Fire:
"####" B 0 A_PlaySoundEx("weapon/SlashClaw","Weapon")
"####" B 0 A_FireCustomMissile("ScatteredFlowerShot",0,2,0,8)
"####" K 1
"####" B 0 A_FireCustomMissile("ScatteredFlowerShot",SCFL_SprD_Ang,0,0,8,0,SCFL_SprD_Ang)
"####" J 1
"####" B 0 A_FireCustomMissile("ScatteredFlowerShot",SCFL_SprD_Ang,0,0,8,0,SCFL_SprD_AngRev)
"####" I 1
"####" B 0 A_FireCustomMissile("ScatteredFlowerShot",SCFL_SprD_AngRev,0,0,8,0,SCFL_SprD_AngRev)
"####" G 1
"####" B 0 A_FireCustomMissile("ScatteredFlowerShot",SCFL_SprD_AngRev,0,0,8,0,SCFL_SprD_Ang)

"####" FFF 2
"####" B 1 Offset(0,32)
"####" B 1 Offset(0,46)
"####" B 1 Offset(0,58)
"####" B 1 Offset(0,70)
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,82)
"####" B 1 Offset(0,96)
"####" B 1 Offset(0,110)
"####" B 0 A_GiveInventory("ScatteredFlowerCooldown")
"####" B 0 A_GiveInventory("RefireBuffer")
"####" B 0 A_Jump(256,"End")
goto End

//AltFire:
//"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End

}
}
//====

actor UseAbility2_Marino : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("ScatteredFlowerAbility",1)
fail
}
}

actor ScatteredFlowerAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("ScatteredFlowerCooldownItem",1,"Nope")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor ScatteredFlowerShot : fastProjectileBase
{
Speed 96
Damage (4)
radius 6
height 10
scale 2.5
damagetype "ScatteredFlower"
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
TNT1 A 1 A_Spawnitemex("ScatteredFlowerTrail",0,0,0,momx,momy,momz,0,9)
Loop
Crash:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_Spawnitemex("ScatteredFlowerPuff",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
TNT1 AA 0 A_Spawnitemex("PoisonedFX1",Random(-10,10),Random(-10,10),Random(-10,10),0,0,Random(1,3),0,SXF_TRANSFERTRANSLATION)
Stop

Death:
CRBM A 0 A_Stop
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_Spawnitemex("ScatteredFlowerStickFX",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}

actor ScatteredFlowerTrail : BasicExplosion
{
Obituary "$OB_SCATTEREDFLOWER"
damagetype "MegaBuster"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("ScatteredFlowerTrail_FX",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("ScatteredFlowerTrail_FX1",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("ScatteredFlowerTrail_FX1",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("ScatteredFlowerTrail_FX2",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("ScatteredFlowerTrail_FX2",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)
TNT1 A 1 
stop
}
}

actor ScatteredFlowerTrail_FX : FXActor_A
{
scale 2.5
Alpha 1.0
States
{
Spawn:
YAMA A 0
YAMA A 1
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor ScatteredFlowerTrail_FX1 : FXActor_A
{
scale 2.5
Alpha 0.8
States
{
Spawn:
YAMA A 0
YAMA A 1
stop
}
}

actor ScatteredFlowerTrail_FX2 : FXActor_A
{
scale 2.5
Alpha 0.4
States
{
Spawn:
YAMA A 0
YAMA A 1
stop
}
}


actor ScatteredFlowerPuff : FXActor_A
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_PlaySoundEx("weapon/OrbBounce","Weapon")
BIM1 AABCDEF 1
stop
}
}


actor ScatteredFlowerStickFX : FXActor_A
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
YAMA A 10 
YAMA AZAZAZAZ 4 
stop
}
}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor ScatteredFlowerCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("ScatteredFlowerCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("ScatteredFlowerCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0
stop
}
}

actor ScatteredFlowerCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("ScatteredFlowerCooldownItem",140)
TNT1 A 0 A_Giveinventory("AbilityCooldownFlag")
TNT1 A 0 A_Spawnitemex("ScatteredFlowerCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor ScatteredFlowerCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 140
}