actor Ability2_Zero : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_REKKOHA"
Obituary "$OB_REKKOHA"
Tag "Rekkoha"
weapon.ammotype2 "AbilityAmmo"
inventory.icon "REKKOI"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
CKIC C 0 
"####" C 1 //A_WeaponReady
goto Fire

Fire:
REKK JJKKLL 1 A_Stop
TNT1 A 0 A_PlaySoundEx("misc/upgrade","Weapon")
TNT1 A 0 A_GiveInventory("RekkohaTimer")
REKK MNO 2 //A_Stop
PUNCHER:
FHUD C 0 ThrustThingZ(0, -125, 0, 0)
XBUS C 0 SetPlayerProperty(0,1,0)
XBUS A 0 A_ChangeFlag("INVULNERABLE",1)
TNT1 A 1 A_JumpIf(z-floorz==0,"RekkohaFire")
TNT1 A 1 A_Jumpifinventory("ConShotTimer",1,"PUNCHER")
goto End
RekkohaFire:
RUMB B 0 A_PlaySoundEX("weapon/breakdashimpact","Body")
RUMB B 0 A_FireCustomMissile("RekkohaShot",0,1,0,0,0,Pitch)
RUMB B 0 A_FireCustomMissile("RekkohaShot",30,0,0,0,0,Pitch)
RUMB B 0 A_FireCustomMissile("RekkohaShot",330,0,0,0,0,Pitch)
TNT1 A 20 A_Quake(8,17,0,300)
XBUS C 0 SetPlayerProperty(0,0,0)
XBUS A 0 A_ChangeFlag("INVULNERABLE",0)
"####" C 0 A_GiveInventory("RekkohaCooldown")
"####" C 0 A_GiveInventory("RefireBuffer")
"####" C 0 A_Jump(256,"End")
goto End

AltFire:
"####" C 1
End:
"####" C 1 A_TakeInventory("AbilityActiveFlag",9)
"####" C 0 
Goto End
}
}
//====

actor UseAbility2_Zero : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("RekkohaAbility",1)
fail
}
}

actor RekkohaAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("RekkohaCooldownItem",1,"Nope")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================
actor RekkohaTimer : PowerupGiver
{
+INVENTORY.AUTOACTIVATE
Powerup.Duration 35
powerup.type "ConShotTimer"
}

Actor RekkohaProtect : powerprotection
{
Powerup.duration 17
Damagefactor "Rekkoha", 0
}


actor RekkohaShot
{
PROJECTILE
+RIPPER
+FLOORHUGGER
+NOINTERACTION
+NOCLIP
damagetype "Rekkoha"
Obituary "$OB_REKKOHA"
Radius 4
Height 4
scale 2.5
damage (0)
+FORCEXYBILLBOARD
speed 64
reactiontime 6
Args 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SpawnItemEx("RekkohaShot_A",0,0,0,MomX,Momy,Momz,0,17929)
Stop

Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 3
stop
}
}

actor RekkohaShot_A : RekkohaShot
{
Args 0,0,0
Var Int User_Step;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SetScale(ScaleX+0.5)
TNT1 A 0 A_SetUserVar("User_Step",Pitch % 3)
TNT1 A 0 A_Jumpif(User_Step == 0,"Spawn2")
TNT1 A 0 A_SpawnItemEx("RekkohaShot_A",0,0,0,MomX,Momy,Momz,0,17929)
stop

Spawn2:
TNT1 A 0 A_SetArg(0,MomX)
TNT1 A 0 A_SetArg(1,MomY)
TNT1 A 0 A_SetArg(2,MomZ)
TNT1 A 0 A_Stop
TNT1 A 3 A_SpawnItemEx("RekkohaSpawner",0,0,-(z-floorz)+24,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("RekkohaShot_B",0,0,0,Args[0],Args[1],Args[2],0,17929)
TNT1 A 2
stop
}
}

actor RekkohaShot_B : RekkohaShot
{
Args 0,0,0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SetPitch(Pitch+1)
TNT1 A 0 A_SetScale(ScaleX+0.5)
TNT1 A 0 A_Jumpif(Pitch >= reactiontime*3,"Death")
TNT1 A 0 A_SpawnItemEx("RekkohaShot_A",0,0,0,MomX,Momy,Momz,0,17929)
stop

}
}


actor RekkohaSpawner : BasicExplosion
{
Damage(0)
DamageType "Rekkoha"
obituary "$OB_REKKOHA"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)

TNT1 A 1 
TNT1 A 0 A_SpawnItemEx("RekkohaSpawner2",0,0,-(z-floorz)+16,0,0,320,0,1)
stop

Death:
TNT1 A 0 
stop
}
}

actor RekkohaSpawner2 : FastProjectileBase
{
+DONTBLAST
+SKYEXPLODE
+THRUACTORS
reactiontime 1
damage (0)
radius 3
height 3
speed 300
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1
TNT1 A 0 A_SpawnItemEx("RekkohaProjectile",0,0,0,0,0,-16,0,1)
TNT1 A 1
stop
}
}

actor RekkohaProjectile : ProjectileBase
{
	+RIPPER
	+NOINTERACTION
	+DONTBLAST
damagetype "Rekkoha"
Obituary "$OB_REKKOHA"
gravity 2.0
Damage (0)
height 7
radius 16
Speed 0
scale 3.5
States
{
	Spawn:
	ICEL W 0
	TNT1 A 0 A_GiveInventory("ProjectileCode",1)
	ICEL W 0 
	ICEL W 0 
	ICEL W 1 A_JumpIf(ceilingz-z>8, "Spawn1")
	ICEL W 0 A_JumpIf(momz==0,"Death")
	ICEL W 0 A_CountDown
	Goto Spawn+2
	Spawn1:
	ICEL W 0 A_ChangeFlag("RIPPER", 0)
	ICEL W 0 A_ChangeFlag("NOINTERACTION",0)
Spawn2:
REKK A 1 A_SpawnItemEx("RekkohaTrailBlue",0,0,63,0,0,0,0,1)
ICEL W 0 A_JumpIf(momz==0,"Death")
ICEL W 0 //A_CountDown
Goto Spawn2
Death:
ASEX A 0 A_SpawnItemEx("RekkohaTrailBlue",0,0,0,0,0,0,0,1)
ASEX A 0 A_SpawnItemEx("ExplosionEffect4",0,0,0,0,0,0,0,1)
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
ASEX A 0 A_playSoundEx("weapon/Napalm","Weapon")
TNT1 A 0 A_Quake(4,12,0,350)
TNT1 A 17 A_Explode(50,160,0,0,160)
stop
}
}




actor RekkohaTrailBlue : BasicClientside
{
-FORCEXYBILLBOARD
scale 3.5
States
{
Spawn:
TNT1 A 1
REKK B 10
REKK C 2 
stop
}
}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor RekkohaCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("CutterFlag",999,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("RekkohaCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("RekkohaCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0
stop
}
}

actor RekkohaCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("RekkohaCooldownItem",420)
TNT1 A 0 A_Giveinventory("AbilityCooldownFlag")
TNT1 A 0 A_Spawnitemex("RekkohaCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor RekkohaCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 420
}