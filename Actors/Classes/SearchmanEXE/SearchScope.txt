Const Int SearchScopeRange = 300;

Const Int SearchScopeMaxRange = 80;
Const Int SearchScopeMinRange = 20;
Const Int SearchScope_DMG1 = 50;
Const Int SearchScope_DMG2 = 300;

Const Int SSR1 = SearchScopeMaxRange*15;
Const Int SSR2 = SearchScopeMinRange*15;

actor Ability2_SearchmanEXE : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_SEARCHSCOPE"
Tag "SearchScope"
weapon.ammotype2 "AbilityAmmo"
inventory.icon "SCOPI"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
SSHD A 0 
"####" C 0 A_ZoomFactor(2.5)
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,32)
"####" A 5
Ready1:
"####" C 1 A_WeaponReady
goto Ready1

Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,0.6*FVM)
TNT1 A 0 A_GiveInventory("ZoomMode",1)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop

Deselect:
SSHD A 5 A_ZoomFactor(1.0)
TNT1 A 0 A_TakeInventory("ZoomMode",9)
TNT1 A 0 A_TakeInventory("AbilityActiveFlag",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" A 1 A_Lower
Loop


Fire:
"####" B 0 A_PlaySoundEx("weapon/HardBuster6","Weapon")
"####" B 0 A_PlaySoundEx("weapon/coilexplode", "SoundSlot6")
"####" C 0 A_FireCustomMissile("SearchScopeBurstStart",0,0,0,0,0,0)
"####" C 17 A_fireBullets(0,0,1,0,"SearchScopePuff",FBF_NORANDOM,SearchScopeRange*15) //Damage is on the puff
goto Fire.End

Fire.End:
"####" A 5 A_ZoomFactor(1.0)
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,110)
"####" A 0 A_GiveInventory("SearchScopeCooldown")
"####" A 0 A_GiveInventory("RefireBuffer")
"####" A 0 A_Jump(256,"End")
goto End

AltFire:
"####" "#" 0 A_JumpIfInventory("ZoomMode",2,"ZoomUp3")
"####" "#" 0 A_JumpIfInventory("ZoomMode",1,"ZoomUp2")
goto ZoomUp1

ZoomUp1:
"####" "#" 0 A_PlaySoundEx("weapon/adapterreturn","SoundSlot6")
"####" "#" 0 A_GiveInventory("ZoomMode",1)
"####" "#" 5 A_ZoomFactor(2.5)
"####" "#" 0 A_Jump(256,"Ready1")
goto Ready1
ZoomUp2:
"####" "#" 0 A_PlaySoundEx("weapon/adapterreturn","SoundSlot6")
"####" "#" 0 A_GiveInventory("ZoomMode",1)
"####" "#" 5 A_ZoomFactor(4.5)
"####" "#" 0 A_Jump(256,"Ready1")
goto Ready1
ZoomUp3:
"####" "#" 0 A_PlaySoundEx("weapon/adapterreturn","SoundSlot6")
"####" "#" 0 A_TakeInventory("ZoomMode",9)
"####" "#" 5 A_ZoomFactor(6.5)
"####" "#" 0 A_Jump(256,"Ready1")
goto Ready1

NoAmmo:
"####" "#" 1 
"####" "#" 0 A_Jump(256,"Ready1")
Goto Ready1

//AltFire:
//"####" A 1
End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End

	Sound.BusterCharge:
		TNT1 A 9 A_PlaySoundEx("weapon/adapterchargeloop","Weapon",0)
		TNT1 A 1 A_JumpIfInventory("WeaponCharge",1,"Sound.BusterCharge")
        stop
}
}
//====

actor UseAbility2_SearchmanEXE : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("SearchScopeAbility",1)
fail
}
}

actor SearchScopeAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("SearchScopeCooldownItem",1,"Nope")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor ZoomMode : inventory
{
inventory.amount 1
inventory.maxamount 3
}

actor SearchScopePuff : BulletPuff
{
scale 2.5
+PUFFONACTORS
+ALWAYSPUFF
+PUFFGETSOWNER
+MTHRUSPECIES
+NOINTERACTION
damagetype "SearchScope"
Obituary "$OB_SEARCHSCOPE"
alpha 1.0
States
{
Spawn:
CRBM A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_PlaySoundEx("weapon/Mirrordeflect","Weapon")
TNT1 A 0 A_Explode(CallACS("SearchManDamageRamp",SearchScope_DMG1,SearchScope_DMG2,SSR2,SSR1),32,0,0,32)
BIM1 AABCDEF 1
stop
}
}



actor SearchScopeBurstStart //: BasicHelper
{
    +MISSILE
    -SOLID
    +NOCLIP
    +NOINTERACTION
    +NOGRAVITY
    +DONTBLAST
    +DONTREFLECT
    +THRUACTORS
    RenderStyle none
    Radius 1
    Height 1

    Speed 15
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Warp(2,0,0,32,0,16+8)//But which interpolation? Does it matter?
        TNT1 A 1 A_SpawnItemEx("SearchScopeBurst",0,0,0,momx,momy,momz,0,8)
        stop
    }
}

actor SearchScopeBurst : FastProjectileBase//FastProj to match damager hitbox
{
    +MISSILE
    +NOGRAVITY
    +SKYEXPLODE
    +EXPLODEONWATER
    +DONTSPLASH
    +PAINLESS
    +CLIENTSIDEONLY
    +THRUACTORS
    RenderStyle None
    Damage (0)
    Radius 1
    Height 1
    Speed 15
    Scale 2.5
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("ProjectileCode",1)
        TNT1 A 0 A_JumpIf(Pitch > SearchScopeRange,"Death2")
        TNT1 A 0 A_SetPitch(Pitch+1)
        TNT1 A 0 A_SpawnItemEx("SearchScopeBurst",0,0,0,momx,momy,momz,0,552+1)//512+32+8
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("SearchScopeFX",0,0,0,0,0,0,0,1)
        TNT1 A 1
        stop
    Death:
        TNT1 A 0 A_SpawnItemEx("SearchScopeEndFX",0,0,0,0,0,0,0,1)
        stop
    Death2:
        TNT1 A 0 A_Stop
        TNT1 A 0 A_SpawnItemEx("SearchScopeEndFX",0,0,0,0,0,0,0,1)
        stop
    }
}


actor SearchScopeFX : BasicGraphicEffect
{
ReactionTime 6
renderstyle "add"
speed 25
scale 2.5
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
MBEA A 3
MBEA AAAAA 1 A_SetScale(ScaleX-0.2)
FXLoop:
MBEA A 1 A_SetScale(ScaleX-0.2)
MBEA A 0 A_Fadeout
MBEA A 0 A_CountDown
loop
}
}


//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor SearchScopeCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("SearchScopeCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("SearchScopeCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0
stop
}
}

actor SearchScopeCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("SearchScopeCooldownItem",192)
TNT1 A 0 A_Giveinventory("AbilityCooldownFlag")
TNT1 A 0 A_Spawnitemex("SearchScopeCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor SearchScopeCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 192
}