actor Ability1_SearchmanEXE : AbilityMasterBase
{
Weapon.AmmoUse 1
Weapon.AmmoGive 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_DYNAMITE"
Tag "Dynamite"
weapon.ammotype1 "Dynamite_Charges"
inventory.icon "BOMBSI"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
Weapon.SlotPriority 5
States
{

Ready:
HSPR B 0 
"####" B 0 A_FireCustomMissile("DynamiteAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 1 A_WeaponReady
goto ready

Fire:
"####" A 0 A_JumpIfNoAmmo("NoAmmo")
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("DynamiteSpawner",0,1,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" IJKLM 2
"####" A 0 A_GiveInventory("DynamiteCooldown")
"####" A 0 A_GiveInventory("RefireBuffer")
"####" B 0 A_Jump(256,"End")
goto End

NoAmmo:
"####" "#" 1
"####" "#" 0 A_Jump(256,"End")
Goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility1_SearchmanEXE : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("DynamiteAbility",1)
fail
}
}

actor DynamiteAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("Dynamite_Charges",1,"Pickup2")
stop

Pickup2:
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,1)
stop
Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================
//[+]========================================================================[+]
//--------------------------------|Ammo & inventory|---------------------------
//[+]========================================================================[+]
actor Dynamite_Charges : Ammo
{
inventory.amount 1
inventory.maxamount 3
}

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]
actor DynamiteAimPath : GolfBallAimInstantStarter { Speed 35 Gravity 1.5}


Const int Dynamite_DMG = 150;
Const int Dynamite_RAD = 160;
Const int Dynamite_RADMin = 96;

actor DynamiteSpawner  
{
+MISSILE
+NOTARGETSWITCH
+NOBLOOD
+DONTBLAST
+SKYEXPLODE
+ThruActors
Obituary "$OB_DYNAMITE"
DamageType "Dynamite"
radius 8
height 8
scale 2.5
Speed 35 
Gravity 1.5
states
{
Spawn:
DMIT A 0
DMIT A 0 A_GiveInventory("ProjectileCode",1)
DMIT A 3
goto spawn+2
Death:
DMIT A 0 A_GiveInventory("SpawnDynamiteItem",1)
stop
}
}


actor Dynamite  
{
+MISSILE
+NOBLOOD
+DONTBLAST
+SEEKERMISSILE
+NOTARGETSWITCH
+SHOOTABLE
+NOGRAVITY
+NOCLIP
+LOOKALLAROUND
Obituary "$OB_DYNAMITE"
DamageType "Dynamite"
scale 2.5
radius 8
height 8
speed 0
health 10
scale 2
states
{
Spawn:
TNT1 A 0
DMIT A 0 A_GiveInventory("ProjectileCode",1)
DMIT A 0 ACS_NamedExecuteAlways("GiveM_EntiTID",0,4,3)
DMIT A 1 A_RearrangePointers(AAPTR_DEFAULT, AAPTR_TARGET, AAPTR_DEFAULT)
DMIT A 30
DMIT A 1 A_PlaySoundEX("weapon/flashstopper","Weapon")
DMIT ABABAB 3 //A_FadeTo(0.3)
DMIT A 0 
DMIT C 0 A_Jump(256,"Scan")
Goto Scan

Scan:
DMIT A 0 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
Scan1:
DMIT A 0 A_SetArg(0,Args[0]+1)
DMIT A 4 A_Jumpif(Args[0]>5,"Scan2")
DMIT A 1  A_LookEx(LOF_NOSOUNDCHECK, 1, Dynamite_RAD, Dynamite_RAD, 360, "Targetcheck")
Goto Scan1
Scan2: //Blink
DMIT A 0 A_SetArg(0,0)
DMIT A 4 A_SpawnItemEx("SLBombPulseFX",0,0,8,0,0,0,0,1)
DMIT A 1  A_LookEx(LOF_NOSOUNDCHECK, 1, Dynamite_RAD, Dynamite_RAD, 360, "Targetcheck")
Goto Scan1
Targetcheck:
DMIT A 1 A_Jumpifcloser(Dynamite_RAD,"Targetcheck2")
Goto Scan
Targetcheck2:
DMIT A 1 A_Jumpif(CallACS("TrapSensorIdentify")==1,"Scan") //You Are A Teammate
Goto Explode

Explode:
DMIT B 0 A_PlaySoundEX("weapon/BombAlarm","Soundslot6")
DMIT ABABA 5
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 1 A_RearrangePointers(AAPTR_MASTER, AAPTR_DEFAULT, AAPTR_DEFAULT)
TNT1 A 0 A_SpawnItemEx("ExplosionEffect2",0,0,0,0,0,0,0,1)
TNT1 A 0 A_PlaySoundEx("weapon/Napalm","Weapon")
TNT1 A 0 A_Explode(Dynamite_DMG,Dynamite_RAD,1,0,Dynamite_RADMin)
stop
Death:
TNT1 A 0 A_GiveInventory("ProjectileDCode",1)
TNT1 A 0 A_PlaySoundEx("misc/MetDie","Weapon")
TNT1 A 0 A_SpawnItemEx("ExplosionEffect",0,0,0,0,0,0,0,1)
TNT1 A 3
stop
}
}

Actor Dynamite_B : Dynamite{Designatedteam 0 Species "LightMember" }
Actor Dynamite_R : Dynamite{Designatedteam 1 Species "WilyMember" }
Actor Dynamite_G : Dynamite{Designatedteam 2 Species "CossackMember" }
Actor Dynamite_O : Dynamite{Designatedteam 3 Species "KingMember" }

actor SpawnDynamiteItem : CustomInventory
{
inventory.amount 1
inventory.maxamount 1
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_SetPitch(z-floorZ)

TNT1 A 0 A_JumpIfInTargetInventory("LightTeamFlag",1,"BFire")
TNT1 A 0 A_JumpIfInTargetInventory("WilyTeamFlag",1,"RFire")
TNT1 A 0 A_JumpIfInTargetInventory("CossackTeamFlag",1,"GFire")
TNT1 A 0 A_JumpIfInTargetInventory("KingTeamFlag",1,"YFire")

TNT1 A 0 A_SpawnItemEx("Dynamite",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//SXF_TRANSFERPOINTERS
stop
BFire:
TNT1 A 0 A_SpawnItemEx("Dynamite_B",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)//
stop
RFire:
TNT1 A 0 A_SpawnItemEx("Dynamite_R",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
GFire:
TNT1 A 0 A_SpawnItemEx("Dynamite_G",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
YFire:
TNT1 A 0 A_SpawnItemEx("Dynamite_O",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERPITCH)
stop
}
}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor DynamiteCooler : WatcherBase 
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("DynamiteCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("DynamiteCooldownItem",1,"Spawn")
TNT1 A 0 A_GiveToTarget("Dynamite_Charges",1)
TNT1 A 0 A_TakeFromTarget("AltCooldownFlag",99)

TNT1 A 0 A_JumpIfInTargetInventory("Dynamite_Charges",3,"Death")
TNT1 A 0 A_GiveToTarget("DynamiteCooldownItem",420)
Goto Spawn
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AltCooldownFlag",99)
TNT1 A 0 A_TakeFromTarget("AltChargesCooldownFlag",99)

TNT1 A 0
stop
}
}


actor DynamiteCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_JumpIfInventory("AltChargesCooldownFlag",1,"Nope")

TNT1 A 0 A_Giveinventory("DynamiteCooldownItem",420)
TNT1 A 0 A_Spawnitemex("DynamiteCooler")
TNT1 A 0 A_Giveinventory("AltChargesCooldownFlag")

TNT1 A 0 A_JumpIfInventory("Dynamite_Charges",1,"Nope")
TNT1 A 0 A_Giveinventory("AltCooldownFlag")
stop
Nope:
TNT1 A 0
stop
}
}

actor DynamiteCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 420
}