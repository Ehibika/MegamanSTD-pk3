actor Ability2_Centaurman : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_STOPARROW"
Tag "Stop Arrow"
weapon.ammotype2 "AbilityAmmo"
inventory.icon "ARROWSH"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
States
{

Ready:
CENH A 0 
"####" A 1 Offset(0,110)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,32)
"####" A 0 A_PlaySoundEx("weapon/centaurcharge","Weapon")
"####" IJK 4
Ready1:
CENH L 1 A_WeaponReady
goto Ready1

Select:
TNT1 A 0 ACS_NamedExecuteAlways("WepSpeedMod",0,0.6*FVM)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
"####" B 1 A_Raise
Loop

Deselect:
TNT1 A 0 A_TakeInventory("AbilityActiveFlag",9)
TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_Lower
"####" B 1 A_Lower
Loop


Fire:
"####" A 0 A_PlaySoundEx("weapon/centaurarrow","Weapon")
"####" A 0 A_PlaySoundEx("weapon/centaurarrowfly","SoundSlot6")

"####" A 0 A_FireCustomMissile("StopArrowRound",0,0,4,4,0,0)
"####" A 0 A_GiveInventory("StopArrowCooldown")
"####" MLMMMMM 2
"####" KJI 3
"####" A 1 Offset(0,32)
"####" A 1 Offset(0,46)
"####" A 1 Offset(0,58)
"####" A 1 Offset(0,70)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,82)
"####" A 1 Offset(0,96)
"####" A 1 Offset(0,110)
"####" A 0 A_GiveInventory("RefireBuffer")
"####" A 0 A_Jump(256,"End")
goto End
/*
AltFire:
"####" "#" 0 A_JumpIfInventory("CombatDashCooldownItem",1,"NoAmmo")
HSPR A 0 A_PlaySoundEx("weapon/BreakDash","SoundSlot6")
"####" A 0 A_GiveInventory("DashSet_A")
"####" BBCCCCCC 1 A_ChangeVelocity(35,0,momz,CVF_RELATIVE|CVF_REPLACE)//A_Recoil(-20)
"####" C 0 A_GiveInventory("DashSetOff")
"####" BA 2
"####" A 0 A_GiveInventory("CombatDashCooldown")
"####" A 0 A_GiveInventory("RefireBuffer")
"####" A 0 A_Jump(256,"Ready1")
goto Ready1
*/
NoAmmo:
"####" "#" 1
"####" "#" 0 A_Jump(256,"Ready1")
Goto Ready1

//AltFire:
//"####" A 1
End:
"####" A 1 A_TakeInventory("AbilityActiveFlag",9)
"####" A 0 
Goto End
}
}
//====

actor UseAbility2_Centaurman : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("StopArrowAbility",1)
fail
}
}

actor StopArrowAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("StopArrowCooldownItem",1,"Nope")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop

Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================

//[+]========================================================================[+]
//--------------------------------|Projectiles |--------------------------------
//[+]========================================================================[+]

actor StopArrowRound : fastProjectileBase
{
Obituary "$OB_STOPARROW"
DamageType "StopArrow"
Speed 128
Damage (10)
scale 3.5
radius 14
height 18
States
{
Spawn:
TNT1 A 0 
AQBA L 0 A_GiveInventory("ProjectileCode",1)
Goto Spawn2
Spawn2:
CARW AABB 1 A_SpawnItemEx("StopArrowTrail",-85,0,-momz,momx,momy,momz,0,9)
Goto Spawn2
Death:
AQBA L 0 A_GiveInventory("ProjectileDCode",1)
TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("StopArrowParticles", 0, 0, 0, 256/24, 256/24, random2(256/24), random(0, 359),1)
TNT1 A 0 A_Spawnitemex("StopArrowPuff1",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION)
stop
}
}
//[+]========================================================================[+]
//--------------------------------|Effect Actors|-------------------------------
//[+]========================================================================[+]
actor StopArrowTrail : BasicExplosion
{
Obituary "$OB_STOPARROW"
damagetype "MegaBuster"
States
{
Spawn:
TNT1 A 0
AQBA L 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_SpawnItemEx("StopArrowTrail_FX",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("StopArrowTrail_FX",0,0,0,-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("StopArrowTrail_FX1",0,0,0,-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("StopArrowTrail_FX1",0,0,0,-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("StopArrowTrail_FX1",0,0,0,-momx*0.8,-momy*0.8,-momz*0.8,0,9)

TNT1 A 0 A_SpawnItemEx("StopArrowParticle",Random2(15),Random2(15),Random2(15),-momx*0.2,-momy*0.2,-momz*0.2,0,9)
TNT1 A 0 A_SpawnItemEx("StopArrowParticle",Random2(15),Random2(15),Random2(15),-momx*0.4,-momy*0.4,-momz*0.4,0,9)
TNT1 A 0 A_SpawnItemEx("StopArrowParticle",Random2(15),Random2(15),Random2(15),-momx*0.6,-momy*0.6,-momz*0.6,0,9)
TNT1 A 0 A_SpawnItemEx("StopArrowParticle",Random2(15),Random2(15),Random2(15),-momx*0.8,-momy*0.8,-momz*0.8,0,9)

TNT1 A 1 
stop
}
}

actor StopArrowTrail_FX : FXActor_B
{
scale 2.5
Alpha 0.9
States
{
Spawn:
CRBM A 0
CARW A 1
//BSTG AAAAA 1 A_Fadeout
stop
}
}

actor StopArrowTrail_FX1 : FXActor_B
{
scale 2.5
Alpha 0.6
States
{
Spawn:
BUST A 0
CARW A 1
stop
}
}

actor StopArrowPuff1 : FXActor_A
{
scale 2.5
States
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_GiveInventory("ProjectileFXCode",1)
SHFX LMNOP 1
stop
}
}

actor StopArrowParticle
{
+NOINTERACTION
+NOGRAVITY
+CLIENTSIDEONLY
+FORCEXYBILLBOARD
-SOLID
+BRIGHT
scale 2.5
height 0
radius 0
states
{
Spawn:
TNT1 A 0 
TNT1 A random(1,6) A_Stop
FLSA NOPO 4
FLSA NZOZPZOZ 2
stop
}
}

actor StopArrowParticles : StopArrowParticle
{
states
{
Spawn:
TNT1 A 0 
TNT1 A random(1,6) 
FLSA NNPOPPOO 2 A_ScaleVelocity(0.6)
FLSA NZOZPZOZ 2
stop
}
}
//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor StopArrowCooler : WatcherBase // Coolers are a type of watcher actor that handle weapon cooldowns
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("StopArrowCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("StopArrowCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0
stop
}
}

actor StopArrowCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("StopArrowCooldownItem",315)
TNT1 A 0 A_Giveinventory("AbilityCooldownFlag")
TNT1 A 0 A_Spawnitemex("StopArrowCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor StopArrowCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 315
}