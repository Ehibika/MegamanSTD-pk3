actor Ability2_Axl : AbilityMasterBase
{
Weapon.AmmoUse2 1
Weapon.AmmoGive2 0
Inventory.Pickupmessage "$PU_MEGABUSTER"
Obituary "$OB_BLASTLAUNCHER"
Tag "BlastLauncher"
weapon.ammotype2 "AbilityAmmo"
inventory.icon "BOMBSI"
+WEAPON.ALLOW_WITH_RESPAWN_INVUL
Weapon.SlotPriority 5
States
{

Ready:
HSPR B 0 
"####" B 0 A_FireCustomMissile("BlastLauncherAimPath",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" B 1 A_WeaponReady
goto ready

Fire:
"####" FFGGG 1
"####" C 0 A_PlaySoundEx("weapon/crackerthrow","Weapon")
"####" B 0 A_FireCustomMissile("BlastLauncher",0,0,0,0,0,Aim_ArcOverHead+(Aim_ArcScale*Sin(-pitch)))
"####" IJKLM 2
"####" A 0 A_GiveInventory("BlastLauncherCooldown")
"####" A 0 A_GiveInventory("RefireBuffer")
"####" B 0 A_Jump(256,"End")
goto End

AltFire:
"####" B 1
End:
"####" B 1 A_TakeInventory("AbilityActiveFlag",9)
"####" B 0 
Goto End
}
}
//====

actor UseAbility2_Axl : CustomInventory
{
states
{
Use:
WEA2 A 0 A_GiveInventory("BlastLauncherAbility",1)
fail
}
}

actor BlastLauncherAbility : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfInventory("BlastLauncherCooldownItem",1,"Nope")
TNT1 A 0 ACS_NamedExecuteAlways("RunClassAbility",0,2)
stop
Nope:
TNT1 A 0 ACS_Execute(979,0)
stop
}
}
//=================
//=================
actor BlastLauncherAimPath : GolfBallAimInstantStarter3 { Speed 35 Gravity 1.5}

actor BlastLauncher 
{
PROJECTILE
damagetype "Explosion"
Obituary "$OB_BLASTLAUNCHER"
radius 4
height 4
scale 2.0
-NOGRAVITY
+DOOMBOuNCE
+BOUNCEONCEILINGS
+ALLOWBOUNCEONACTORS
+BOUNCEONACTORS
+MTHRUSPECIES
+USEBOUNCESTATE
+NOTARGETSWITCH
+NOEXPLODEFLOOR
+SLIDESONWALLS
+MBFBOUNCER
speed 35
gravity 1.5
Damage (0)
wallbouncefactor 0.8
BounceFactor 0.3
reactiontime 25
States
{
Spawn:
BOMB S 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
Spawn2:
BOMB S 1 A_SpawnItemEX("BlastLauncherTrail",0,0,5,0,0,0,0,SXF_TRANSFERTRANSLATION,0)
BOMB S 0 //A_CountDown
Goto Spawn2

Bounce.Floor:
Bounce.Ceiling:
MINB A 0 A_ChangeFlag("NOGRAVITY",false)
MINB A 0 A_ChangeFlag("USEBOUNCESTATE",false)
BOMB S 1 A_SpawnItemEX("BlastLauncher2",0,0,0,momx,momy,momz,0,SXF_TRANSFERTRANSLATION|SXF_ABSOLUTEMOMENTUM,0)
stop

Death:
MINB A 0 A_SpawnItemEx("BlastLauncherExplosion")
stop
}
}

actor BlastLauncher2 : BlastLauncher
{
-USEBOUNCESTATE
-NOGRAVITY
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
TNT1 A 0 A_PlaySoundEX("weapon/BombAlarm","Soundslot6")
TNT1 A 1 A_Jump(256,"Spawn2")
Goto Spawn2

Spawn2:
BOMB S 1 A_CountDown
BOMB S 0 A_CheckFloor("SlowDown")
loop
SlowDown:
BOMB S 0 A_ScaleVelocity(0.3)
Goto Spawn2
	
Death:
MINB A 0 A_SpawnItemEx("BlastLauncherExplosion",0,0,0,0,0,0,0,1)
stop
}
}

/*
actor BlastLauncherExplosion : BasicExplosion
{
Scale 5.5
States
{
Spawn:
NAPC A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC D 0 A_Explode(50, 216, 1,0,108)
NAPC D 0 A_Explode(50, 216, 1,0,216)
NAPC A 0 A_PlaySoundEx("weapon/HyperBomb", "Weapon")
MXP2 ABCDEFGHIJKLMNOPQRST 1 
stop
}
}

actor BlastLauncherFakeExplosion : BasicExplosion
{
Scale 5.5
States
{
Spawn:
NAPC A 0 
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
NAPC A 0 A_PlaySoundEx("weapon/HyperBomb", "Weapon")
MXP2 ABCDEFGHIJKLMNOPQRST 1
stop
}
}
*/

actor BlastLauncherExplosion : BasicExplosion
{
damagetype "SelfDestruct"
Obituary "$OB_BLASTLAUNCHER"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
//TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H0",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H1",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H2",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H3",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H4",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H5",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX_H6",0,0,0,0,0,0,0,1)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,2,0,0,17,0,33)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,-2,0,0,-17,0,33)
//110
NAPC D 0 A_Explode(50, 216, 1,0,108)
NAPC D 0 A_Explode(50, 216, 1,0,216)
TNT1 A 0 A_Playsoundex("weapon/HyperBomb","Soundslot5")
TNT1 A 17 A_Quake(6,17,0,300)
stop
}
}

actor SelfDestructBlastFX_H0 : BasicClientSide
{
renderstyle none
Reactiontime 12//Sections
Args 20,6
//Speed/lines
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetArg(3,Args[1]+1)
TNT1 A 0 A_SetArg(4,Reactiontime)
SpawnL:
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Cos(90-180/Args[3]*Args[1])*Args[0],0,Sin(90-180/Args[3]*Args[1])*Args[0],Cos(90-180/Args[3]*Args[1])*Args[0],0,Sin(90-180/Args[3]*Args[1])*Args[0],360/Args[4]*Reactiontime,33)
TNT1 A 0 A_SetArg(1,Args[1]-1)
TNT1 A 0 A_JumpIf(Args[1]<=0,1)
loop
TNT1 A 0 A_Countdown
TNT1 A 0 A_SetArg(1,Args[3]-1)
loop
Death:
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,Args[0],0,0,Args[0],0,33)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",0,0,-Args[0],0,0,-Args[0],0,33)
TNT1 A 1
stop
}
}

actor SelfDestructBlastFX_H1 : BasicClientSide
{
var int user_RT;
var int user_A;

Mass 100
MeleeRange 0
accuracy 6
renderstyle none
reactiontime 15//18
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_RT",360/reactiontime)
TNT1 A 0 A_SetUserVar("user_A",random(0,user_RT))
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Mass/accuracy,0,0,Mass/accuracy,0,MeleeRange/accuracy,user_A,37)
TNT1 A 0 A_SetUserVar("user_A",user_A+user_RT)
TNT1 A 0 A_Countdown
goto Spawn+3
Death:
TNT1 A 1
stop
}
}
actor SelfDestructBlastFX_H2 : SelfDestructBlastFX_H1
{
Mass 96
MeleeRange 26
reactiontime 12//15
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetUserVar("user_RT",360/reactiontime)
TNT1 A 0 A_SetUserVar("user_A",random(0,user_RT))
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Mass/accuracy,0,MeleeRange/accuracy,Mass/accuracy,0,MeleeRange/accuracy,user_A,37)
TNT1 A 0 A_SpawnItemEx("SelfDestructBlastFX",Mass/accuracy,0,MeleeRange/-accuracy,Mass/accuracy,0,MeleeRange/-accuracy,user_A,37)
TNT1 A 0 A_SetUserVar("user_A",user_A+user_RT)
TNT1 A 0 A_Countdown
goto Spawn+3
Death:
TNT1 A 1
stop
}
}

actor SelfDestructBlastFX_H3 : SelfDestructBlastFX_H2
{
Mass 87
MeleeRange 50
reactiontime 10//12
}
actor SelfDestructBlastFX_H4 : SelfDestructBlastFX_H2
{
Mass 70
MeleeRange 70
reactiontime 8//9
}
actor SelfDestructBlastFX_H5 : SelfDestructBlastFX_H2
{
Mass 50
MeleeRange 87
reactiontime 6
}
actor SelfDestructBlastFX_H6 : SelfDestructBlastFX_H2
{
Mass 26
MeleeRange 96
reactiontime 4//3
}

actor SelfDestructBlastFX : BasicClientSide
{
+FORCEXYBILLBOARD
+BRIGHT
renderstyle "Translucent"
States
{

Spawn:
TNT1 A 0
TNT1 A 0 A_GiveInventory("ProjectileCode",1)
HARD DCB 1
ASEX ABBCD 1
ASEX EEFFF 1 A_Fadeout
stop
}
}

//[+]========================================================================[+]
//--------------------------------|Effect Actors|------------------------------
//[+]========================================================================[+]
actor BlastLauncherTrail : BasicClientside
{
scale 1.5
States
{
Spawn:
TNT1 A 0
TNT1 A 2
MINB IIIIII 1
MINB IIIIII 1 A_Fadeout
stop
}
}

//[+]========================================================================[+]
//------------------------------|Cooldown Actors|------------------------------
//[+]========================================================================[+]
actor BlastLauncherCooler : WatcherBase 
{
States
{
Spawn:
TNT1 A 0 A_JumpIfInTargetInventory("isDead",1,"DeathTake")
TNT1 A 1
TNT1 A 0 A_TakeFromTarget("BlastLauncherCooldownItem",1)
TNT1 A 0 A_JumpIfInTargetInventory("BlastLauncherCooldownItem",1,"Spawn")
Death:
DeathTake:
TNT1 A 0 A_TakeFromTarget("AbilityCooldownFlag",99)
TNT1 A 0
stop
}
}

actor BlastLauncherCooldown : CustomInventory
{
inventory.amount 1
Inventory.MaxAmount 100
+AUTOACTIVATE
States
{
Spawn:
TNT1 A 0
stop
Pickup:
TNT1 A 0 A_Giveinventory("BlastLauncherCooldownItem",280)
TNT1 A 0 A_Giveinventory("AbilityCooldownFlag")
TNT1 A 0 A_Spawnitemex("BlastLauncherCooler")
stop
Nope:
//BUST B 0 A_PlaySoundEx("weapon/heat3","Item")
TNT1 A 0
stop
}
}

actor BlastLauncherCooldownItem : inventory
{
inventory.amount 1
inventory.maxamount 280
}